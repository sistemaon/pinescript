// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© AugustoErni

//@version=5
indicator("Tweezer Top/Bottom Pattern (Candlestick Pattern)", overlay=true)

import AugustoErni/uptrend_downtrend_loopback_candle_identification_lib/3 as upAndDownTrendsCandleIdentification

showTweezerTop = input.bool(title="Show Tweezer Top", defval=true)
showTweezerBottom = input.bool(title="Show Tweezer Bottom", defval=true)
lookbackPeriod = input.int(defval=5, title="Lookback Period", minval=1, step=1, tooltip="Loopback period to identify uptrend/downtrend analysis from candles.")
dojiThreshold = input.float(0.1, title="Doji Threshold (0.1 = 10%)", step=0.01, tooltip="To identify doji candle.")
weakCandleThreshold = input.float(0.30, title="Weak Candle Threshold (0.30 = 30%)", step=0.01, tooltip="To identify possible weak and/or small candle.")

isDoji = math.abs(close - open) <= (high - low) * dojiThreshold
isWeakCandle = math.abs(close - open) <= (high - low) * weakCandleThreshold

isLookbackUptrendCandle = upAndDownTrendsCandleIdentification.uptrendLoopbackCandleIdentification(lookbackPeriod)
isLookbackDowntrendCandle = upAndDownTrendsCandleIdentification.downtrendLoopbackCandleIdentification(lookbackPeriod)

isBullishCandle = close > open
isBearishCandle = open > close

tweezerTopConditionCandle = math.floor(high[1]) == math.floor(high)
twezzerBottomConditionCandle = math.floor(low[1]) == math.floor(low)
firstConditionCandle = not isDoji and not isWeakCandle and not isDoji[1] and not isWeakCandle[1]

isTwezzerTop = firstConditionCandle and tweezerTopConditionCandle and isLookbackUptrendCandle
isTwezzerBottom = firstConditionCandle and twezzerBottomConditionCandle and isLookbackDowntrendCandle

barcolor(isTwezzerTop and isBullishCandle ? color.white : isTwezzerTop and isBearishCandle ? color.black : na)
barcolor(isTwezzerTop and isBullishCandle[1] ? color.rgb(213, 213, 213) : isTwezzerTop and isBearishCandle[1] ? color.rgb(90, 90, 90) : na, offset=-1)

barcolor(isTwezzerBottom and isBullishCandle ? color.white : isTwezzerBottom and isBearishCandle ? color.black : na)
barcolor(isTwezzerBottom and isBullishCandle[1] ? color.rgb(213, 213, 213) : isTwezzerBottom and isBearishCandle[1] ? color.rgb(90, 90, 90) : na, offset=-1)

plotshape(isTwezzerTop and showTweezerTop, title="Twezzer Top", style=shape.arrowdown, location=location.abovebar, color=color.rgb(255, 255, 255), text="Twezzer Top", textcolor=color.red)
plotshape(isTwezzerBottom and showTweezerBottom, title="Twezzer Bottom", style=shape.arrowup, location=location.belowbar, color=color.rgb(0, 0, 0), text="Twezzer Bottom", textcolor=color.green)