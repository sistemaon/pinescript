
# Plots (_Plotagem_)

A funÃ§Ã£o [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) Ã© a mais frequentemente usada para exibir informaÃ§Ãµes calculadas usando scripts Pine. Ela Ã© versÃ¡til e pode plotar diferentes estilos de linhas, histogramas, Ã¡reas, colunas (como colunas de volume), preenchimentos, cÃ­rculos ou cruzes.

O uso de [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) para criar preenchimentos Ã© explicado na pÃ¡gina sobre [Fills](./05_08_fills.md).

Este script demonstra alguns usos diferentes de [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) em um script de sobreposiÃ§Ã£o:

![Plots 01](./imgs/Plots-Introduction-01.Ctk4DlIO_24M1qs.webp)

```c
//@version=5
indicator("`plot()`", "", true)
plot(high, "Blue `high` line")
plot(math.avg(close, open), "Crosses in body center", close > open ? color.lime : color.purple, 6, plot.style_cross)
plot(math.min(open, close), "Navy step line on body low point", color.navy, 3, plot.style_stepline)
plot(low, "Gray dot on `low`", color.gray, 3, plot.style_circles)

color VIOLET = #AA00FF
color GOLD   = #CCCC00
ma = ta.alma(hl2, 40, 0.85, 6)
var almaColor = color.silver
almaColor := ma > ma[2] ? GOLD : ma < ma[2]  ? VIOLET : almaColor
plot(ma, "Two-color ALMA", almaColor, 2)
```

__Note que:__

- A primeira chamada de [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) plota uma linha azul de 1 pixel ao longo das mÃ¡ximas das barras.
- A segunda plota cruzes no ponto mÃ©dio dos corpos. As cruzes sÃ£o coloridas de verde-limÃ£o quando a barra estÃ¡ em alta e roxas quando estÃ¡ em baixa. O argumento usado para `linewidth` Ã© `6`, mas nÃ£o Ã© um valor de pixel; apenas um tamanho relativo.
- A terceira chamada plota uma linha escalonada de 3 pixels de largura seguindo o ponto baixo dos corpos.
- A quarta chamada plota um cÃ­rculo cinza na [mÃ­nima](https://br.tradingview.com/pine-script-reference/v5/#var_low) das barras.
- A Ãºltima plotagem requer alguma preparaÃ§Ã£o. Primeiro, define as cores de alta/baixa, calcula uma [MÃ©dia MÃ³vel Arnaud Legoux](https://br.tradingview.com/support/solutions/43000594683), depois realiza cÃ¡lculos de cor. Inicializa a variÃ¡vel de cor na barra zero apenas, usando [var](https://br.tradingview.com/pine-script-reference/v5/#kw_var). Inicializando com [color.silver](https://br.tradingview.com/pine-script-reference/v5/#const_color%7Bdot%7Dsilver), entÃ£o, nas primeiras barras do conjunto de dados, atÃ© que uma das condiÃ§Ãµes cause a mudanÃ§a de cor, a linha serÃ¡ prata. As condiÃ§Ãµes que mudam a cor da linha exigem que ela esteja mais alta/baixa do que seu valor de duas barras atrÃ¡s. Isso faz com que as transiÃ§Ãµes de cor sejam menos ruidosas do que se olhÃ¡sse apenas para um valor mais alto/baixo do que o anterior.

Este script mostra outros usos de [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) em um painel:

![Plots 02](./imgs/Plots-Introduction-02.CEeiodqC_Z1WHdqJ.webp)

```c
//@version=5
indicator("Volume change", format = format.volume)

color GREEN         = #008000
color GREEN_LIGHT   = color.new(GREEN, 50)
color GREEN_LIGHTER = color.new(GREEN, 85)
color PINK          = #FF0080
color PINK_LIGHT    = color.new(PINK, 50)
color PINK_LIGHTER  = color.new(PINK, 90)

bool  barUp = ta.rising(close, 1)
bool  barDn = ta.falling(close, 1)
float volumeChange = ta.change(volume)

volumeColor = barUp ? GREEN_LIGHTER : barDn ? PINK_LIGHTER : color.gray
plot(volume, "Volume columns", volumeColor, style = plot.style_columns)

volumeChangeColor = barUp ? volumeChange > 0 ? GREEN : GREEN_LIGHT : volumeChange > 0 ? PINK : PINK_LIGHT
plot(volumeChange, "Volume change columns", volumeChangeColor, 12, plot.style_histogram)

plot(0, "Zero line", color.gray)
```

__Note que:__

- Ã‰ plotado valores normais de [volume](https://br.tradingview.com/pine-script-reference/v5/#var_volume) como colunas largas acima da linha zero (veja o `style = plot.style_columns` na nossa chamada de [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot)).
- Antes de plotar as colunas, calcula o `volumeColor` usando os valores das variÃ¡veis booleanas `barUp` e `barDn`. Elas se tornam respectivamente `true` quando o [fechamento](https://br.tradingview.com/pine-script-reference/v5/#var_close) da barra atual Ã© maior/menor que o anterior. Note que o "Volume" nÃ£o usa a mesma condiÃ§Ã£o; ele identifica uma barra de alta com `close > open`. As cores `GREEN_LIGHTER` e `PINK_LIGHTER` sÃ£o usadas para as colunas de volume.
- Porque a primeira plotagem plota colunas, nÃ£o utiliza o parÃ¢metro `linewidth`, pois ele nÃ£o tem efeito sobre colunas.
- A segunda plotagem do script Ã© a __mudanÃ§a__ no volume, que foi calculado anteriormente usando `ta.change(volume)`. Este valor Ã© plotado como um histograma, onde o parÃ¢metro `linewidth` controla a largura da coluna. Foi definido essa largura como `12` para que os elementos do histograma sejam mais finos que as colunas da primeira plotagem. Valores positivos/negativos de `volumeChange` sÃ£o plotados acima/abaixo da linha zero; nenhuma manipulaÃ§Ã£o Ã© necessÃ¡ria para obter este efeito.
- Antes de plotar o histograma de valores de `volumeChange`, calcula seu valor de cor, que pode ser uma de quatro cores diferentes. Usa-se as cores brilhantes `GREEN` ou `PINK` quando a barra estÃ¡ em alta/baixa E o volume aumentou desde a Ãºltima barra (`volumeChange > 0`). Como `volumeChange` Ã© positivo nesse caso, o elemento do histograma serÃ¡ plotado acima da linha zero. Ã‰ usado as cores `GREEN_LIGHT` ou `PINK_LIGHT` quando a barra estÃ¡ em alta/baixa E o volume NÃƒO aumentou desde a Ãºltima barra. Como `volumeChange` Ã© negativo nesse caso, o elemento do histograma serÃ¡ plotado abaixo da linha zero.
- Finalmente, plotado uma linha zero. Poderia ter usado `hline(0)` ali.
- Usa `format = format.volume` na chamada de [indicator()](https://br.tradingview.com/pine-script-reference/v5/#fun_indicator) para que grandes valores exibidos por este script sejam abreviados como os do indicador incorporado "Volume".

Chamadas de [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) devem sempre ser colocadas na primeira posiÃ§Ã£o de uma linha, o que implica que estÃ£o sempre no escopo global do script. Elas nÃ£o podem ser colocadas em funÃ§Ãµes definidas pelo usuÃ¡rio ou estruturas como [if](https://br.tradingview.com/pine-script-reference/v5/#kw_if), [for](https://br.tradingview.com/pine-script-reference/v5/#kw_for), etc. No entanto, chamadas para [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) __podem__ ser projetadas para plotar condicionalmente de duas maneiras, abordado na seÃ§Ã£o [plots condicionais](./05_15_plots.md#plotagem-condicional) desta pÃ¡gina.

Um script sÃ³ pode plotar no seu prÃ³prio espaÃ§o visual, seja em um painel ou no grÃ¡fico como uma sobreposiÃ§Ã£o. Scripts que rodam em um painel podem apenas [colorir barras](./05_03_coloracao_de_barras.md) na Ã¡rea do grÃ¡fico.

## ParÃ¢metros de `plot()`

A funÃ§Ã£o [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) tem a seguinte assinatura:

```c
plot(series, title, color, linewidth, style, trackprice, histbase, offset, join, editable, show_last, display, force_overlay) â†’ plot
```

Os parÃ¢metros de [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) sÃ£o:

`series`

Ã‰ o Ãºnico parÃ¢metro obrigatÃ³rio. Seu argumento deve ser do tipo "series int/float". Note que, devido Ã s regras de auto-conversÃ£o no Pine Script converterem na direÃ§Ã£o int ðŸ † float ðŸ † bool, uma variÃ¡vel do tipo "bool" nÃ£o pode ser usada como estÃ¡; deve ser convertida para "int" ou "float" para ser usada como argumento. Por exemplo, se `newDay` Ã© do tipo "bool", entÃ£o `newDay ? 1 : 0` pode ser usado para plotar 1 quando a variÃ¡vel Ã© `true`, e zero quando Ã© `false`.

`title`

Requer um argumento do tipo "const string", entÃ£o deve ser conhecido no tempo de compilaÃ§Ã£o. A string aparece:

- Na escala do script quando o campo "ConfiguraÃ§Ãµes do grÃ¡fico/Escalas/RÃ³tulo do Nome do Indicador" ("_Chart settings/Scales/Indicator Name Label_") estÃ¡ marcado.
- Na Janela de Dados ("_Data Window_").
- Na aba "ConfiguraÃ§Ãµes/Estilo" ("_Settings/Style_").
- No dropdown de campos [input.source()](https://br.tradingview.com/pine-script-reference/v5/#fun_input%7Bdot%7Dsource).
- No campo "CondiÃ§Ã£o" ("_Condition_") da caixa de diÃ¡logo "Criar Alerta" ("_Create Alert_"), quando o script estÃ¡ selecionado.
- Como cabeÃ§alho de coluna ao exportar dados do grÃ¡fico para um arquivo CSV.

`color`

Aceita "series color", entÃ£o pode ser calculado dinamicamente, barra a barra. Plotar com [na](https://br.tradingview.com/pine-script-reference/v5/#var_na) como a cor, ou qualquer cor com uma transparÃªncia de 100, Ã© uma maneira de esconder plotagens quando nÃ£o sÃ£o necessÃ¡rias.

`linewidth`

Refere-se ao tamanho do elemento plotado, mas nÃ£o se aplica a todos os estilos. Quando uma linha Ã© plotada, a unidade Ã© pixels. NÃ£o tem impacto quando [plot.style_columns](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_columns) Ã© utilizado.

`style`

Os argumentos disponÃ­veis sÃ£o:

- [plot.style_line](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_line) (padrÃ£o): Plota uma linha contÃ­nua usando o argumento `linewidth` em pixels para sua largura. Valores [na](https://br.tradingview.com/pine-script-reference/v5/#var_na) nÃ£o serÃ£o plotados como linha, mas serÃ£o conectados quando um valor que nÃ£o Ã© [na](https://br.tradingview.com/pine-script-reference/v5/#var_na) aparecer. Valores nÃ£o-[na](https://br.tradingview.com/pine-script-reference/v5/#var_na) sÃ£o conectados apenas se estiverem visÃ­veis no grÃ¡fico.
- [plot.style_linebr](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_linebr): Permite a plotagem de linhas descontÃ­nuas, nÃ£o plotando em valores [na](https://br.tradingview.com/pine-script-reference/v5/#var_na) e nÃ£o conectando lacunas, ou seja, nÃ£o criando pontes sobre valores [na](https://br.tradingview.com/pine-script-reference/v5/#var_na).
- [plot.style_stepline](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_stepline): Plota usando um efeito de escada. As transiÃ§Ãµes entre mudanÃ§as de valores sÃ£o feitas usando uma linha vertical desenhada no meio das barras, em vez de uma linha diagonal ponto a ponto conectando os pontos mÃ©dios das barras. TambÃ©m pode ser usado para alcanÃ§ar um efeito semelhante ao de [plot.style_linebr](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_linebr), mas apenas se o cuidado for tomado para nÃ£o plotar cor em valores [na](https://br.tradingview.com/pine-script-reference/v5/#var_na).
- [plot.style_area](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_area): Plota uma linha de largura `linewidth`, preenchendo a Ã¡rea entre a linha e o `histbase`. O argumento `color` Ã© usado tanto para a linha quanto para o preenchimento. Pode-se fazer a linha de uma cor diferente usando outra chamada de [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot). Valores positivos sÃ£o plotados acima do `histbase`, valores negativos abaixo dele.
- [plot.style_areabr](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_areabr): Semelhante a [plot.style_area](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_area), mas nÃ£o cria pontes sobre valores [na](https://br.tradingview.com/pine-script-reference/v5/#var_na). Outra diferenÃ§a Ã© como a escala do indicador Ã© calculada. Apenas os valores plotados sÃ£o usados no cÃ¡lculo da escala _y_ do espaÃ§o visual do script. Se apenas valores altos situados longe do `histbase` forem plotados, por exemplo, esses valores serÃ£o usados para calcular a escala _y_ do espaÃ§o visual do script. Valores positivos sÃ£o plotados acima do `histbase`, valores negativos abaixo dele.
- [plot.style_columns](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_columns): Plota colunas semelhantes Ã s do indicador incorporado "Volume". O valor de `linewidth` __nÃ£o__ afeta a largura das colunas. Valores positivos sÃ£o plotados acima do `histbase`, valores negativos abaixo dele. Sempre inclui o valor de `histbase` na escala _y_ do espaÃ§o visual do script.
- [plot.style_histogram](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_histogram): Plota colunas semelhantes Ã s do indicador incorporado "Volume", exceto que o valor de `linewidth` Ã© usado para determinar a largura das barras do histograma em pixels. Note que, como `linewidth` requer um valor "input int", a largura das barras do histograma nÃ£o pode variar de barra para barra. Valores positivos sÃ£o plotados acima do `histbase`, valores negativos abaixo dele. Sempre inclui o valor de `histbase` na escala _y_ do espaÃ§o visual do script.
- [plot.style_circles](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_circles) e [plot.style_cross](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_cross): Plotam uma forma que nÃ£o Ã© conectada entre as barras, a menos que `join = true` tambÃ©m seja usado. Para esses estilos, o argumento `linewidth` torna-se uma medida de tamanho relativa â€” suas unidades nÃ£o sÃ£o pixels.

`trackprice`

O valor padrÃ£o Ã© `false`. Quando `true`, uma linha pontilhada composta de pequenos quadrados serÃ¡ plotada em toda a largura do espaÃ§o visual do script. Frequentemente usado em conjunto com `show_last = 1, offset = -99999` para ocultar a plotagem real e deixar apenas a linha pontilhada residual.

`histbase`

Ã‰ o ponto de referÃªncia usado com [plot.style_area](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_area), [plot.style_columns](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_columns) e [plot.style_histogram](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_histogram). Determina o nÃ­vel que separa valores positivos e negativos do argumento `series`. NÃ£o pode ser calculado dinamicamente, pois requer um valor "input int/float".

`offset`

Permite deslocar a plotagem no passado/futuro usando um deslocamento negativo/positivo em barras. O valor nÃ£o pode mudar durante a execuÃ§Ã£o do script.

`join`

Afeta apenas os estilos [plot.style_circles](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_circles) ou [plot.style_cross](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_cross). Quando `true`, as formas sÃ£o conectadas por uma linha de um pixel.

`editable`

Este parÃ¢metro booleano controla se as propriedades da plotagem podem ser editadas na aba "ConfiguraÃ§Ãµes/Estilo" ("_Settings/Style_"). O valor padrÃ£o Ã© `true`.

`show_last`

Permite controlar quantas das Ãºltimas barras os valores plotados sÃ£o visÃ­veis. Um argumento "input int" Ã© necessÃ¡rio, entÃ£o nÃ£o pode ser calculado dinamicamente.

`display`

O padrÃ£o Ã© [display.all](https://br.tradingview.com/pine-script-reference/v5/#const_display%7Bdot%7Dall). Quando configurado para [display.none](https://br.tradingview.com/pine-script-reference/v5/#const_display%7Bdot%7Dnone), os valores plotados nÃ£o afetarÃ£o a escala do espaÃ§o visual do script. A plotagem serÃ¡ invisÃ­vel e nÃ£o aparecerÃ¡ nos valores do indicador ou na Janela de Dados. Pode ser Ãºtil em plotagens destinadas a serem usadas como entradas externas para outros scripts, ou para plotagens usadas com o espaÃ§o reservado `{{plot("[plot_title]")}}` em chamadas de [alertcondition()](https://br.tradingview.com/pine-script-reference/v5/#fun_alertcondition), por exemplo:

```c
//@version=5
indicator("")
r = ta.rsi(close, 14)
xUp = ta.crossover(r, 50)
plot(r, "RSI", display = display.none)
alertcondition(xUp, "xUp alert", message = 'RSI is bullish at: {{plot("RSI")}}')
```

`force_overlay`

Se `true`, os resultados plotados serÃ£o exibidos no painel principal do grÃ¡fico, mesmo quando o script ocupar um painel separado. Opcional. O valor padrÃ£o Ã© `false`.

## Plotagem Condicional

Chamadas de [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) nÃ£o podem ser usadas em estruturas condicionais como [if](https://br.tradingview.com/pine-script-reference/v5/#kw_if), mas podem ser controladas variando seus valores plotados ou suas cores. Quando nenhuma plotagem Ã© necessÃ¡ria, Ã© possÃ­vel plotar valores [na](https://br.tradingview.com/pine-script-reference/v5/#var_na) ou usar a cor [na](https://br.tradingview.com/pine-script-reference/v5/#var_na) ou qualquer cor com 100 de transparÃªncia (o que tambÃ©m a torna invisÃ­vel).

### Controle de Valor

Uma maneira de controlar a exibiÃ§Ã£o das plotagens Ã© plotar valores [na](https://br.tradingview.com/pine-script-reference/v5/#var_na) quando nenhuma plotagem Ã© necessÃ¡ria. Ã€s vezes, valores retornados por funÃ§Ãµes como [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request%7Bdot%7Dsecurity) retornarÃ£o valores [na](https://br.tradingview.com/pine-script-reference/v5/#var_na), quando `gaps = barmerge.gaps_on` Ã© usado, por exemplo. Em ambos os casos, Ã s vezes Ã© Ãºtil plotar linhas descontÃ­nuas. Este script mostra algumas maneiras de fazer isso:

![controle de valor](./imgs/Plots-PlottingConditionally-01.DCGRnCbX_1NjoOk.webp)

```c
//@version=5
indicator("Discontinuous plots", "", true)
bool plotValues = bar_index % 3 == 0
plot(plotValues ? high : na, color = color.fuchsia, linewidth = 6, style = plot.style_linebr)
plot(plotValues ? high : na)
plot(plotValues ? math.max(open, close) : na, color = color.navy, linewidth = 6, style = plot.style_cross)
plot(plotValues ? math.min(open, close) : na, color = color.navy, linewidth = 6, style = plot.style_circles)
plot(plotValues ? low : na, color = plotValues ? color.green : na, linewidth = 6, style = plot.style_stepline)
```

__Note que:__

- A condiÃ§Ã£o que determina quando plotar Ã© definida usando `bar_index % 3 == 0`, que se torna `true` quando o restante da divisÃ£o do Ã­ndice da barra por 3 Ã© zero. Isso acontecerÃ¡ a cada trÃªs barras.
- Na primeira plotagem, Ã© usado [plot.style_linebr](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_linebr), que plota a linha fÃºcsia nos mÃ¡ximos. EstÃ¡ centrado no ponto mÃ©dio horizontal da barra.
- A segunda plotagem mostra o resultado de plotar os mesmos valores, mas sem usar cuidado especial para quebrar a linha. O que acontece aqui Ã© que a linha azul fina da chamada simples de [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) Ã© automaticamente conectada sobre valores [na](https://br.tradingview.com/pine-script-reference/v5/#var_na) (ou _gaps_), entÃ£o a plotagem nÃ£o Ã© interrompida.
- Em seguida, sÃ£o plotados cruzes e cÃ­rculos azul-marinho nos topos e fundos dos corpos. Os estilos [plot.style_circles](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_circles) e [plot.style_cross](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_cross) sÃ£o uma maneira simples de plotar valores descontÃ­nuos, por exemplo, para nÃ­veis de stop ou take profit, ou nÃ­veis de suporte e resistÃªncia.
- A Ãºltima plotagem em verde nos mÃ­nimos das barras Ã© feita usando [plot.style_stepline](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_stepline). Note como seus segmentos sÃ£o mais largos que os segmentos da linha fÃºcsia plotados com [plot.style_linebr](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_linebr). TambÃ©m observe como na Ãºltima barra, ele plota apenas atÃ© a metade, atÃ© que a prÃ³xima barra entre.
- A ordem de plotagem de cada plotagem Ã© controlada por sua ordem de apariÃ§Ã£o no script.

Este script mostra como restringir a plotagem para barras apÃ³s uma data definida pelo usuÃ¡rio. A funÃ§Ã£o [input.time()](https://br.tradingview.com/pine-script-reference/v5/#fun_input%7Bdot%7Dtime) Ã© usada para criar um widget de entrada permitindo que os usuÃ¡rios do script selecionem uma data e hora, usando 1Âº de janeiro de 2021 como seu valor padrÃ£o:

```c
//@version=5
indicator("", "", true)
startInput = input.time(timestamp("2021-01-01"))
plot(time > startInput ? close : na)
```

### Controle de Cor

A seÃ§Ã£o [ColoraÃ§Ã£o Condicional](./05_07_cores.md#coloraÃ§Ã£o-condicional) da pÃ¡gina sobre cores discute o controle de cor para plotagens. Veja alguns exemplos.

O valor do parÃ¢metro `color` na [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) pode ser uma constante, como uma das [cores constantes](./05_07_cores.md#constante-de-cores) incorporadas ou um [literal de cor](./04_09_tipagem_do_sistema.md#color). No Pine Script, o tipo qualificado de tais cores Ã© chamado de __"const color"__ (veja a pÃ¡gina do [Sistema de Tipos](./04_09_tipagem_do_sistema.md)). Elas sÃ£o conhecidas em tempo de compilaÃ§Ã£o:

```c
//@version=5
indicator("", "", true)
plot(close, color = color.gray)
```

A cor de uma plotagem tambÃ©m pode ser determinada usando informaÃ§Ãµes que sÃ£o conhecidas apenas quando o script comeÃ§a a execuÃ§Ã£o na primeira barra histÃ³rica de um grÃ¡fico (barra zero, ou seja, `bar_index == 0` ou `barstate.isfirst == true`), como serÃ¡ o caso quando as informaÃ§Ãµes necessÃ¡rias para determinar uma cor dependem do grÃ¡fico em que o script estÃ¡ sendo executado. Aqui, calcula-se uma cor de plotagem usando a variÃ¡vel incorporada [syminfo.type](https://br.tradingview.com/pine-script-reference/v5/#var_syminfo%7Bdot%7Dtype), que retorna o tipo do sÃ­mbolo do grÃ¡fico. O tipo qualificado de `plotColor` neste caso serÃ¡ __"simple color"__:

```c
//@version=5
indicator("", "", true)
plotColor = switch syminfo.type
    "stock"     => color.purple
    "futures"   => color.red
    "index"     => color.gray
    "forex"     => color.fuchsia
    "crypto"    => color.lime
    "fund"      => color.orange
    "dr"        => color.aqua
    "cfd"       => color.blue
plot(close, color = plotColor)
printTable(txt) => var table t = table.new(position.middle_right, 1, 1), table.cell(t, 0, 0, txt, bgcolor = color.yellow)
printTable(syminfo.type)
```

As cores das plotagens tambÃ©m podem ser escolhidas atravÃ©s das entradas de um script. Neste caso, a variÃ¡vel `lineColorInput` Ã© do tipo __"input color"__:

```c
//@version=5
indicator("", "", true)
color lineColorInput = input(#1848CC, "Line color")
plot(close, color = lineColorInput)
```

Finalmente, as cores das plotagens tambÃ©m podem ser valores _dinÃ¢micos_, ou seja, valores calculados que podem mudar a cada barra. Esses valores sÃ£o do tipo __"series color"__:

```c
//@version=5
indicator("", "", true)
plotColor = close >= open ? color.lime : color.red
plot(close, color = plotColor)
```

Ao plotar nÃ­veis de pivÃ´, um requisito comum Ã© evitar a plotagem de transiÃ§Ãµes de nÃ­veis. Usar [linhas](./05_12_lines_e_boxes.md) Ã© uma alternativa, mas vocÃª tambÃ©m pode usar [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) desta maneira:

![Controle de cor](./imgs/Plots-PlottingConditionally-02.Dgz5RTVC_Z2npVgz.webp)

```c
//@version=5
indicator("Pivot plots", "", true)
pivotHigh = fixnan(ta.pivothigh(3,3))
plot(pivotHigh, "High pivot", ta.change(pivotHigh) ? na : color.olive, 3)
plotchar(ta.change(pivotHigh), "ta.change(pivotHigh)", "â€¢", location.top, size = size.small)
```

__Note que:__

- Ã‰ usado `pivotHigh = fixnan(ta.pivothigh(3,3))` para manter os valores de pivÃ´. Como [ta.pivothigh()](https://br.tradingview.com/pine-script-reference/v5/#fun_ta%7Bdot%7Dpivothigh) sÃ³ retorna um valor quando um novo pivÃ´ Ã© encontrado, utiliza [fixnan()](https://br.tradingview.com/pine-script-reference/v5/#fun_fixnan) para preencher as lacunas com o Ãºltimo valor de pivÃ´ retornado. As lacunas aqui se referem aos valores [na](https://br.tradingview.com/pine-script-reference/v5/#var_na) que [ta.pivothigh()](https://br.tradingview.com/pine-script-reference/v5/#fun_ta%7Bdot%7Dpivothigh) retorna quando nenhum novo pivÃ´ Ã© encontrado.
- Os pivÃ´s sÃ£o detectados trÃªs barras apÃ³s ocorrerem pois usam o argumento `3` para ambos os parÃ¢metros `leftbars` e `rightbars` na chamada [ta.pivothigh()](https://br.tradingview.com/pine-script-reference/v5/#fun_ta%7Bdot%7Dpivothigh).
- A Ãºltima plotagem estÃ¡ plotando um valor contÃ­nuo, mas estÃ¡ definindo a cor da plotagem para [na](https://br.tradingview.com/pine-script-reference/v5/#var_na) quando o valor do pivÃ´ muda, entÃ£o a plotagem nÃ£o Ã© visÃ­vel entÃ£o. Por causa disso, uma plotagem visÃ­vel sÃ³ aparecerÃ¡ na barra seguinte Ã quela em que plotou usando a cor [na](https://br.tradingview.com/pine-script-reference/v5/#var_na).
- O ponto azul indica quando um novo pivÃ´ alto Ã© detectado e nenhuma plotagem Ã© desenhada entre a barra anterior e aquela. Note como o pivÃ´ na barra indicada pela seta acabou de ser detectado na barra em tempo real, trÃªs barras depois, e como nenhuma plotagem Ã© desenhada. A plotagem sÃ³ aparecerÃ¡ na prÃ³xima barra, tornando a plotagem visÃ­vel __quatro barras__ apÃ³s o pivÃ´ real.

## Levels (_NÃ­veis_)

O Pine Script tem uma funÃ§Ã£o [hline()](https://br.tradingview.com/pine-script-reference/v5/#fun_hline) para plotar linhas horizontais (veja a pÃ¡gina sobre [NÃ­veis](./05_10_levels.md)). A [hline()](https://br.tradingview.com/pine-script-reference/v5/#fun_hline) Ã© Ãºtil porque tem alguns estilos de linha indisponÃ­veis com [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot), mas tambÃ©m tem algumas limitaÃ§Ãµes, nomeadamente que nÃ£o aceita "series color" e que seu parÃ¢metro `price` requer um "input int/float", entÃ£o nÃ£o pode variar durante a execuÃ§Ã£o do script.

Consegue-se plotar nÃ­veis com [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) de algumas maneiras diferentes. Este mostra um indicador [CCI](https://br.tradingview.com/support/solutions/43000502001) com nÃ­veis plotados usando [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot):

![Levels](./imgs/Plots-Levels-01.jio4KIpw_Z2s6Io2.webp)

```c
//@version=5
indicator("CCI levels with `plot()`")
plot(ta.cci(close, 20))
plot(0,  "Zero", color.gray, 1, plot.style_circles)
plot(bar_index % 2 == 0 ?  100 : na,  "100", color.lime, 1, plot.style_linebr)
plot(bar_index % 2 == 0 ? -100 : na, "-100", color.fuchsia, 1, plot.style_linebr)
plot( 200,  "200", color.green, 2, trackprice = true, show_last = 1, offset = -99999)
plot(-200, "-200", color.red,   2, trackprice = true, show_last = 1, offset = -99999)
plot( 300,  "300", color.new(color.green, 50), 1)
plot(-300, "-300", color.new(color.red, 50),   1)
```

__Note que:__

- O nÃ­vel zero Ã© plotado usando [plot.style_circles](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_circles).
- Os nÃ­veis 100 sÃ£o plotados usando um valor condicional que sÃ³ plota a cada segunda barra. Para evitar que os valores [na](https://br.tradingview.com/pine-script-reference/v5/#var_na) sejam conectados, o estilo de linha [plot.style_linebr](https://br.tradingview.com/pine-script-reference/v5/#const_plot%7Bdot%7Dstyle_linebr) Ã© usado.
- Os nÃ­veis 200 sÃ£o plotados usando `trackprice = true` para plotar um padrÃ£o distinto de pequenos quadrados que se estendem por toda a largura do espaÃ§o visual do script. O `show_last = 1` exibe apenas o Ãºltimo valor plotado, que apareceria como uma linha reta de uma barra se o prÃ³ximo truque nÃ£o fosse usado: o `offset = -99999` empurra esse segmento de uma barra para muito longe no passado, de modo que nunca Ã© visÃ­vel.
- Os nÃ­veis 300 sÃ£o plotados usando uma linha contÃ­nua, mas uma transparÃªncia mais leve Ã© usada para tornÃ¡-los menos proeminentes.

### Offsets

O parÃ¢metro `offset` especifica o deslocamento usado quando a linha Ã© plotada (valores negativos deslocam para o passado, valores positivos deslocam para o futuro). Por exemplo:

```c
//@version=5
indicator("", "", true)
plot(close, color = color.red, offset = -5)
plot(close, color = color.lime, offset = 5)
```

![Offsets](./imgs/Plots-Offsets-01.CBzf5b6H_1aubfP.webp)

Como pode ser visto na captura de tela, a sÃ©rie _vermelha_ foi deslocada para a esquerda (jÃ¡ que o valor do argumento Ã© negativo), enquanto a sÃ©rie _verde_ foi deslocada para a direita (seu valor Ã© positivo).

## Limite de Contagem de Plotagem

Cada script Ã© limitado a um mÃ¡ximo de 64 plotagens. Todas as chamadas de `plot*()` e [alertcondition()](https://br.tradingview.com/pine-script-reference/v5/#fun_alertcondition) contam na contagem de plotagens de um script. Alguns tipos de chamadas contam mais de uma vez no total de plotagens.

Chamadas de [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) contam como uma na contagem total de plotagens se usarem um argumento "const color" para o parÃ¢metro `color`, o que significa que Ã© conhecido em tempo de compilaÃ§Ã£o, por exemplo:

```c
plot(close, color = color.green)
```

Quando usam outro tipo qualificado, como qualquer um destes, contam como duas na contagem total de plotagens:

```c
plot(close, color = syminfo.mintick > 0.0001 ? color.green : color.red) //ðŸ † "simple color"
plot(close, color = input.color(color.purple)) //ðŸ † "input color"
plot(close, color = close > open ? color.green : color.red) //ðŸ † "series color"
plot(close, color = color.new(color.silver, close > open ? 40 : 0)) //ðŸ † "series color"
```

<!-- ## Escala

Nem todos os valores podem ser plotados em qualquer lugar. O espaÃ§o visual do seu script Ã© sempre limitado por limites superiores e inferiores que sÃ£o ajustados dinamicamente com os valores plotados. Um indicador [RSI](https://br.tradingview.com/support/solutions/43000502338) plotarÃ¡ valores entre 0 e 100, por isso geralmente Ã© exibido em um _painel_ distinto â€” ou Ã¡rea â€” acima ou abaixo do grÃ¡fico. Se os valores do RSI fossem plotados como uma sobreposiÃ§Ã£o no grÃ¡fico, o efeito seria distorcer a escala de preÃ§o normal do sÃ­mbolo, a menos que estivesse prÃ³ximo do intervalo de 0 a 100 do RSI. Este exemplo mostra uma linha de sinal RSI e uma linha central no nÃ­vel 50, com o script rodando em um painel separado:

![Escala 01](./imgs/Plots-Scale-01.CE6dQl_T_1UEEw.webp)

```c
//@version=5
indicator("RSI")
myRSI = ta.rsi(close, 20)
bullColor = color.from_gradient(myRSI, 50, 80, color.new(color.lime, 70), color.new(color.lime, 0))
bearColor = color.from_gradient(myRSI, 20, 50, color.new(color.red,   0), color.new(color.red, 70))
myRSIColor = myRSI > 50 ? bullColor : bearColor
plot(myRSI, "RSI", myRSIColor, 3)
hline(50)
```

Note que o eixo _y_ do espaÃ§o visual do script Ã© dimensionado automaticamente usando a faixa de valores plotados, ou seja, os valores do RSI. Veja a pÃ¡gina sobre [Cores](./05_07_cores.md) para mais informaÃ§Ãµes sobre a funÃ§Ã£o [color.from_gradient()](https://br.tradingview.com/pine-script-reference/v5/#fun_color%7Bdot%7Dfrom_gradient) usada no script.

Ao tentar plotar os valores de [fechamento](https://br.tradingview.com/pine-script-reference/v5/#var_close) do sÃ­mbolo no mesmo espaÃ§o adicionando a seguinte linha ao nosso script:

```c
plot(close)
```

Isto Ã© o que acontece:

![Escala 02](./imgs/Plots-Scale-02.D8wP6yEJ_Z1txJ3M.webp)

O grÃ¡fico estÃ¡ no sÃ­mbolo BTCUSD, cujos preÃ§os de [fechamento](https://br.tradingview.com/pine-script-reference/v5/#var_close) estÃ£o em torno de 40000 durante este perÃ­odo. Plotar valores na faixa de 40000 torna as plotagens de RSI na faixa de 0 a 100 indiscernÃ­veis. As mesmas plotagens distorcidas ocorreriam se colocasse o indicador [RSI](https://br.tradingview.com/support/solutions/43000502338) no grÃ¡fico como uma sobreposiÃ§Ã£o.

### Mesclando dois indicadores

Se planeja mesclar dois sinais em um script, primeiro considere a escala de cada um. Ã‰ impossÃ­vel, por exemplo, plotar corretamente um [RSI](https://br.tradingview.com/support/solutions/43000502338) e um [MACD](https://br.tradingview.com/support/solutions/43000502344) no mesmo espaÃ§o visual do script, pois o RSI tem uma faixa fixa (0 a 100), enquanto o MACD nÃ£o tem, pois plota mÃ©dias mÃ³veis calculadas sobre o preÃ§o.

Se ambos os indicadores usarem faixas fixas, pode-se deslocar os valores de um deles para que nÃ£o se sobreponham. Poderia, por exemplo, plotar tanto o [RSI](https://br.tradingview.com/support/solutions/43000502338) (0 a 100) quanto o [Indicador de ForÃ§a Verdadeira (TSI)](https://br.tradingview.com/support/solutions/43000592290) _True Strength Index_ (-100 a +100) deslocando um deles. A estratÃ©gia aqui serÃ¡ comprimir e deslocar os valores do [TSI](https://br.tradingview.com/support/solutions/43000592290) para que plotem sobre o [RSI](https://br.tradingview.com/support/solutions/43000502338):

![Mesclando dois indicadores](./imgs/Plots-Scale-03.D9mEXIxt_ZtI6jd.webp)

```c
//@version=5
indicator("RSI and TSI")
myRSI = ta.rsi(close, 20)
bullColor = color.from_gradient(myRSI, 50, 80, color.new(color.lime, 70), color.new(color.lime, 0))
bearColor = color.from_gradient(myRSI, 20, 50, color.new(color.red,   0), color.new(color.red, 70))
myRSIColor = myRSI > 50 ? bullColor : bearColor
plot(myRSI, "RSI", myRSIColor, 3)
hline(100)
hline(50)
hline(0)

// 1. Compress TSI's range from -100/100 to -50/50.
// 2. Shift it higher by 150, so its -50 min value becomes 100.
myTSI = 150 + (100 * ta.tsi(close, 13, 25) / 2)
plot(myTSI, "TSI", color.blue, 2)
plot(ta.ema(myTSI, 13), "TSI EMA", #FF006E)
hline(200)
hline(150)
```

__Note que:__

- Foram adicionados nÃ­veis usando [hline](https://br.tradingview.com/pine-script-reference/v5/#fun_hline) para situar ambos os sinais.
- Para que ambas as linhas de sinal oscilem na mesma faixa de 100, divide-se o valor do [TSI](https://br.tradingview.com/pine-script-reference/v5/#fun_ta.tsi) por 2, pois tem uma faixa de 200 (-100 a +100). Em seguida, desloca-se esse valor para cima em 150 para que oscile entre 100 e 200, fazendo de 150 sua linha central.
- As manipulaÃ§Ãµes feitas aqui sÃ£o tÃ­picas dos compromissos necessÃ¡rios para trazer dois indicadores com escalas diferentes no mesmo espaÃ§o visual, mesmo quando seus valores, ao contrÃ¡rio do [MACD](https://br.tradingview.com/pine-script-reference/v5/#fun_ta.macd), estÃ£o limitados a uma faixa fixa. -->
