
# Time (_Tempo_)

## Quatro ReferÃªncias

Quatro referÃªncias diferentes entram em jogo ao usar valores de data e hora no Pine Script:

1. __Fuso horÃ¡rio UTC__: O formato nativo para valores de tempo no Pine Script Ã© o __tempo Unix em milissegundos__. O tempo Unix Ã© o tempo decorrido desde o __Unix Epoch em 1Âº de janeiro de 1970__. Veja aqui a [hora atual em Unix em segundos](https://www.unixtimestamp.com/pt) e aqui para mais informaÃ§Ãµes sobre o [Tempo Unix](https://pt.wikipedia.org/wiki/Era_Unix). Um valor para o tempo Unix Ã© chamado de _timestamp_. __Timestamps Unix__ sÃ£o sempre expressos no fuso horÃ¡rio UTC (ou "GMT", ou "GMT+0"). Eles sÃ£o medidos a partir de uma referÃªncia fixa, ou seja, o Epoch Unix, e nÃ£o variam com fusos horÃ¡rios. Algumas funcionalidades incorporadas usam o fuso horÃ¡rio UTC como referÃªncia.
2. __Fuso horÃ¡rio da bolsa__: Uma segunda referÃªncia de tempo importante para os traders Ã© o fuso horÃ¡rio da bolsa onde um instrumento Ã© negociado. Algumas funcionalidades incorporadas, como [hour](https://br.tradingview.com/pine-script-reference/v5/#var_hour), retornam valores no fuso horÃ¡rio da bolsa por padrÃ£o.
3. __ParÃ¢metro `timezone`__: Algumas funÃ§Ãµes que normalmente retornam valores no fuso horÃ¡rio da bolsa, como [hour()](https://br.tradingview.com/pine-script-reference/v5/#fun_hour), incluem um parÃ¢metro `timezone` que permite adaptar o resultado da funÃ§Ã£o para outro fuso horÃ¡rio. Outras funÃ§Ãµes, como [time()](https://br.tradingview.com/pine-script-reference/v5/#fun_time), incluem os parÃ¢metros `session` e `timezone`. Nesses casos, o argumento `timezone` se aplica a como o argumento `session` Ã© interpretado â€” nÃ£o ao valor de _tempo_ retornado pela funÃ§Ã£o.
4. __Fuso horÃ¡rio do grÃ¡fico__: Este Ã© o fuso horÃ¡rio escolhido pelo usuÃ¡rio no grÃ¡fico usando o campo "_ConfiguraÃ§Ãµes do GrÃ¡fico/SÃ­mbolo/Fuso HorÃ¡rio_" "_Chart Settings/Symbol/Time Zone_". Esta configuraÃ§Ã£o afeta apenas a exibiÃ§Ã£o de datas e horÃ¡rios no grÃ¡fico. NÃ£o afeta o comportamento dos scripts Pine, e eles nÃ£o tÃªm visibilidade sobre essa configuraÃ§Ã£o.

Ao discutir variÃ¡veis ou funÃ§Ãµes, serÃ¡ notado se elas retornam datas ou horÃ¡rios em UTC ou no fuso horÃ¡rio da bolsa. Scripts nÃ£o tÃªm visibilidade sobre a configuraÃ§Ã£o de fuso horÃ¡rio do usuÃ¡rio no grÃ¡fico.

## FunÃ§Ãµes e VariÃ¡veis Incorporadas de Tempo

Pine Script possui _variÃ¡veis_ "__incorporadas__" "__built-in__" para:

- Obter informaÃ§Ãµes de timestamp da barra atual (fuso horÃ¡rio UTC): [time](https://br.tradingview.com/pine-script-reference/v5/#var_time) e [time_close](https://br.tradingview.com/pine-script-reference/v5/#var_time_close)
- Obter informaÃ§Ãµes de timestamp para o inÃ­cio do dia de negociaÃ§Ã£o atual (fuso horÃ¡rio UTC): [time_tradingday](https://br.tradingview.com/pine-script-reference/v5/#var_time_tradingday)
- Obter o horÃ¡rio atual em incrementos de um segundo (fuso horÃ¡rio UTC): [timenow](https://br.tradingview.com/pine-script-reference/v5/#var_timenow)
- Recuperar valores de calendÃ¡rio e tempo da barra (fuso horÃ¡rio da bolsa): [year](https://br.tradingview.com/pine-script-reference/v5/#var_year), [month](https://br.tradingview.com/pine-script-reference/v5/#var_month), [weekofyear](https://br.tradingview.com/pine-script-reference/v5/#var_weekofyear), [dayofmonth](https://br.tradingview.com/pine-script-reference/v5/#var_dayofmonth), [dayofweek](https://br.tradingview.com/pine-script-reference/v5/#var_dayofweek), [hour](https://br.tradingview.com/pine-script-reference/v5/#var_hour), [minute](https://br.tradingview.com/pine-script-reference/v5/#var_minute) e [second](https://br.tradingview.com/pine-script-reference/v5/#var_second)
- Retornar o fuso horÃ¡rio da bolsa do sÃ­mbolo do grÃ¡fico com [syminfo.timezone](https://br.tradingview.com/pine-script-reference/v5/#var_syminfo%7Bdot%7Dtimezone)

TambÃ©m hÃ¡ _funÃ§Ãµes_ __incorporadas__ que podem:

- Retornar timestamps de barras de outros perÃ­odos com [time()](https://br.tradingview.com/pine-script-reference/v5/#fun_time) e [time_close()](https://br.tradingview.com/pine-script-reference/v5/#fun_time_close), sem a necessidade de uma chamada [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request%7Bdot%7Dsecurity)
- Recuperar valores de calendÃ¡rio e tempo de qualquer timestamp, que podem ser ajustados com um fuso horÃ¡rio: [year()](https://br.tradingview.com/pine-script-reference/v5/#fun_year), [month()](https://br.tradingview.com/pine-script-reference/v5/#fun_month), [weekofyear()](https://br.tradingview.com/pine-script-reference/v5/#fun_weekofyear), [dayofmonth()](https://br.tradingview.com/pine-script-reference/v5/#fun_dayofmonth), [dayofweek()](https://br.tradingview.com/pine-script-reference/v5/#fun_dayofweek), [hour()](https://br.tradingview.com/pine-script-reference/v5/#fun_hour), [minute()](https://br.tradingview.com/pine-script-reference/v5/#fun_minute) e [second()](https://br.tradingview.com/pine-script-reference/v5/#fun_second)
- Criar um timestamp usando [timestamp()](https://br.tradingview.com/pine-script-reference/v5/#fun_timestamp)
- Converter um timestamp para uma string de data/hora formatada para exibiÃ§Ã£o, usando [str.format()](https://br.tradingview.com/pine-script-reference/v5/#fun_str%7Bdot%7Dformat)
- Inserir valores de data e hora. Veja a seÃ§Ã£o sobre [Entradas](./05_09_inputs.md).
- Trabalhar com [informaÃ§Ãµes de sessÃ£o](./05_17_sessoes.md).

## Fuso HorÃ¡rio

Os usuÃ¡rios do TradingView podem alterar o fuso horÃ¡rio usado para exibir os horÃ¡rios das barras em seus grÃ¡ficos. Scripts Pine nÃ£o tÃªm visibilidade sobre essa configuraÃ§Ã£o. Embora haja uma variÃ¡vel [syminfo.timezone](https://br.tradingview.com/pine-script-reference/v5/#var_syminfo%7Bdot%7Dtimezone) para retornar o fuso horÃ¡rio da bolsa onde o instrumento do grÃ¡fico Ã© negociado, __nÃ£o__ existe um equivalente `chart.timezone`.

Ao exibir horÃ¡rios no grÃ¡fico, esta Ã© uma maneira de fornecer aos usuÃ¡rios um meio de ajustar os valores de tempo do seu script aos do grÃ¡fico deles. Dessa forma, os horÃ¡rios exibidos podem corresponder ao fuso horÃ¡rio usado pelos traders em seu grÃ¡fico:

![Fuso horÃ¡rio 01](./imgs/Time-TimeZones-01.BhZfcsQ2_BWgd3.webp)

```c
//@version=5
indicator("Time zone control")
MS_IN_1H = 1000 * 60 * 60
TOOLTIP01 = "Enter your time zone's offset (+ or âˆ’), including a decimal fraction if needed."
hoursOffsetInput = input.float(0.0, "Timezone offset (in hours)", minval = -12.0, maxval = 14.0, step = 0.5, tooltip = TOOLTIP01)

printTable(txt) => 
    var table t = table.new(position.middle_right, 1, 1)
    table.cell(t, 0, 0, txt, text_halign = text.align_right, bgcolor = color.yellow)

msOffsetInput = hoursOffsetInput * MS_IN_1H
printTable(
  str.format("Last bar''s open time UTC: {0,date,HH:mm:ss yyyy.MM.dd}", time) +
  str.format("\nLast bar''s close time UTC: {0,date,HH:mm:ss yyyy.MM.dd}", time_close) +
  str.format("\n\nLast bar''s open time EXCHANGE: {0,date,HH:mm:ss yyyy.MM.dd}", time(timeframe.period, syminfo.session, syminfo.timezone)) +
  str.format("\nLast bar''s close time EXCHANGE: {0,date,HH:mm:ss yyyy.MM.dd}", time_close(timeframe.period, syminfo.session, syminfo.timezone)) +
  str.format("\n\nLast bar''s open time OFFSET ({0}): {1,date,HH:mm:ss yyyy.MM.dd}", hoursOffsetInput, time + msOffsetInput) +
  str.format("\nLast bar''s close time OFFSET ({0}): {1,date,HH:mm:ss yyyy.MM.dd}", hoursOffsetInput, time_close + msOffsetInput) +
  str.format("\n\nCurrent time OFFSET ({0}): {1,date,HH:mm:ss yyyy.MM.dd}", hoursOffsetInput, timenow + msOffsetInput))
```

__Note que:__

- Converte-se o deslocamento do usuÃ¡rio expresso em horas para milissegundos com `msOffsetInput`. Em seguida, adiciona-se esse deslocamento a um timestamp em formato UTC antes de convertÃª-lo para o formato de exibiÃ§Ã£o, por exemplo, `time + msOffsetInput` e `timenow + msOffsetInput`.
- Utiliza-se um `tooltip` para fornecer _instruÃ§Ãµes_ aos usuÃ¡rios.
- Fornecem-se valores `minval` e `maxval` para proteger o campo de entrada e um valor `step` de 0,5 para que, quando utilizarem as setas para cima/baixo do campo, possam entender intuitivamente que fraÃ§Ãµes podem ser usadas.
- A funÃ§Ã£o [str.format()](https://br.tradingview.com/pine-script-reference/v5/#fun_str%7Bdot%7Dformat) formata os valores de tempo, nomeadamente o horÃ¡rio da Ãºltima barra e o horÃ¡rio atual.

Algumas funÃ§Ãµes que normalmente retornam valores no fuso horÃ¡rio da bolsa fornecem meios para adaptar o resultado a outro fuso horÃ¡rio por meio do parÃ¢metro `timezone`. Este script ilustra como fazer isso com [hour()](https://br.tradingview.com/pine-script-reference/v5/#fun_hour):

![Fuso horÃ¡rio 02](./imgs/Time-TimeZones-02.xptlcK0i_1VVd7u.webp)

```c
//@version=5
indicator('`hour(time, "GMT+0")` in orange')
color BLUE_LIGHT = #0000FF30
plot(hour, "", BLUE_LIGHT, 8)
plot(hour(time, syminfo.timezone))
plot(hour(time, "GMT+0"),"UTC", color.orange)
```

__Note que:__

- A variÃ¡vel [hour](https://br.tradingview.com/pine-script-reference/v5/#var_hour) e a funÃ§Ã£o [hour()](https://br.tradingview.com/pine-script-reference/v5/#fun_hour) normalmente retornam um valor no fuso horÃ¡rio da bolsa. Assim, os plots em azul para `hour` e `hour(time, syminfo.timezone)` se sobrepÃµem. Usar a forma de funÃ§Ã£o com `syminfo.timezone` Ã© redundante se a hora da bolsa for necessÃ¡ria.
- A linha laranja plotando `hour(time, "GMT+0")`, no entanto, retorna a hora da barra em UTC, ou horÃ¡rio "GMT+0", que neste caso Ã© quatro horas a menos que a hora da bolsa, jÃ¡ que a MSFT Ã© negociada na NASDAQ, cujo fuso horÃ¡rio Ã© UTC-4.

### Strings de Fuso HorÃ¡rio

O argumento usado para o parÃ¢metro `timezone` em funÃ§Ãµes como [time()](https://br.tradingview.com/pine-script-reference/v5/#fun_time), [timestamp()](https://br.tradingview.com/pine-script-reference/v5/#fun_timestamp), [hour()](https://br.tradingview.com/pine-script-reference/v5/#fun_hour), etc., pode estar em diferentes formatos, que podem ser encontrados na pÃ¡gina de referÃªncia [IANA time zone database name](https://en.wikipedia.org/wiki/List_of_tz_database_time_zones). Os conteÃºdos das colunas "TZ database name", "UTC offset Â±hh:mm" e "UTC DST offset Â±hh:mm" da tabela dessa pÃ¡gina podem ser usados.

Para expressar um deslocamento de +5,5 horas a partir do UTC, essas strings encontradas na pÃ¡gina de referÃªncia sÃ£o todas equivalentes:

- `"GMT+05:30"`
- `"Asia/Calcutta"`
- `"Asia/Colombo"`
- `"Asia/Kolkata"`

Deslocamentos "_nÃ£o-fracionÃ¡rios_" "_non-fractional_" podem ser expressos na forma `"GMT+5"`. `"GMT+5.5"` nÃ£o Ã© permitido.

## VariÃ¡veis de Tempo

### `time` e `time_close`

O plot de [time](https://br.tradingview.com/pine-script-reference/v5/#var_time) e [time_close](https://br.tradingview.com/pine-script-reference/v5/#var_time_close), o timestamp Unix em milissegundos do horÃ¡rio de abertura e fechamento da barra:

![Time e time_close](./imgs/Time-TimeAndTimeclose-01.Db16O5Ht_ZGCjBm.webp)

```c
//@version=5
indicator("`time` and `time_close` values on bars")
plot(time, "`time`")
plot(time_close, "`time_close`")
```

__Note que:__

- As variÃ¡veis [time](https://br.tradingview.com/pine-script-reference/v5/#var_time) e [time_close](https://br.tradingview.com/pine-script-reference/v5/#var_time_close) retornam um timestamp em [UNIX time](https://en.wikipedia.org/wiki/Unix_time), que Ã© independente do fuso horÃ¡rio selecionado pelo usuÃ¡rio no grÃ¡fico. Neste caso, a configuraÃ§Ã£o de fuso horÃ¡rio do __grÃ¡fico__ Ã© o fuso horÃ¡rio da bolsa, entÃ£o, qualquer que seja o sÃ­mbolo no grÃ¡fico, o fuso horÃ¡rio da bolsa serÃ¡ usado para exibir os valores de data e hora no cursor do grÃ¡fico. O fuso horÃ¡rio da NASDAQ Ã© UTC-4, mas isso afeta apenas a exibiÃ§Ã£o de valores de data/hora no grÃ¡fico; nÃ£o impacta os valores plotados pelo script.
- O Ãºltimo valor de [time](https://br.tradingview.com/pine-script-reference/v5/#var_time) para o plot mostrado na escala Ã© o nÃºmero de milissegundos decorridos desde 00:00:00 UTC de 1Âº de janeiro de 1970 atÃ© o horÃ¡rio de abertura da barra. Corresponde Ã s 17:30 do dia 27 de setembro de 2021. No entanto, como o grÃ¡fico usa o fuso horÃ¡rio UTC-4 (o fuso horÃ¡rio da NASDAQ), exibe o horÃ¡rio 13:30, quatro horas antes do horÃ¡rio UTC.
- A diferenÃ§a entre os dois valores na Ãºltima barra Ã© o nÃºmero de milissegundos em uma hora (1000 * 60 * 60 = 3.600.000) por ser um grÃ¡fico de 1H.

### `time_tradingday`

A variÃ¡vel [time_tradingday](https://br.tradingview.com/pine-script-reference/v5/#var_time_tradingday) Ã© Ãºtil quando um sÃ­mbolo Ã© negociado em sessÃµes noturnas que comeÃ§am e terminam em dias de calendÃ¡rio diferentes. Por exemplo, isso acontece nos mercados forex, onde uma sessÃ£o pode abrir no domingo Ã s 17:00 e fechar na segunda-feira Ã s 17:00.

A variÃ¡vel retorna o horÃ¡rio de inÃ­cio do dia de negociaÃ§Ã£o em [tempo UNIX](https://pt.wikipedia.org/wiki/Era_Unix) quando usada em _timeframes_ de 1D ou menos. Quando usada em _timeframes_ superiores a 1D, retorna o horÃ¡rio de inÃ­cio do Ãºltimo dia de negociaÃ§Ã£o na barra (por exemplo, em 1W, retornarÃ¡ o horÃ¡rio de inÃ­cio do Ãºltimo dia de negociaÃ§Ã£o da semana).

### `timenow`

O [timenow](https://br.tradingview.com/pine-script-reference/v5/#var_timenow) retorna a hora atual em [tempo UNIX](https://pt.wikipedia.org/wiki/Era_Unix). Funciona em tempo real, mas tambÃ©m quando um script Ã© executado em barras histÃ³ricas. Em tempo real, seus scripts perceberÃ£o mudanÃ§as apenas quando forem executados em atualizaÃ§Ãµes de feed. Quando nÃ£o ocorrem atualizaÃ§Ãµes, o script fica inativo, portanto, nÃ£o pode atualizar sua exibiÃ§Ã£o. Veja a pÃ¡gina sobre o [modelo de execuÃ§Ã£o](./04_01_modelo_de_execucao.md) do Pine Script para mais informaÃ§Ãµes.

Este script usa os valores de [timenow](https://br.tradingview.com/pine-script-reference/v5/#var_timenow) e [time_close](https://br.tradingview.com/pine-script-reference/v5/#var_time_close) para calcular uma contagem regressiva em tempo real para barras intradiÃ¡rias. Ao contrÃ¡rio da contagem regressiva no grÃ¡fico, esta sÃ³ serÃ¡ atualizada quando uma atualizaÃ§Ã£o de feed causar a execuÃ§Ã£o de outra iteraÃ§Ã£o do script:

```c
//@version=5
indicator("", "", true)

printTable(txt) => 
    var table t = table.new(position.middle_right, 1, 1)
    table.cell(t, 0, 0, txt, text_halign = text.align_right, bgcolor = color.yellow)

printTable(str.format("{0,time,HH:mm:ss.SSS}", time_close - timenow))
```

### VariÃ¡veis de Datas e HorÃ¡rios do CalendÃ¡rio

VariÃ¡veis de data e hora do calendÃ¡rio, como [year](https://br.tradingview.com/pine-script-reference/v5/#var_year), [month](https://br.tradingview.com/pine-script-reference/v5/#var_month), [weekofyear](https://br.tradingview.com/pine-script-reference/v5/#var_weekofyear), [dayofmonth](https://br.tradingview.com/pine-script-reference/v5/#var_dayofmonth), [dayofweek](https://br.tradingview.com/pine-script-reference/v5/#var_dayofweek), [hour](https://br.tradingview.com/pine-script-reference/v5/#var_hour), [minute](https://br.tradingview.com/pine-script-reference/v5/#var_minute) e [second](https://br.tradingview.com/pine-script-reference/v5/#var_second) podem ser Ãºteis para testar datas ou horÃ¡rios especÃ­ficos e como argumentos para [timestamp()](https://br.tradingview.com/pine-script-reference/v5/#fun_timestamp).

Ao testar datas ou horÃ¡rios especÃ­ficos, Ã© necessÃ¡rio considerar a possibilidade de o script ser executado em _timeframes_ onde a condiÃ§Ã£o testada nÃ£o pode ser detectada ou para casos em que uma barra com o requisito especÃ­fico nÃ£o existirÃ¡. Suponha, por exemplo, que se deseje detectar o primeiro dia de negociaÃ§Ã£o do mÃªs. Este script mostra como usar apenas [dayofmonth](https://br.tradingview.com/pine-script-reference/v5/#var_dayofmonth) nÃ£o funcionarÃ¡ quando um grÃ¡fico semanal for usado ou quando nÃ£o houver negociaÃ§Ã£o no primeiro dia do mÃªs:

![VariÃ¡veis de datas e horÃ¡rios do calendÃ¡rio](./imgs/Time-CalendarDatesAndTimes-01.CsQOdc6g_Z2aBm8f.webp)

```c
//@version=5
indicator("", "", true)
firstDayIncorrect = dayofmonth == 1
firstDay = ta.change(time("M"))
plotchar(firstDayIncorrect, "firstDayIncorrect", "â€¢", location.top, size = size.small)
bgcolor(firstDay ? color.silver : na)
```

__Note que:__

- Usar `ta.change(time("M"))` Ã© mais robusto, pois funciona em todos os meses (#1 e #2), exibidos como o fundo prateado, enquanto o ponto azul detectado usando `dayofmonth == 1` nÃ£o funciona (#1) quando o primeiro dia de negociaÃ§Ã£o de setembro ocorre no dia 2.
- A condiÃ§Ã£o `dayofmonth == 1` serÃ¡ `true` em todas as barras do primeiro dia do mÃªs, mas `ta.change(time("M"))` serÃ¡ `true` apenas na primeira.

Se quiser que o script seja exibido apenas para os anos de 2020 em diante, pode usar:

```c
//@version=5
indicator("", "", true)
plot(year >= 2020 ? close : na, linewidth = 3)
```

### `syminfo.timezone()`

O [syminfo.timezone](https://br.tradingview.com/pine-script-reference/v5/#var_syminfo%7Bdot%7Dtimezone) retorna o fuso horÃ¡rio da bolsa do sÃ­mbolo do grÃ¡fico. Pode ser Ãºtil quando um parÃ¢metro `timezone` estÃ¡ disponÃ­vel em uma funÃ§Ã£o e deseja-se mencionar que estÃ¡ usando explicitamente o fuso horÃ¡rio da bolsa. Geralmente Ã© redundante, pois quando nenhum argumento Ã© fornecido para `timezone`, assume-se o fuso horÃ¡rio da bolsa.

## FunÃ§Ãµes de Tempo

### `time()` e `time_close()`

As funÃ§Ãµes [time()](https://br.tradingview.com/pine-script-reference/v5/#fun_time) e [time_close()](https://br.tradingview.com/pine-script-reference/v5/#fun_time_close) tÃªm a seguinte assinatura:

```c
time(timeframe, session, timezone) â†’ series int
time_close(timeframe, session, timezone) â†’ series int
```

TrÃªs __argumentos__ aceitos:

##### `timeframe`

Uma string no formato [timeframe.period](https://br.tradingview.com/pine-script-reference/v5/#var_timeframe%7Bdot%7Dperiod).

##### `session`

Uma string opcional no formato de especificaÃ§Ã£o de sessÃ£o: `"hhmm-hhmm[:days]"`, onde a parte `[:days]` Ã© opcional. Veja a pÃ¡gina sobre [sessÃµes](./05_17_sessoes.md) para mais informaÃ§Ãµes.

##### `timezone`

Um valor opcional que qualifica o argumento para `session` quando um Ã© usado.

Veja as entradas de [time()](https://br.tradingview.com/pine-script-reference/v5/#fun_time) e [time_close()](https://br.tradingview.com/pine-script-reference/v5/#fun_time_close) no Manual de ReferÃªncia para mais informaÃ§Ãµes.

A funÃ§Ã£o [time()](https://br.tradingview.com/pine-script-reference/v5/#fun_time) Ã© mais frequentemente usada para:

1. Testar se uma barra estÃ¡ em um perÃ­odo de tempo especÃ­fico, o que exigirÃ¡ o uso do parÃ¢metro `session`. Nesses casos, `timeframe.period`, ou seja, o perÃ­odo do grÃ¡fico, serÃ¡ frequentemente usado para o primeiro parÃ¢metro. Ao usar a funÃ§Ã£o dessa maneira, confia-se no fato de que ela retornarÃ¡ [na](https://br.tradingview.com/pine-script-reference/v5/#var_na) quando a barra nÃ£o fizer parte do perÃ­odo especificado no argumento `session`.
2. Detectar mudanÃ§as em perÃ­odos de tempo superiores ao do grÃ¡fico, usando o perÃ­odo de tempo superior para o argumento `timeframe`. Ao usar a funÃ§Ã£o para esse propÃ³sito, procura-se mudanÃ§as no valor retornado, o que significa que a barra de perÃ­odo superior mudou. Isso geralmente exigirÃ¡ o uso de [ta.change()](https://br.tradingview.com/pine-script-reference/v5/#fun_ta%7Bdot%7Dchange) para testar, por exemplo, `ta.change(time("D"))` retornarÃ¡ a mudanÃ§a no tempo quando uma nova barra de perÃ­odo superior entrar, entÃ£o o resultado da expressÃ£o serÃ¡ convertido em um valor "bool" quando usado em uma expressÃ£o condicional. O resultado "bool" serÃ¡ `true` quando houver uma mudanÃ§a e `false` quando nÃ£o houver mudanÃ§a.

#### Testando SessÃµes

Um exemplo do primeiro caso Ã© determinar se o horÃ¡rio de inÃ­cio de uma barra faz parte de um perÃ­odo entre 11:00 e 13:00:

![Testando sessÃµes](./imgs/Time-Time()AndTimeclose()-01.D7rt1wV__1zldez.webp)

```c
//@version=5
indicator("Session bars", "", true)
inSession = not na(time(timeframe.period, "1100-1300"))
bgcolor(inSession ? color.silver : na)
```

__Note que:__

- `time(timeframe.period, "1100-1300")` verifica se o perÃ­odo do grÃ¡fico inclui o horÃ¡rio de abertura da barra entre 11:00 e 13:00, inclusive. A funÃ§Ã£o retorna o horÃ¡rio de abertura se a barra estiver na sessÃ£o. Se __nÃ£o__ estiver, a funÃ§Ã£o retorna [na](https://br.tradingview.com/pine-script-reference/v5/#var_na).
- Interessa-se em identificar os casos quando [time()](https://br.tradingview.com/pine-script-reference/v5/#fun_time) nÃ£o retorna [na](https://br.tradingview.com/pine-script-reference/v5/#var_na), pois isso significa que a barra estÃ¡ na sessÃ£o, entÃ£o testa-se `not na(...)`. O valor de retorno real de [time()](https://br.tradingview.com/pine-script-reference/v5/#fun_time) quando nÃ£o Ã© [na](https://br.tradingview.com/pine-script-reference/v5/#var_na) nÃ£o Ã© utilizado; interessa-se apenas se retorna [na](https://br.tradingview.com/pine-script-reference/v5/#var_na) ou nÃ£o.

#### Testando MudanÃ§as em _Timeframes_ Maiores

Muitas vezes, Ã© Ãºtil detectar mudanÃ§as em um _timeframe_ maior. Por exemplo, pode-se querer detectar mudanÃ§as no dia de negociaÃ§Ã£o enquanto estÃ¡ em grÃ¡ficos intradiÃ¡rios. Nesses casos, pode-se usar o fato de que `time("D")` retorna o horÃ¡rio de abertura da barra de 1D, mesmo que o grÃ¡fico esteja em um _timeframe_ intradiÃ¡rio, como 1H:

![Testando mudanÃ§as em timeframes maiores](./imgs/Time-TestingForChangesInHTF-01.Dfuq4jRU_2pdjFq.webp)

```c
//@version=5
indicator("", "", true)
bool newDay = ta.change(time("D"))
bgcolor(newDay ? color.silver : na)

newExchangeDay = ta.change(dayofmonth)
plotchar(newExchangeDay, "newExchangeDay", "ðŸ ‡", location.top, size = size.small)
```

__Note que:__

- A variÃ¡vel `newDay` detecta mudanÃ§as no horÃ¡rio de abertura de barras de 1D, seguindo as convenÃ§Ãµes para o sÃ­mbolo do grÃ¡fico, que usa sessÃµes noturnas das 17:00 Ã s 17:00. Muda de valor quando uma nova sessÃ£o comeÃ§a.
- Como `newExchangeDay` detecta mudanÃ§as no [dayofmonth](https://br.tradingview.com/pine-script-reference/v5/#var_dayofmonth) do dia do calendÃ¡rio, ele muda quando o dia muda no grÃ¡fico.
- Os dois mÃ©todos de detecÃ§Ã£o de mudanÃ§as sÃ³ coincidem no grÃ¡fico quando hÃ¡ dias sem negociaÃ§Ã£o. Nos domingos, por exemplo, ambos os mÃ©todos de detecÃ§Ã£o identificarÃ£o uma mudanÃ§a, pois o dia do calendÃ¡rio muda do Ãºltimo dia de negociaÃ§Ã£o (sexta-feira) para o primeiro dia do calendÃ¡rio da nova semana, domingo, que Ã© quando a sessÃ£o noturna de segunda-feira comeÃ§a Ã s 17:00.

### FunÃ§Ãµes de Datas e HorÃ¡rios do CalendÃ¡rio

FunÃ§Ãµes de data e hora do calendÃ¡rio, como [year()](https://br.tradingview.com/pine-script-reference/v5/#fun_year), [month()](https://br.tradingview.com/pine-script-reference/v5/#fun_month), [weekofyear()](https://br.tradingview.com/pine-script-reference/v5/#fun_weekofyear), [dayofmonth()](https://br.tradingview.com/pine-script-reference/v5/#fun_dayofmonth), [dayofweek()](https://br.tradingview.com/pine-script-reference/v5/#fun_dayofweek), [hour()](https://br.tradingview.com/pine-script-reference/v5/#fun_hour), [minute()](https://br.tradingview.com/pine-script-reference/v5/#fun_minute) e [second()](https://br.tradingview.com/pine-script-reference/v5/#fun_second) podem ser Ãºteis para testar datas ou horÃ¡rios especÃ­ficos. Possuem assinatures semelhantes ao [dayofmonth()](https://br.tradingview.com/pine-script-reference/v5/#fun_dayofmonth):

```c
dayofmonth(time) â†’ series int
dayofmonth(time, timezone) â†’ series int
```

Isso irÃ¡ plotar o dia de abertura da barra onde o tempo 1Âº de janeiro de 2021 Ã s 00:00 cai entre seus valores [time](https://br.tradingview.com/pine-script-reference/v5/#var_time) e [time_close](https://br.tradingview.com/pine-script-reference/v5/#var_time_close):

```c
//@version=5
indicator("")
exchangeDay = dayofmonth(timestamp("2021-01-01"))
plot(exchangeDay)
```

O valor serÃ¡ o dia 31 ou o dia 1, dependendo do dia do calendÃ¡rio em que a sessÃ£o abre no sÃ­mbolo do grÃ¡fico. A data para sÃ­mbolos negociados 24x7 em bolsas usando o fuso horÃ¡rio UTC serÃ¡ o dia 1. Para sÃ­mbolos negociados em bolsas no fuso horÃ¡rio UTC-4, a data serÃ¡ o dia 31.

### `timestamp()`

A funÃ§Ã£o [timestamp()](https://br.tradingview.com/pine-script-reference/v5/#fun_timestamp) possui algumas assinaturas diferentes:

```c
timestamp(year, month, day, hour, minute, second) â†’ simple/series int
timestamp(timezone, year, month, day, hour, minute, second) â†’ simple/series int
timestamp(dateString) â†’ const int
```

A Ãºnica diferenÃ§a entre os dois primeiros Ã© o parÃ¢metro `timezone`. Seu valor padrÃ£o Ã© [syminfo.timezone](https://br.tradingview.com/pine-script-reference/v5/#var_syminfo%7Bdot%7Dtimezone). Consulte a seÃ§Ã£o [Strings de Fuso HorÃ¡rio](./05_21_time.md#strings-de-fuso-horÃ¡rio) desta pÃ¡gina para valores vÃ¡lidos.

A terceira forma Ã© usada como um valor `defval` em [input.time()](https://br.tradingview.com/pine-script-reference/v5/#fun_input%7Bdot%7Dtime). Consulte a entrada [timestamp()](https://br.tradingview.com/pine-script-reference/v5/#fun_timestamp) no Manual de ReferÃªncia para mais informaÃ§Ãµes.

[timestamp()](https://br.tradingview.com/pine-script-reference/v5/#fun_timestamp) Ã© Ãºtil para gerar um timestamp para uma data especÃ­fica. Para gerar um timestamp para 1Âº de janeiro de 2021, use qualquer um destes mÃ©todos:

```c
//@version=5
indicator("")
yearBeginning1 = timestamp("2021-01-01")
yearBeginning2 = timestamp(2021, 1, 1, 0, 0)
printTable(txt) => var table t = table.new(position.middle_right, 1, 1), table.cell(t, 0, 0, txt, bgcolor = color.yellow)
printTable(str.format("yearBeginning1: {0,date,yyyy.MM.dd hh:mm}\nyearBeginning2: {1,date,yyyy.MM.dd hh:mm}", yearBeginning1, yearBeginning1))
```

Deslocamentos podem ser usados nos argumentos de [timestamp()](https://br.tradingview.com/pine-script-reference/v5/#fun_timestamp). Aqui, subtrai-se 2 do valor fornecido para seu parÃ¢metro `day` para obter a data/hora da Ãºltima barra do grÃ¡fico hÃ¡ dois dias. Observe que, devido aos diferentes alinhamentos de barras em vÃ¡rios instrumentos, a barra identificada no grÃ¡fico pode nÃ£o estar exatamente a 48 horas de distÃ¢ncia, embora o valor retornado pela funÃ§Ã£o esteja correto:

```c
//@version=5
indicator("")
twoDaysAgo = timestamp(year, month, dayofmonth - 2, hour, minute)
printTable(txt) => var table t = table.new(position.middle_right, 1, 1), table.cell(t, 0, 0, txt, bgcolor = color.yellow)
printTable(str.format("{0,date,yyyy.MM.dd hh:mm}", twoDaysAgo))
```

## FormataÃ§Ã£o de Datas e Horas

Timestamps podem ser formatados usando [str.format()](https://br.tradingview.com/pine-script-reference/v5/#fun_str%7Bdot%7Dformat). Estes sÃ£o exemplos de vÃ¡rios formatos:

![FormataÃ§Ã£o de Datas e Horas](./imgs/Time-FormattingDatesAndTime-01.C3KF2qwQ_Z1A6rFf.webp)

```c
//@version=5
indicator("", "", true)

print(txt, styl) => 
    var alignment = styl == label.style_label_right ? text.align_right : text.align_left
    var lbl = label.new(na, na, "", xloc.bar_index, yloc.price, color(na), styl, color.black, size.large, alignment)
    if barstate.islast
        label.set_xy(lbl, bar_index, hl2[1])
        label.set_text(lbl, txt)

var string format = 
  "{0,date,yyyy.MM.dd hh:mm:ss}\n" +
  "{1,date,short}\n" +
  "{2,date,medium}\n" +
  "{3,date,long}\n" +
  "{4,date,full}\n" +
  "{5,date,h a z (zzzz)}\n" +
  "{6,time,short}\n" +
  "{7,time,medium}\n" +
  "{8,date,'Month 'MM, 'Week' ww, 'Day 'DD}\n" +
  "{9,time,full}\n" + 
  "{10,time,hh:mm:ss}\n" +
  "{11,time,HH:mm:ss}\n" +
  "{12,time,HH:mm:ss} Left in bar\n"

print(format, label.style_label_right)
print(str.format(format,
  time, time, time, time, time, time, time, 
  timenow, timenow, timenow, timenow, 
  timenow - time, time_close - timenow), label.style_label_left)
```
