
# Style Guide (_Guia de Estilo_)

Este guia de estilo fornece recomenda√ß√µes sobre como nomear vari√°veis e organizar seus scripts Pine de uma maneira padr√£o que funcione bem. Scripts que seguem as melhores pr√°ticas ser√£o mais f√°ceis de ler, entender e manter.

Scripts que utilizam essas diretrizes podem ser encontrados nas contas [TradingView](https://br.tradingview.com/u/TradingView/#published-scripts) e [PineCoders](https://br.tradingview.com/u/PineCoders/#published-scripts) na plataforma.

## Conven√ß√µes de Nomea√ß√£o

Recomenda-se o uso de:

- `camelCase` para todos os identificadores, ou seja, nomes de vari√°veis ou fun√ß√µes: `ma`, `maFast`, `maLengthInput`, `maColor`, `roundedOHLC()`, `pivotHi()`.
- `SNAKE_CASE` em letras mai√∫sculas para constantes: `BULL_COLOR`, `BEAR_COLOR`, `MAX_LOOKBACK`.
- O uso de sufixos qualificadores quando fornecem pistas valiosas sobre o tipo ou a proced√™ncia de uma vari√°vel: `maShowInput`, `bearColor`, `bearColorInput`, `volumesArray`, `maPlotID`, `resultsTable`, `levelsColorArray`.

## Organiza√ß√£o do Script

O compilador Pine Script √© bastante tolerante quanto √† posi√ß√£o de declara√ß√µes espec√≠ficas ou √† [anota√ß√£o do compilador](./04_03_estrutura_do_script.md#anota√ß√µes-do-compilador) no script. Embora outras disposi√ß√µes sejam sintaticamente corretas, recomenda-se organizar os scripts da seguinte maneira:

```c
<license>
<version>
<declaration_statement>
<import_statements>
<constant_declarations>
<inputs>
<function_declarations>
<calculations>
<strategy_calls>
<visuals>
<alerts>
```

### `<license>`

Se publicar seus scripts de c√≥digo aberto publicamente no TradingView (scripts tamb√©m podem ser publicados de forma privada), seu c√≥digo de c√≥digo aberto √©, por padr√£o, protegido pela licen√ßa Mozilla. Pode-se escolher qualquer outra licen√ßa preferida.

A reutiliza√ß√£o de c√≥digo desses scripts √© regida pelas [Regras da Casa sobre Publica√ß√£o de Scripts](https://br.tradingview.com/support/solutions/43000590599), que t√™m preced√™ncia sobre a licen√ßa do autor.

Os coment√°rios padr√£o de licen√ßa que aparecem no in√≠cio dos scripts s√£o:

```c
// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// ¬© username
```

### `<version>`

Esta √© a [anota√ß√£o do compilador](./04_03_estrutura_do_script.md#anota√ß√µes-do-compilador) que define a vers√£o do Pine Script que o script usar√°. Se nenhuma estiver presente, v1 ser√° usada. Para v5, utilize:

```c
//@version=5  
```

### `<declaration_statement>`

Esta √© a declara√ß√£o obrigat√≥ria que define o tipo de script. Deve ser uma chamada para [indicator()](https://br.tradingview.com/pine-script-reference/v5/#fun_indicator), [strategy()](https://br.tradingview.com/pine-script-reference/v5/#fun_strategy) ou [library()](https://br.tradingview.com/pine-script-reference/v5/#fun_library).

### `<import_statements>`

Se o script utilizar uma ou mais [bibliotecas Pine Script](./05_11_libraries.md), as instru√ß√µes de [importa√ß√£o](https://br.tradingview.com/pine-script-reference/v5/#kw_import) pertencem a esta se√ß√£o.

### `<constant_declarations>`

Scripts podem declarar vari√°veis qualificadas como ‚Äúconst‚Äù, ou seja, que referenciam um valor constante.

Referem-se a vari√°veis como ‚Äúconstantes‚Äù quando atendem a estes crit√©rios:

- Sua declara√ß√£o usa a palavra-chave opcional `const` (veja a se√ß√£o sobre [qualificadores de tipo](./04_09_tipagem_do_sistema.md#qualificadores) no Manual do Usu√°rio para mais informa√ß√µes).
- S√£o inicializadas usando um literal (por exemplo, `100` ou `"AAPL"`) ou um valor incorporado qualificado como ‚Äúconst‚Äù (por exemplo, `color.green`).
- Seu valor n√£o muda durante a execu√ß√£o do script.

Utiliza-se `SNAKE_CASE` para nomear essas vari√°veis e agrupar suas declara√ß√µes pr√≥ximo ao in√≠cio do script. Por exemplo:

```c
// ‚Äî‚Äî‚Äî‚Äî‚Äî Constants
int     MS_IN_MIN   = 60 * 1000
int     MS_IN_HOUR  = MS_IN_MIN  * 60
int     MS_IN_DAY   = MS_IN_HOUR * 24

color   GRAY        = #808080ff
color   LIME        = #00FF00ff
color   MAROON      = #800000ff
color   ORANGE      = #FF8000ff
color   PINK        = #FF0080ff
color   TEAL        = #008080ff
color   BG_DIV      = color.new(ORANGE, 90)
color   BG_RESETS   = color.new(GRAY, 90)

string  RST1        = "No reset; cumulate since the beginning of the chart"
string  RST2        = "On a stepped higher timeframe (HTF)"
string  RST3        = "On a fixed HTF"
string  RST4        = "At a fixed time"
string  RST5        = "At the beginning of the regular session"
string  RST6        = "At the first visible chart bar"
string  RST7        = "Fixed rolling period"

string  LTF1        = "Least precise, covering many chart bars"
string  LTF2        = "Less precise, covering some chart bars"
string  LTF3        = "More precise, covering less chart bars"
string  LTF4        = "Most precise, 1min intrabars"

string  TT_TOTVOL     = "The 'Bodies' value is the transparency of the total volume candle bodies. Zero is opaque, 100 is transparent."
string  TT_RST_HTF    = "This value is used when '" + RST3 +"' is selected."
string  TT_RST_TIME   = "These values are used when '" + RST4 +"' is selected.
  A reset will occur when the time is greater or equal to the bar's open time, and less than its close time.\nHour: 0-23\nMinute: 0-59"
string  TT_RST_PERIOD = "This value is used when '" + RST7 +"' is selected."
```

Neste exemplo:

- As constantes `RST*` e `LTF*` ser√£o usadas como elementos de tupla no argumento `options` de chamadas `input.*()`.
- As constantes `TT_*` ser√£o usadas como argumentos `tooltip` em chamadas `input.*()`. Note como se utiliza uma continua√ß√£o de linha para literais de string longos.
- N√£o se utiliza [var](https://br.tradingview.com/pine-script-reference/v5/#kw_var) para inicializar constantes. O tempo de execu√ß√£o do Pine Script √© otimizado para lidar com declara√ß√µes em cada barra, mas usar [var](https://br.tradingview.com/pine-script-reference/v5/#kw_var) para inicializar uma vari√°vel apenas na primeira vez que √© declarada implica uma pequena penalidade no desempenho do script devido √† manuten√ß√£o que as vari√°veis [var](https://br.tradingview.com/pine-script-reference/v5/#kw_var) requerem em barras subsequentes.

__Note que:__

- Literais usados em mais de um lugar em um script devem sempre ser declarados como constantes. Usar a constante em vez do literal torna o script mais leg√≠vel se receber um nome significativo, e a pr√°tica facilita a manuten√ß√£o do c√≥digo. Mesmo que a quantidade de milissegundos em um dia seja improv√°vel de mudar no futuro, `MS_IN_DAY` √© mais significativo do que `1000 * 60 * 60 * 24`.
- Constantes usadas apenas no bloco local de uma fun√ß√£o ou declara√ß√£o [if](https://br.tradingview.com/pine-script-reference/v5/#kw_if), [while](https://br.tradingview.com/pine-script-reference/v5/#kw_while), etc., podem ser declaradas nesse bloco local.

### `<inputs>`

√â __muito__ mais f√°cil ler scripts quando todas as entradas est√£o na mesma se√ß√£o de c√≥digo. Colocar essa se√ß√£o no in√≠cio do script tamb√©m reflete como s√£o processadas em tempo de execu√ß√£o, ou seja, antes do restante do script ser executado.

Sufixar nomes de vari√°veis de entrada com `input` as torna mais facilmente identific√°veis quando s√£o usadas posteriormente no script: `maLengthInput`, `bearColorInput`, `showAvgInput`, etc.

```c
// ‚Äî‚Äî‚Äî‚Äî‚Äî Inputs
string  resetInput              = input.string(RST2,        "CVD Resets",                       inline = "00", options = [RST1, RST2, RST3, RST4, RST5, RST6, RST7])
string  fixedTfInput            = input.timeframe("D",      "‚ÄÉ‚ÄÉFixed HTF:‚ÄÉ‚ÄÉ",                   tooltip = TT_RST_HTF)
int     hourInput               = input.int(9,              "‚ÄÉ‚ÄÉFixed time hour:‚Äâ‚Äâ",             inline = "01", minval = 0, maxval = 23)
int     minuteInput             = input.int(30,             "minute",                           inline = "01", minval = 0, maxval = 59, tooltip = TT_RST_TIME)
int     fixedPeriodInput        = input.int(20,             "‚ÄÉ‚ÄÉFixed period:‚ÄÉ‚ÄÉ",                inline = "02", minval = 1, tooltip = TT_RST_PERIOD)
string  ltfModeInput            = input.string(LTF3,        "Intrabar precision",               inline = "03", options = [LTF1, LTF2, LTF3, LTF4]) 
```

### `<function_declarations>`

Todas as fun√ß√µes definidas pelo usu√°rio devem ser definidas no escopo global do script; defini√ß√µes de fun√ß√µes aninhadas n√£o s√£o permitidas no Pine Script.

O design ideal da fun√ß√£o deve minimizar o uso de vari√°veis globais no escopo da fun√ß√£o, pois prejudicam a portabilidade da fun√ß√£o. Quando n√£o for poss√≠vel evitar, essas fun√ß√µes devem seguir as declara√ß√µes de vari√°veis globais no c√≥digo, o que implica que nem sempre podem ser colocadas na se√ß√£o `<function_declarations>`. Tais depend√™ncias de vari√°veis globais devem ser documentadas idealmente nos coment√°rios da fun√ß√£o.

Tamb√©m ajudar√° os leitores se documentar o objetivo da fun√ß√£o, par√¢metros e resultado. A mesma sintaxe usada em [bibliotecas](./05_11_libraries.md) pode ser usada para documentar suas fun√ß√µes. Isso pode facilitar a portabilidade de suas fun√ß√µes para uma biblioteca, caso decida fazer isso:

```c
//@version=5
indicator("<function_declarations>", "", true)

string SIZE_LARGE  = "Large"
string SIZE_NORMAL = "Normal"
string SIZE_SMALL  = "Small"

string sizeInput = input.string(SIZE_NORMAL, "Size", options = [SIZE_LARGE, SIZE_NORMAL, SIZE_SMALL])

// @function        Used to produce an argument for the `size` parameter in built-in functions.
// @param userSize  (simple string) User-selected size.
// @returns         One of the `size.*` built-in constants.
// Dependencies     SIZE_LARGE, SIZE_NORMAL, SIZE_SMALL
getSize(simple string userSize) =>
    result = 
      switch userSize
        SIZE_LARGE  => size.large
        SIZE_NORMAL => size.normal
        SIZE_SMALL  => size.small
        => size.auto

if ta.rising(close, 3)
    label.new(bar_index, na, yloc = yloc.abovebar, style = label.style_arrowup, size = getSize(sizeInput))
```

### `<calculations>`

Esta √© a se√ß√£o onde devem ser colocados os c√°lculos principais e a l√≥gica do script. O c√≥digo pode ser mais f√°cil de ler quando as declara√ß√µes de vari√°veis s√£o colocadas pr√≥ximas ao segmento de c√≥digo que as usa. Alguns programadores preferem colocar todas as suas declara√ß√µes de vari√°veis n√£o constantes no in√≠cio desta se√ß√£o, o que nem sempre √© poss√≠vel para todas as vari√°veis, pois algumas podem exigir que alguns c√°lculos tenham sido executados antes de sua declara√ß√£o.

### `<strategy_calls>`

As estrat√©gias s√£o mais f√°ceis de ler quando as chamadas de estrat√©gia s√£o agrupadas na mesma se√ß√£o do script.

### `<visuals>`

Esta se√ß√£o deve incluir idealmente todas as instru√ß√µes que produzem os visuais do script, sejam eles gr√°ficos, desenhos, cores de fundo, plotagem de velas, etc. Veja a se√ß√£o sobre [cores](./05_07_cores.md) do manual do usu√°rio do Pine Script para mais informa√ß√µes sobre como a profundidade relativa dos visuais √© determinada.

### `<alerts>`

O c√≥digo de alertas geralmente exigir√° que os c√°lculos do script tenham sido executados antes dele, ent√£o faz sentido coloc√°-lo no final do script.

## Espa√ßamento

Um espa√ßo deve ser usado em ambos os lados de todos os operadores, exceto operadores un√°rios (`-1`). Tamb√©m √© recomendado um espa√ßo ap√≥s todas as v√≠rgulas e ao usar argumentos nomeados de fun√ß√µes, como em `plot(series = close)`:

```c
int a = close > open ? 1 : -1
var int newLen = 2
newLen := min(20, newlen + 1)
float a = -b
float c = d > e ? d - e : d
int index = bar_index % 2 == 0 ? 1 : 2
plot(close, color = color.red)
```

## Quebra de Linha

A quebra de linha pode tornar linhas longas mais f√°ceis de ler. Quebras de linha s√£o definidas usando um n√≠vel de indenta√ß√£o que n√£o √© m√∫ltiplo de quatro, pois quatro espa√ßos ou uma tabula√ß√£o s√£o usados para definir blocos locais. Aqui, usando dois espa√ßos:

```c
plot(
   series = close,
   title = "Close",
   color = color.blue,
   show_last = 10
 )
```

## Alinhamento Vertical

O alinhamento vertical usando tabula√ß√µes ou espa√ßos pode ser √∫til em se√ß√µes de c√≥digo contendo muitas linhas semelhantes, como declara√ß√µes de constantes ou entradas. Elas podem facilitar edi√ß√µes em massa usando o recurso de m√∫ltiplos cursos do Pine Editor (`ctrl` \+ `alt` \+ `ü†Ö`):

```c
// Colors used as defaults in inputs.
color COLOR_AQUA  = #0080FFff
color COLOR_BLACK = #000000ff
color COLOR_BLUE  = #013BCAff
color COLOR_CORAL = #FF8080ff
color COLOR_GOLD  = #CCCC00ff
```

## Tipagem Expl√≠cita

Incluir o tipo de vari√°veis ao declar√°-las n√£o √© obrigat√≥rio. No entanto, isso ajuda a tornar os scripts mais f√°ceis de ler, navegar e entender. Pode ajudar a esclarecer os tipos esperados em cada ponto da execu√ß√£o do script e distinguir a declara√ß√£o de uma vari√°vel (usando `=`) de suas reatribui√ß√µes (usando `:=`). Usar tipagem expl√≠cita tamb√©m pode tornar os scripts mais f√°ceis de [depurar](./06_02_debugging.md).
