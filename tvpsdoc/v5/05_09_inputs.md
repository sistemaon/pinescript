
# Inputs (_Entradas_)

Os _Inputs_ ("_Entradas_") permitem que scripts recebam valores que os usu√°rios podem alterar. Us√°-las para valores-chave torna os scripts mais adapt√°veis √†s prefer√™ncias do usu√°rio.

O script a seguir plota uma [m√©dia m√≥vel simples (SMA)](https://br.tradingview.com/support/solutions/43000502589) de 20 per√≠odos usando `ta.sma(close, 20)`. Embora seja simples de escrever, n√£o √© muito flex√≠vel, pois essa MA espec√≠fica √© tudo o que ele sempre plotar√°:

```c
//@version=5
indicator("MA", "", true)
plot(ta.sma(close, 20))
```

Se, em vez disso, o script for escrito desta maneira, ele se torna muito mais flex√≠vel, pois os usu√°rios poder√£o selecionar a "_source_" ("_fonte_") e a "_length_" ("_comprimento_") que desejam usar para o c√°lculo da MA:

```c
//@version=5
indicator("MA", "", true)
sourceInput = input(close, "Source")
lengthInput = input(20, "Length")
plot(ta.sma(sourceInput, lengthInput))
```

Entradas s√≥ podem ser acessadas quando um script est√° sendo executado no gr√°fico. Os usu√°rios do script acessam as entradas atrav√©s da caixa de di√°logo "_Settings_" ("_Configura√ß√µes_") do script, que pode ser alcan√ßada de uma das seguintes maneiras:

- Com um clique duplo no nome de um indicador no gr√°fico.
- Clicando com o bot√£o direito do mouse no nome do script e escolhendo o item "_Settings_" ("_Configura√ß√µes_") no menu suspenso.
- Escolhendo o item "_Settings_" ("_Configura√ß√µes_") no √≠cone do menu "_More_" ("_Mais_") (tr√™s pontos) que aparece ao passar o cursor sobre o nome do indicador no gr√°fico.
- Com um clique duplo no nome do indicador na "_Data Window_" ("_Janela de Dados_") (quarto √≠cone abaixo √† direita do gr√°fico).

A caixa de di√°logo "_Settings_" ("_Configura√ß√µes_") sempre cont√©m as abas "_Style_" ("_Estilo_") e "_Visibility_" ("_Visibilidade_"), que permitem aos usu√°rios especificar suas prefer√™ncias sobre os visuais do script e os timeframes do gr√°fico onde ele deve ser vis√≠vel.

Quando um script cont√©m chamadas para fun√ß√µes `input.*()`, uma aba "_Inputs_" ("_Entradas_") aparece na caixa de di√°logo "_Settings_" ("_Configura√ß√µes_").

![Inputs](./imgs/Inputs-Introduction-1.png)

No fluxo de execu√ß√£o de um script, as entradas s√£o processadas quando o script j√° est√° em um gr√°fico e um usu√°rio altera valores na aba "_Inputs_" ("_Entradas_"). As mudan√ßas desencadeiam uma reexecu√ß√£o do script em todas as barras do gr√°fico, ent√£o quando um usu√°rio altera um valor de entrada, seu script recalcula usando esse novo valor.


# Fun√ß√µes de Input

As seguintes fun√ß√µes de _input_ (_entrada_) est√£o dispon√≠veis:

- [input()](https://br.tradingview.com/pine-script-reference/v5/#fun_input)
- [input.int()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}int)
- [input.float()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}float)
- [input.bool()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}bool)
- [input.color()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}color)
- [input.string()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}string)
- [input.timeframe()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}timeframe)
- [input.symbol()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}symbol)
- [input.price()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}price)
- [input.source()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}source)
- [input.session()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}session)
- [input.time()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}time)

Um _widget_ de _entrada_ espec√≠fico √© criado na aba "_Inputs_" ("_Entradas_") para aceitar cada tipo de entrada. A menos que especificado de outra forma na chamada `input.*()`, cada entrada aparece em uma nova linha da aba "_Inputs_" ("_Entradas_"), na ordem em que as chamadas `input.*()` aparecem no script.

√â recomendado pelo [Guia de Estilo](./000_style_guide.md) colocar as chamadas `input.*()` no in√≠cio do script.

As defini√ß√µes das fun√ß√µes de entrada normalmente cont√™m muitos par√¢metros, permitindo controlar o valor padr√£o das entradas, seus limites e sua organiza√ß√£o na aba "_Inputs_" ("_Entradas_").

Uma chamada `input.*()` sendo apenas outra chamada de fun√ß√£o no Pine Script, seu resultado pode ser combinado com operadores [aritm√©ticos](./04_05_operadores.md#operadores-aritm√©ticos), [compara√ß√£o](./04_05_operadores.md#operadores-de-compara√ß√£o), [l√≥gicos](./04_05_operadores.md#operadores-l√≥gicos) ou [tern√°rios](./04_05_operadores.md#operador-tern√°rio-) para formar uma express√£o a ser atribu√≠da √† vari√°vel. Aqui, o resultado da chamada [input.string()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}string) √© comparado √† string "`On`". O resultado da express√£o √© ent√£o armazenado na vari√°vel `plotDisplayInput`. Como essa vari√°vel cont√©m um valor `true` ou `false`, ela √© do tipo "input bool":

```c
//@version=5
indicator("Input in an expression`", "", true)
bool plotDisplayInput = input.string("On", "Plot Display", options = ["On", "Off"]) == "On"
plot(plotDisplayInput ? close : na)
```

Todos os valores retornados pelas fun√ß√µes `input.*()`, exceto os de "source", s√£o valores qualificados como "input". Veja a se√ß√£o sobre [qualificadores de tipo](./04_09_tipagem_do_sistema.md#qualificadores) para mais informa√ß√µes.


# Par√¢metros das Fun√ß√µes de Input

Os par√¢metros comuns a todas as fun√ß√µes de entrada s√£o: `defval`, `title`, `tooltip`, `inline` e `group`. Alguns par√¢metros s√£o usados por outras fun√ß√µes de entrada: `options`, `minval`, `maxval`, `step` e `confirm`.

Todos esses par√¢metros esperam argumentos "const" (exceto se for uma entrada usada para uma "source", que retorna um resultado "series float"). Isso significa que devem ser conhecidos no momento da compila√ß√£o e n√£o podem mudar durante a execu√ß√£o do script. Como o resultado de uma fun√ß√£o `input.*()` √© sempre qualificado como "input" ou "series", segue-se que o resultado de uma chamada de fun√ß√£o `input.*()` n√£o pode ser usado como argumento em uma chamada `input.*()` subsequente porque o qualificador "input" √© mais forte que "const".

Revis√£o de cada par√¢metro:

- `defval` √© o primeiro par√¢metro de todas as fun√ß√µes de entrada. √â o valor padr√£o que aparecer√° no widget de entrada. Requer um argumento do tipo de valor de entrada para o qual a fun√ß√£o √© usada.
- `title` requer um argumento "const string". √â o _label_ (_r√≥tulo_) do campo.
- `tooltip` requer um argumento "const string". Quando o par√¢metro √© usado, um √≠cone de ponto de interroga√ß√£o aparecer√° √† direita do campo. Quando os usu√°rios passarem o cursor sobre ele, o texto do _tooltip_ (_dica de ferramenta_) aparecer√°. Note que, se v√°rios campos de entrada forem agrupados em uma linha usando `inline`, o _tooltip_ (_dica de ferramenta_) sempre aparecer√° √† direita do campo mais √† direita e exibir√° o texto do √∫ltimo argumento `tooltip` usado na linha. Quebras de linha (`\n`) s√£o suportadas na string do argumento.
- `inline` requer um argumento "const string". Usar o mesmo argumento para o par√¢metro em v√°rias chamadas `input.*()` agrupar√° seus widgets de entrada na mesma linha. H√° um limite para a largura que a aba "_Inputs_" pode expandir, ent√£o uma quantidade limitada de campos de entrada pode ser ajustada em uma linha. Usar uma chamada `input.*()` com um argumento √∫nico para `inline` tem o efeito de trazer o campo de entrada para a esquerda, imediatamente ap√≥s o _label_, ignorando o alinhamento padr√£o √† esquerda de todos os campos de entrada usado quando nenhum argumento `inline` √© usado.
- `group` requer um argumento "const string". √â usado para agrupar qualquer n√∫mero de entradas na mesma se√ß√£o. A string usada como argumento `group` se torna o t√≠tulo da se√ß√£o. Todas as chamadas `input.*()` a serem agrupadas devem usar a mesma string para seu argumento de `group`.
- `options` requer uma lista de elementos separados por v√≠rgulas e delimitados por colchetes (por exemplo, `["ON", "OFF"]`). √â usado para criar um menu suspenso (_dropdown_) que oferece os elementos da lista na forma de sele√ß√µes de menu. Apenas um item do menu pode ser selecionado. Quando uma lista de `options` √© usada, o valor `defval` deve ser um dos elementos da lista. Quando `options` √© usado em fun√ß√µes de entrada que permitem `minval`, `maxval` ou `step`, esses par√¢metros n√£o podem ser usados simultaneamente.
- `minval` requer um argumento "const int/float", dependendo do tipo de valor de `defval`. √â o valor m√≠nimo v√°lido para o campo de entrada.
- `maxval` requer um argumento "const int/float", dependendo do tipo de valor de `defval`. √â o valor m√°ximo v√°lido para o campo de entrada.
- `step` √© o incremento pelo qual o valor do campo se mover√° quando as setas para cima/baixo do widget forem usadas.
- `confirm` requer um argumento "const bool" (`true` ou `false`). Esse par√¢metro afeta o comportamento do script quando ele √© adicionado a um gr√°fico. Chamadas `input.*()` usando `confirm = true` far√£o com que a aba "_Settings/Inputs_" ("_Configura√ß√µes/Entradas_") apare√ßa quando o script for adicionado ao gr√°fico. `confirm` √© √∫til para garantir que os usu√°rios configurem um campo espec√≠fico.

Os par√¢metros `minval`, `maxval` e `step` s√£o somente presentes nas assinaturas das fon√ß√µes [input.int()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}int) e [input.float()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}float)


# Tipos de Input

As pr√≥ximas se√ß√µes explicam o que cada fun√ß√£o de entrada faz. No decorrer, ser√£o exploradas as diferentes maneiras de usar as fun√ß√µes de entrada e organizar sua exibi√ß√£o.

## Input Simples

[input()](https://br.tradingview.com/pine-script-reference/v5/#fun_input) √© uma fun√ß√£o simples e gen√©rica que suporta os tipos fundamentais do Pine Script: "int", "float", "bool", "color" e "string". Ela tamb√©m suporta entradas "source", que s√£o valores relacionados ao pre√ßo, como [close](https://br.tradingview.com/pine-script-reference/v5/#var_close), [hl2](https://br.tradingview.com/pine-script-reference/v5/#hl2), [hlc3](https://br.tradingview.com/pine-script-reference/v5/#var_hlc3) e [hlcc4](https://br.tradingview.com/pine-script-reference/v5/#var_hlcc4), ou que podem ser usadas para receber o valor de sa√≠da de outro script.

Sua assinatura √©:

```c
input(defval, title, tooltip, inline, group) ‚Üí input int/float/bool/color/string | series float
```

A fun√ß√£o detecta automaticamente o tipo de entrada analisando o tipo do argumento `defval` usado na chamada da fun√ß√£o. Este script mostra todos os tipos suportados e o tipo qualificado retornado pela fun√ß√£o quando usada com argumentos `defval` de diferentes tipos:

```c
//@version=5
indicator("`input()`", "", true)
a = input(1, "input int")
b = input(1.0, "input float")
c = input(true, "input bool")
d = input(color.orange, "input color")
e = input("1", "input string")
f = input(close, "series float")
plot(na)
```

## Input Integer

Existem duas assinaturas para a fun√ß√£o [input.int()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}int); uma quando `options` n√£o √© usada e outra quando √© usada:

```c
input.int(defval, title, minval, maxval, step, tooltip, inline, group, confirm) ‚Üí input int
input.int(defval, title, options, tooltip, inline, group, confirm) ‚Üí input int
```

Esta chamada usa o par√¢metro `options` para propor uma lista predefinida de comprimentos para a MA:

```c
//@version=5
indicator("MA", "", true)
maLengthInput = input.int(10, options = [3, 5, 7, 10, 14, 20, 50, 100, 200])
ma = ta.sma(close, maLengthInput)
plot(ma)
```

Esta usa o par√¢metro `minval` para limitar o _length_ (_comprimento_):

```c
//@version=5
indicator("MA", "", true)
maLengthInput = input.int(10, minval = 2)
ma = ta.sma(close, maLengthInput)
plot(ma)
```

A vers√£o com a lista `options` usa um menu suspenso para seu widget. Quando o par√¢metro `options` n√£o √© usado, um widget de entrada simples √© usado para inserir o valor.

![Input integer](./imgs/Inputs-InputTypes-02.png)

## Input Float

Existem duas assinaturas para a fun√ß√£o [input.float()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}float); uma quando `options` n√£o √© usada e outra quando √© usada:

```c
input.float(defval, title, minval, maxval, step, tooltip, inline, group, confirm) ‚Üí input int
input.float(defval, title, options, tooltip, inline, group, confirm) ‚Üí input int
```

Aqui, uma entrada "float" √© usada para o fator multiplicador do desvio padr√£o, para calcular as Bandas de Bollinger:

```c
//@version=5
indicator("MA", "", true)
maLengthInput = input.int(10, minval = 1)
bbFactorInput = input.float(1.5, minval = 0, step = 0.5)
ma      = ta.sma(close, maLengthInput)
bbWidth = ta.stdev(ma, maLengthInput) * bbFactorInput
bbHi    = ma + bbWidth
bbLo    = ma - bbWidth
plot(ma)
plot(bbHi, "BB Hi", color.gray)
plot(bbLo, "BB Lo", color.gray)
```

Os widgets de entrada para floats s√£o semelhantes aos usados para entradas inteiras.

![Input float](./imgs/Inputs-InputTypes-03.png)

## Input Boolean

Continuando a desenvolver o script, desta vez adicionando uma entrada booleana para permitir que os usu√°rios alternem a exibi√ß√£o das BBs:

```c
//@version=5
indicator("MA", "", true)
maLengthInput = input.int(10,    "MA length", minval = 1)
bbFactorInput = input.float(1.5, "BB factor", inline = "01", minval = 0, step = 0.5)
showBBInput   = input.bool(true, "Show BB",   inline = "01")
ma      = ta.sma(close, maLengthInput)
bbWidth = ta.stdev(ma, maLengthInput) * bbFactorInput
bbHi    = ma + bbWidth
bbLo    = ma - bbWidth
plot(ma, "MA", color.aqua)
plot(showBBInput ? bbHi : na, "BB Hi", color.gray)
plot(showBBInput ? bbLo : na, "BB Lo", color.gray)
```

__Note que:__

- Foi adicionada uma entrada usando [input.bool()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}bool) para definir o valor de `showBBInput`.
- O par√¢metro `inline` foi usado nessa entrada e na de `bbFactorInput` para coloc√°-las na mesma linha. O argumento `"01"` foi usado em ambos os casos. √â assim que o compilador do Pine Script reconhece que elas pertencem √† mesma linha. A string espec√≠fica usada como argumento n√£o √© importante e n√£o aparece em nenhum lugar na aba "_Inputs_" ("_Entradas_"); √© usada apenas para identificar quais entradas v√£o na mesma linha.
- Os argumentos `title` das chamadas `input.*()` foram alinhados verticalmente para facilitar a leitura.
- A vari√°vel `showBBInput` √© usada nas duas chamadas [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) para plotar condicionalmente. Quando o usu√°rio desmarca a caixa de sele√ß√£o da entrada `showBBInput`, o valor da vari√°vel se torna `false`. Quando isso acontece, as chamadas [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) plotam o valor [na](https://br.tradingview.com/pine-script-reference/v5/#var_na), que n√£o exibe nada. O valor padr√£o da entrada √© `true`, ent√£o as BBs s√£o plotadas por padr√£o.
- Como o par√¢metro `inline` √© usado para a vari√°vel `bbFactorInput`, seu campo de entrada na aba "_Inputs_" ("_Entradas_") n√£o se alinha verticalmente com o de `maLengthInput`, em que n√£o usa `inline`.

![Input boolean](./imgs/Inputs-InputTypes-04.png)

## Input Color

Como explicado na se√ß√£o sele√ß√£o de cores atrav√©s das configura√ß√µes do script da p√°gina "[Cores](./05_07_cores.md)", as sele√ß√µes de cores que geralmente aparecem na aba "_Settings_/_Style_" ("_Configura√ß√µes_/_Estilo_") nem sempre est√£o dispon√≠veis. Quando isso ocorre, os usu√°rios do script n√£o ter√£o meios de alterar as cores usadas pelo seu script. Para esses casos, √© essencial fornecer entradas de cor se for desejado que as cores do script sejam modific√°veis atrav√©s das "Configura√ß√µes" do script. Em vez de usar a aba "_Settings_/_Style_" ("_Configura√ß√µes_/_Estilo_") para alterar cores, os usu√°rios do script poder√£o alterar as cores usando chamadas para [input.color()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}color).

Suponha que seja necess√°rio plotar as BBs em um tom mais claro quando os valores [high](https://br.tradingview.com/pine-script-reference/v5/#var_high) e [low](https://br.tradingview.com/pine-script-reference/v5/#var_low) forem maiores/menores do que as BBs.

Poderia-se usar um c√≥digo como este para criar as cores:

```c
bbHiColor = color.new(color.gray, high > bbHi ? 60 : 0)
bbLoColor = color.new(color.gray, low  < bbLo ? 60 : 0)
```

Quando componentes de cor din√¢micos (ou "series"), como a transpar√™ncia aqui, s√£o usados, os widgets de cor na aba "Configura√ß√µes/Estilo" n√£o aparecer√£o mais. Criando-se os pr√≥prios widgets, eles aparecer√£o na aba "_Inputs_" ("_Entradas_"):

```c
//@version=5
indicator("MA", "", true)
maLengthInput = input.int(10,           "MA length", inline = "01", minval = 1)
maColorInput  = input.color(color.aqua, "",          inline = "01")
bbFactorInput = input.float(1.5,        "BB factor", inline = "02", minval = 0, step = 0.5)
bbColorInput  = input.color(color.gray, "",          inline = "02")
showBBInput   = input.bool(true,        "Show BB",   inline = "02")
ma      = ta.sma(close, maLengthInput)
bbWidth = ta.stdev(ma, maLengthInput) * bbFactorInput
bbHi    = ma + bbWidth
bbLo    = ma - bbWidth
bbHiColor = color.new(bbColorInput, high > bbHi ? 60 : 0)
bbLoColor = color.new(bbColorInput, low  < bbLo ? 60 : 0)
plot(ma, "MA", maColorInput)
plot(showBBInput ? bbHi : na, "BB Hi", bbHiColor, 2)
plot(showBBInput ? bbLo : na, "BB Lo", bbLoColor, 2)
```

__Note que:__

- Foram adicionadas duas chamadas para input.color() para obter os valores das vari√°veis maColorInput e bbColorInput. maColorInput √© usado diretamente na chamada plot(ma, "MA", maColorInput), e bbColorInput √© usado para construir as vari√°veis bbHiColor e bbLoColor, que modulam a transpar√™ncia usando a posi√ß√£o do pre√ßo em rela√ß√£o √†s BBs. Um valor condicional √© usado para o valor de transp na chamada color.new(), para gerar diferentes transpar√™ncias da mesma cor base.
- N√£o √© usado um argumento title para as novas entradas de cor porque elas est√£o na mesma linha que outras entradas, permitindo que os usu√°rios entendam a quais plots se aplicam.
- Os argumentos inline foram reorganizados para refletir o fato de que as entradas est√£o agrupadas em duas linhas distintas.

![Input color](./imgs/Inputs-InputTypes-05.png)

## Input Timeframe

Entradas de timeframe podem ser √∫teis quando se deseja em poder alterar o timeframe usado para calcular valores em seus scripts.

Eliminando as BBs das se√ß√µes anteriores e adicionando uma entrada de timeframe a um script simples de MA:

```c
//@version=5
indicator("MA", "", true)
tfInput = input.timeframe("D", "Timeframe")
ma = ta.sma(close, 20)
securityNoRepaint(sym, tf, src) =>
    request.security(sym, tf, src[barstate.isrealtime ? 1 : 0])[barstate.isrealtime ? 0 : 1]
maHTF = securityNoRepaint(syminfo.tickerid, tfInput, ma)
plot(maHTF, "MA", color.aqua)
```

__Note que:__

- A fun√ß√£o [input.timeframe()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}timeframe) √© usada para receber a entrada de timeframe.
- A fun√ß√£o cria um widget do menu suspenso onde alguns timeframes padr√£o s√£o propostos. A lista de timeframes tamb√©m inclui qualquer timeframe que tenha sido favoritado na interface do usu√°rio do gr√°fico.
- `tfInput` √© usado na chamada [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request{dot}security). Tamb√©m √© usado `gaps = barmerge.gaps_on` na chamada, para que a fun√ß√£o s√≥ retorne dados quando o timeframe superior for completado.

![Input timeframe](./imgs/Inputs-InputTypes-06.png)

## Input Symbol

A fun√ß√£o [input.symbol()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}symbol) cria um widget que permite aos usu√°rios pesquisar e selecionar s√≠mbolos, assim como fariam na interface do usu√°rio do gr√°fico.

Adicionando uma entrada de s√≠mbolo ao script:

```c
//@version=5
indicator("MA", "", true)
tfInput = input.timeframe("D", "Timeframe")
symbolInput = input.symbol("", "Symbol")
ma = ta.sma(close, 20)
securityNoRepaint(sym, tf, src) =>
    request.security(sym, tf, src[barstate.isrealtime ? 1 : 0])[barstate.isrealtime ? 0 : 1]
maHTF = securityNoRepaint(symbolInput, tfInput, ma)
plot(maHTF, "MA", color.aqua)
```

__Note que:__

- O argumento `defval` usado √© uma string vazia. Isso faz com que [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request{dot}security), onde a vari√°vel `symbolInput` contendo essa entrada √© usada, para utilizar o s√≠mbolo do gr√°fico por padr√£o. Se o usu√°rio selecionar outro s√≠mbolo e quiser retornar ao valor padr√£o usando o s√≠mbolo do gr√°fico, ele precisar√° usar a sele√ß√£o "_Reset Settings_" ("_Redefinir Configura√ß√µes_") do menu "_Defaults_" ("_Padr√µes_") da aba "_Inputs_" ("_Entradas_").
- A fun√ß√£o definida pelo usu√°rio `securityNoRepaint()` √© usada para [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request{dot}security) de maneira que n√£o cause repaint; ela s√≥ retorna valores quando o timeframe superior √© completado.

## Input Session

Entradas de sess√£o s√£o √∫teis para coletar valores de in√≠cio e t√©rmino para per√≠odos de tempo. A fun√ß√£o incorporada [input.session()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}session) cria um widget de entrada permitindo que os usu√°rios especifiquem o in√≠cio e o fim de uma sess√£o. As sele√ß√µes podem ser feitas usando um menu suspenso ou inserindo valores de tempo no formato "hh:mm".

O valor retornado por [input.session()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}session) √© uma string v√°lida no formato de sess√£o. Veja a p√°gina do manual sobre [sess√µes](./05_17_sessoes.md) para maiores informa√ß√µes.

As informa√ß√µes de sess√£o tamb√©m podem conter informa√ß√µes sobre os dias em que a sess√£o √© v√°lida. Aqui, a chamada da fun√ß√£o [input.string()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}string) √© usada para inserir essas informa√ß√µes de dias:

```c
//@version=5
indicator("Session input", "", true)
string sessionInput = input.session("0600-1700", "Session")
string daysInput = input.string("1234567", tooltip = "1 = Sunday, 7 = Saturday")
sessionString = sessionInput + ":" + daysInput
inSession = not na(time(timeframe.period, sessionString))
bgcolor(inSession ? color.silver : na)
```

__Note que:__

- Este script prop√µe uma sess√£o padr√£o de "0600-1700".
- A chamada [input.string()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}string) usa uma dica de ferramenta para fornecer aos usu√°rios ajuda sobre o formato a ser usado para inserir informa√ß√µes de dias.
- Uma string de sess√£o completa √© constru√≠da concatenando as duas strings que o script recebe como entradas.
- O tipo das duas entradas √© explicitamente declarado com a palavra-chave [string](https://br.tradingview.com/pine-script-reference/v5/#type_string) para deixar claro que essas vari√°veis conter√£o uma string.
- √â detectado se a barra do gr√°fico est√° na sess√£o definida pelo usu√°rio chamando [time()](https://br.tradingview.com/pine-script-reference/v5/#fun_time) com a string da sess√£o. Se o valor de [time](https://br.tradingview.com/pine-script-reference/v5/#var_time) da barra atual (o tempo no [open](https://br.tradingview.com/pine-script-reference/v5/#var_open) da barra) n√£o estiver na sess√£o, [time()](https://br.tradingview.com/pine-script-reference/v5/#fun_time) retorna [na](https://br.tradingview.com/pine-script-reference/v5/#var_na), ent√£o `inSession` ser√° verdadeiro sempre que [time()](https://br.tradingview.com/pine-script-reference/v5/#fun_time) retornar um valor que n√£o seja [na](https://br.tradingview.com/pine-script-reference/v5/#var_na).

![Input session](./imgs/Inputs-InputTypes-07.png)

## Input Source

Entradas da fonte, [input.source()](https://br.tradingview.com/pine-script-reference/v5/#fun_input.source), s√£o √∫teis para fornecer uma sele√ß√£o de dois tipos de fontes:

- Valores de pre√ßo, a saber: [open](https://br.tradingview.com/pine-script-reference/v5/#var_open), [high](https://br.tradingview.com/pine-script-reference/v5/#var_high), [low](https://br.tradingview.com/pine-script-reference/v5/#var_low), [close](https://br.tradingview.com/pine-script-reference/v5/#var_close), [hl2](https://br.tradingview.com/pine-script-reference/v5/#var_hl2), [hlc3](https://br.tradingview.com/pine-script-reference/v5/#var_hlc3) e [ohlc4](https://br.tradingview.com/pine-script-reference/v5/#var_ohlc4).
- Os valores plotados por outros scripts no gr√°fico. Isso pode ser √∫til para "_link_" ("_vincular_") dois ou mais scripts juntos, enviando a sa√≠da de um como entrada para outro script.

Este script simplesmente plota a sele√ß√£o da fonte do usu√°rio. Prop√µe-se o valor [high](https://br.tradingview.com/pine-script-reference/v5/#var_high) como padr√£o:

```c
//@version=5
indicator("Source input", "", true)
srcInput = input.source(high, "Source")
plot(srcInput, "Src", color.new(color.purple, 70), 6)
```

Isso mostra um gr√°fico onde, al√©m do script, foi carregado um indicador "Arnaud Legoux Moving Average". Veja aqui como o widget de entrada da fonte do script √© usado para selecionar a sa√≠da do script ALMA como entrada no script. Como script plota essa fonte em uma linha grossa de cor roxo claro, √© poss√≠vel ver os plots dos dois scripts sobrepostos porque eles plotam o mesmo valor:

![Input source](./imgs/Inputs-InputTypes-08.png)

## Input Time

Entradas de tempo usam a fun√ß√£o [input.time()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}time). A fun√ß√£o retorna um hor√°rio Unix em milissegundos (veja a p√°gina [Tempo](./05_21_time.md) para mais informa√ß√µes). Esse tipo de dado tamb√©m cont√©m informa√ß√µes de data, ent√£o a fun√ß√£o [input.time()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}time) _retorna um hor√°rio __e__ uma data_. Por isso, seu widget permite a sele√ß√£o de ambos.

Aqui, o tempo da barra √© testado contra um valor de entrada, e uma seta √© plotada quando o tempo √© maior:

```c
//@version=5
indicator("Time input", "T", true)
timeAndDateInput = input.time(timestamp("1 Aug 2021 00:00 +0300"), "Date and time")
barIsLater = time > timeAndDateInput
plotchar(barIsLater, "barIsLater", "ü†Ü", location.top, size = size.tiny)
```

Observe que o valor `defval` usado √© uma chamada para a fun√ß√£o [timestamp()](https://br.tradingview.com/pine-script-reference/v5/#fun_timestamp).


# Outros Recursos que Afetam Inputs

Alguns par√¢metros da fun√ß√£o [indicator()](https://br.tradingview.com/pine-script-reference/v5/#fun_indicator), quando usados, ir√£o preencher a aba "_Inputs_" ("_Entradas_") do script com um campo. Os par√¢metros s√£o `timeframe` e `timeframe_gaps`.

Exemplo:

```c
//@version=5
indicator("MA", "", true, timeframe = "D", timeframe_gaps = false)
plot(ta.vwma(close, 10))
```

![Outros recursos que afetam inputs](./imgs/Inputs-OtherFeaturesAffectingInputs-03.png)


# Dicas

O design das entradas do seu script tem um impacto importante na usabilidade dos seus scripts. Entradas bem projetadas s√£o mais intuitivas e proporcionam uma melhor experi√™ncia do usu√°rio:

- Escolha _labels_ claros e concisos (o argumento `title` da sua entrada).
- Escolha os valores padr√£o com cuidado.
- Forne√ßa valores `minval` e `maxval` que evitem que seu c√≥digo produza resultados inesperados, por exemplo, limite o valor m√≠nimo dos comprimentos para 1 ou 2, dependendo do tipo de MA que est√° sendo usado.
- Forne√ßa um valor `step` que seja congruente com o valor que est√° sendo capturado. Passos de 5 podem ser mais √∫teis em uma faixa de 0-200, por exemplo, ou passos de 0,05 em uma escala de 0,0-1,0.
- Agrupe entradas relacionadas na mesma linha usando `inline`; cores de alta e baixa, por exemplo, ou a largura e a cor de uma linha.
- Quando houver muitas entradas, agrupe-as em se√ß√µes significativas usando `group`. Coloque as se√ß√µes mais importantes no topo.
- Fa√ßa o mesmo para entradas individuais __dentro__ das se√ß√µes.

Pode ser vantajoso alinhar verticalmente diferentes argumentos de m√∫ltiplas chamadas `input.*()` no seu c√≥digo. Quando for necess√°rio fazer altera√ß√µes globais, isso permitir√° usar o recurso de multi-cursor do Editor para operar em todas as linhas de uma vez.

Por vezes √© necess√°rio usar espa√ßos Unicode para conseguir o alinhamento ideal nas entradas.

Exemplo:

```c
//@version=5
indicator("Aligned inputs", "", true)

var GRP1 = "Not aligned"
ma1SourceInput   = input(close, "MA source",     inline = "11", group = GRP1)
ma1LengthInput   = input(close, "Length",        inline = "11", group = GRP1)
long1SourceInput = input(close, "Signal source", inline = "12", group = GRP1)
long1LengthInput = input(close, "Length",        inline = "12", group = GRP1)

var GRP2 = "Aligned"
// The three spaces after "MA source" are Unicode EN spaces (U+2002).
ma2SourceInput   = input(close, "MA source‚ÄÇ‚ÄÇ‚ÄÇ",  inline = "21", group = GRP2)
ma2LengthInput   = input(close, "Length",        inline = "21", group = GRP2)
long2SourceInput = input(close, "Signal source", inline = "22", group = GRP2)
long2LengthInput = input(close, "Length",        inline = "22", group = GRP2)

plot(ta.vwma(close, 10))
```

![Dicas](./imgs/Inputs-Tips-1.png)

__Note que:__

- O par√¢metro `group` √© usado para distinguir entre as duas se√ß√µes de entradas. Uma constante √© usada para manter o nome dos grupos. Dessa forma, se for decidido mudar o nome do grupo, ser√° necess√°rio mudar apenas em um lugar.
- Os widgets de entrada da primeira se√ß√£o n√£o se alinham verticalmente. Est√° sendo usado `inline`, que coloca os widgets de entrada imediatamente √† direita do _label_. Pelos _labels_ para as entradas `ma1SourceInput` e `long1SourceInput` t√™m comprimentos diferentes, os _labels_ est√£o em posi√ß√µes _y_ diferentes.
- Para compensar o desalinhamento, o argumento `title` na linha de `ma2SourceInput` √© preenchido com tr√™s espa√ßos EN Unicode (U+2002). Espa√ßos Unicode s√£o necess√°rios porque espa√ßos comuns seriam removidos do _label_. Alinhamento preciso pode ser alcan√ßado combinando diferentes quantidades e tipos de espa√ßos Unicode. Veja aqui uma lista de [espa√ßos Unicode](https://jkorpela.fi/chars/spaces.html) de diferentes larguras.
