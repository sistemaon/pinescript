
# Inputs (_Entradas_)

Os _Inputs_ ("_Entradas_") permitem que scripts recebam valores que os usuÃ¡rios podem alterar. UsÃ¡-las para valores-chave torna os scripts mais adaptÃ¡veis Ã s preferÃªncias do usuÃ¡rio.

O script a seguir plota uma [mÃ©dia mÃ³vel simples (SMA)](https://br.tradingview.com/support/solutions/43000502589) de 20 perÃ­odos usando `ta.sma(close, 20)`. Embora seja simples de escrever, nÃ£o Ã© muito flexÃ­vel, pois essa MA especÃ­fica Ã© tudo o que ele sempre plotarÃ¡:

```c
//@version=5
indicator("MA", "", true)
plot(ta.sma(close, 20))
```

Se, em vez disso, o script for escrito desta maneira, ele se torna muito mais flexÃ­vel, pois os usuÃ¡rios poderÃ£o selecionar a "_source_" ("_fonte_") e a "_length_" ("_comprimento_") que desejam usar para o cÃ¡lculo da MA:

```c
//@version=5
indicator("MA", "", true)
sourceInput = input(close, "Source")
lengthInput = input(20, "Length")
plot(ta.sma(sourceInput, lengthInput))
```

Entradas sÃ³ podem ser acessadas quando um script estÃ¡ sendo executado no grÃ¡fico. Os usuÃ¡rios do script acessam as entradas atravÃ©s da caixa de diÃ¡logo "_Settings_" ("_ConfiguraÃ§Ãµes_") do script, que pode ser alcanÃ§ada de uma das seguintes maneiras:

- Com um clique duplo no nome de um indicador no grÃ¡fico.
- Clicando com o botÃ£o direito do mouse no nome do script e escolhendo o item "_Settings_" ("_ConfiguraÃ§Ãµes_") no menu suspenso.
- Escolhendo o item "_Settings_" ("_ConfiguraÃ§Ãµes_") no Ã­cone do menu "_More_" ("_Mais_") (trÃªs pontos) que aparece ao passar o cursor sobre o nome do indicador no grÃ¡fico.
- Com um clique duplo no nome do indicador na "_Data Window_" ("_Janela de Dados_") (quarto Ã­cone abaixo Ã  direita do grÃ¡fico).

A caixa de diÃ¡logo "_Settings_" ("_ConfiguraÃ§Ãµes_") sempre contÃ©m as abas "_Style_" ("_Estilo_") e "_Visibility_" ("_Visibilidade_"), que permitem aos usuÃ¡rios especificar suas preferÃªncias sobre os visuais do script e os timeframes do grÃ¡fico onde ele deve ser visÃ­vel.

Quando um script contÃ©m chamadas para funÃ§Ãµes `input.*()`, uma aba "_Inputs_" ("_Entradas_") aparece na caixa de diÃ¡logo "_Settings_" ("_ConfiguraÃ§Ãµes_").

![Inputs](./imgs/Inputs-Introduction-1.png)

No fluxo de execuÃ§Ã£o de um script, as entradas sÃ£o processadas quando o script jÃ¡ estÃ¡ em um grÃ¡fico e um usuÃ¡rio altera valores na aba "_Inputs_" ("_Entradas_"). As mudanÃ§as desencadeiam uma reexecuÃ§Ã£o do script em todas as barras do grÃ¡fico, entÃ£o quando um usuÃ¡rio altera um valor de entrada, seu script recalcula usando esse novo valor.


# FunÃ§Ãµes de Input

As seguintes funÃ§Ãµes de _input_ (_entrada_) estÃ£o disponÃ­veis:

- [input()](https://br.tradingview.com/pine-script-reference/v5/#fun_input)
- [input.int()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}int)
- [input.float()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}float)
- [input.bool()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}bool)
- [input.color()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}color)
- [input.string()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}string)
- [input.timeframe()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}timeframe)
- [input.symbol()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}symbol)
- [input.price()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}price)
- [input.source()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}source)
- [input.session()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}session)
- [input.time()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}time)

Um _widget_ de _entrada_ especÃ­fico Ã© criado na aba "_Inputs_" ("_Entradas_") para aceitar cada tipo de entrada. A menos que especificado de outra forma na chamada `input.*()`, cada entrada aparece em uma nova linha da aba "_Inputs_" ("_Entradas_"), na ordem em que as chamadas `input.*()` aparecem no script.

Ã‰ recomendado pelo [Guia de Estilo](./000_style_guide.md) colocar as chamadas `input.*()` no inÃ­cio do script.

As definiÃ§Ãµes das funÃ§Ãµes de entrada normalmente contÃªm muitos parÃ¢metros, permitindo controlar o valor padrÃ£o das entradas, seus limites e sua organizaÃ§Ã£o na aba "_Inputs_" ("_Entradas_").

Uma chamada `input.*()` sendo apenas outra chamada de funÃ§Ã£o no Pine Script, seu resultado pode ser combinado com operadores [aritmÃ©ticos](./04_05_operadores.md#operadores-aritmÃ©ticos), [comparaÃ§Ã£o](./04_05_operadores.md#operadores-de-comparaÃ§Ã£o), [lÃ³gicos](./04_05_operadores.md#operadores-lÃ³gicos) ou [ternÃ¡rios](./04_05_operadores.md#operador-ternÃ¡rio-) para formar uma expressÃ£o a ser atribuÃ­da Ã  variÃ¡vel. Aqui, o resultado da chamada [input.string()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}string) Ã© comparado Ã  string "`On`". O resultado da expressÃ£o Ã© entÃ£o armazenado na variÃ¡vel `plotDisplayInput`. Como essa variÃ¡vel contÃ©m um valor `true` ou `false`, ela Ã© do tipo "input bool":

```c
//@version=5
indicator("Input in an expression`", "", true)
bool plotDisplayInput = input.string("On", "Plot Display", options = ["On", "Off"]) == "On"
plot(plotDisplayInput ? close : na)
```

Todos os valores retornados pelas funÃ§Ãµes `input.*()`, exceto os de "source", sÃ£o valores qualificados como "input". Veja a seÃ§Ã£o sobre [qualificadores de tipo](./04_09_tipagem_do_sistema.md#qualificadores) para mais informaÃ§Ãµes.


# ParÃ¢metros das FunÃ§Ãµes de Input

Os parÃ¢metros comuns a todas as funÃ§Ãµes de entrada sÃ£o: `defval`, `title`, `tooltip`, `inline` e `group`. Alguns parÃ¢metros sÃ£o usados por outras funÃ§Ãµes de entrada: `options`, `minval`, `maxval`, `step` e `confirm`.

Todos esses parÃ¢metros esperam argumentos "const" (exceto se for uma entrada usada para uma "source", que retorna um resultado "series float"). Isso significa que devem ser conhecidos no momento da compilaÃ§Ã£o e nÃ£o podem mudar durante a execuÃ§Ã£o do script. Como o resultado de uma funÃ§Ã£o `input.*()` Ã© sempre qualificado como "input" ou "series", segue-se que o resultado de uma chamada de funÃ§Ã£o `input.*()` nÃ£o pode ser usado como argumento em uma chamada `input.*()` subsequente porque o qualificador "input" Ã© mais forte que "const".

RevisÃ£o de cada parÃ¢metro:

- `defval` Ã© o primeiro parÃ¢metro de todas as funÃ§Ãµes de entrada. Ã‰ o valor padrÃ£o que aparecerÃ¡ no widget de entrada. Requer um argumento do tipo de valor de entrada para o qual a funÃ§Ã£o Ã© usada.
- `title` requer um argumento "const string". Ã‰ o _label_ (_rÃ³tulo_) do campo.
- `tooltip` requer um argumento "const string". Quando o parÃ¢metro Ã© usado, um Ã­cone de ponto de interrogaÃ§Ã£o aparecerÃ¡ Ã  direita do campo. Quando os usuÃ¡rios passarem o cursor sobre ele, o texto do _tooltip_ (_dica de ferramenta_) aparecerÃ¡. Note que, se vÃ¡rios campos de entrada forem agrupados em uma linha usando `inline`, o _tooltip_ (_dica de ferramenta_) sempre aparecerÃ¡ Ã  direita do campo mais Ã  direita e exibirÃ¡ o texto do Ãºltimo argumento `tooltip` usado na linha. Quebras de linha (`\n`) sÃ£o suportadas na string do argumento.
- `inline` requer um argumento "const string". Usar o mesmo argumento para o parÃ¢metro em vÃ¡rias chamadas `input.*()` agruparÃ¡ seus widgets de entrada na mesma linha. HÃ¡ um limite para a largura que a aba "_Inputs_" pode expandir, entÃ£o uma quantidade limitada de campos de entrada pode ser ajustada em uma linha. Usar uma chamada `input.*()` com um argumento Ãºnico para `inline` tem o efeito de trazer o campo de entrada para a esquerda, imediatamente apÃ³s o _label_, ignorando o alinhamento padrÃ£o Ã  esquerda de todos os campos de entrada usado quando nenhum argumento `inline` Ã© usado.
- `group` requer um argumento "const string". Ã‰ usado para agrupar qualquer nÃºmero de entradas na mesma seÃ§Ã£o. A string usada como argumento `group` se torna o tÃ­tulo da seÃ§Ã£o. Todas as chamadas `input.*()` a serem agrupadas devem usar a mesma string para seu argumento de `group`.
- `options` requer uma lista de elementos separados por vÃ­rgulas e delimitados por colchetes (por exemplo, `["ON", "OFF"]`). Ã‰ usado para criar um menu suspenso (_dropdown_) que oferece os elementos da lista na forma de seleÃ§Ãµes de menu. Apenas um item do menu pode ser selecionado. Quando uma lista de `options` Ã© usada, o valor `defval` deve ser um dos elementos da lista. Quando `options` Ã© usado em funÃ§Ãµes de entrada que permitem `minval`, `maxval` ou `step`, esses parÃ¢metros nÃ£o podem ser usados simultaneamente.
- `minval` requer um argumento "const int/float", dependendo do tipo de valor de `defval`. Ã‰ o valor mÃ­nimo vÃ¡lido para o campo de entrada.
- `maxval` requer um argumento "const int/float", dependendo do tipo de valor de `defval`. Ã‰ o valor mÃ¡ximo vÃ¡lido para o campo de entrada.
- `step` Ã© o incremento pelo qual o valor do campo se moverÃ¡ quando as setas para cima/baixo do widget forem usadas.
- `confirm` requer um argumento "const bool" (`true` ou `false`). Esse parÃ¢metro afeta o comportamento do script quando ele Ã© adicionado a um grÃ¡fico. Chamadas `input.*()` usando `confirm = true` farÃ£o com que a aba "_Settings/Inputs_" ("_ConfiguraÃ§Ãµes/Entradas_") apareÃ§a quando o script for adicionado ao grÃ¡fico. `confirm` Ã© Ãºtil para garantir que os usuÃ¡rios configurem um campo especÃ­fico.

Os parÃ¢metros `minval`, `maxval` e `step` sÃ£o somente presentes nas assinaturas das fonÃ§Ãµes [input.int()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}int) e [input.float()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}float)


# Tipos de Input

As prÃ³ximas seÃ§Ãµes explicam o que cada funÃ§Ã£o de entrada faz. No decorrer, serÃ£o exploradas as diferentes maneiras de usar as funÃ§Ãµes de entrada e organizar sua exibiÃ§Ã£o.

## Input Simples

[input()](https://br.tradingview.com/pine-script-reference/v5/#fun_input) Ã© uma funÃ§Ã£o simples e genÃ©rica que suporta os tipos fundamentais do Pine Script: "int", "float", "bool", "color" e "string". Ela tambÃ©m suporta entradas "source", que sÃ£o valores relacionados ao preÃ§o, como [close](https://br.tradingview.com/pine-script-reference/v5/#var_close), [hl2](https://br.tradingview.com/pine-script-reference/v5/#hl2), [hlc3](https://br.tradingview.com/pine-script-reference/v5/#var_hlc3) e [hlcc4](https://br.tradingview.com/pine-script-reference/v5/#var_hlcc4), ou que podem ser usadas para receber o valor de saÃ­da de outro script.

Sua assinatura Ã©:

```c
input(defval, title, tooltip, inline, group) â†’ input int/float/bool/color/string | series float
```

A funÃ§Ã£o detecta automaticamente o tipo de entrada analisando o tipo do argumento `defval` usado na chamada da funÃ§Ã£o. Este script mostra todos os tipos suportados e o tipo qualificado retornado pela funÃ§Ã£o quando usada com argumentos `defval` de diferentes tipos:

```c
//@version=5
indicator("`input()`", "", true)
a = input(1, "input int")
b = input(1.0, "input float")
c = input(true, "input bool")
d = input(color.orange, "input color")
e = input("1", "input string")
f = input(close, "series float")
plot(na)
```

## Input Integer

Existem duas assinaturas para a funÃ§Ã£o [input.int()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}int); uma quando `options` nÃ£o Ã© usada e outra quando Ã© usada:

```c
input.int(defval, title, minval, maxval, step, tooltip, inline, group, confirm) â†’ input int
input.int(defval, title, options, tooltip, inline, group, confirm) â†’ input int
```

Esta chamada usa o parÃ¢metro `options` para propor uma lista predefinida de comprimentos para a MA:

```c
//@version=5
indicator("MA", "", true)
maLengthInput = input.int(10, options = [3, 5, 7, 10, 14, 20, 50, 100, 200])
ma = ta.sma(close, maLengthInput)
plot(ma)
```

Esta usa o parÃ¢metro `minval` para limitar o _length_ (_comprimento_):

```c
//@version=5
indicator("MA", "", true)
maLengthInput = input.int(10, minval = 2)
ma = ta.sma(close, maLengthInput)
plot(ma)
```

A versÃ£o com a lista `options` usa um menu suspenso para seu widget. Quando o parÃ¢metro `options` nÃ£o Ã© usado, um widget de entrada simples Ã© usado para inserir o valor.

![Input integer](./imgs/Inputs-InputTypes-02.png)

## Input Float

Existem duas assinaturas para a funÃ§Ã£o [input.float()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}float); uma quando `options` nÃ£o Ã© usada e outra quando Ã© usada:

```c
input.float(defval, title, minval, maxval, step, tooltip, inline, group, confirm) â†’ input int
input.float(defval, title, options, tooltip, inline, group, confirm) â†’ input int
```

Aqui, uma entrada "float" Ã© usada para o fator multiplicador do desvio padrÃ£o, para calcular as Bandas de Bollinger:

```c
//@version=5
indicator("MA", "", true)
maLengthInput = input.int(10, minval = 1)
bbFactorInput = input.float(1.5, minval = 0, step = 0.5)
ma      = ta.sma(close, maLengthInput)
bbWidth = ta.stdev(ma, maLengthInput) * bbFactorInput
bbHi    = ma + bbWidth
bbLo    = ma - bbWidth
plot(ma)
plot(bbHi, "BB Hi", color.gray)
plot(bbLo, "BB Lo", color.gray)
```

Os widgets de entrada para floats sÃ£o semelhantes aos usados para entradas inteiras.

![Input float](./imgs/Inputs-InputTypes-03.png)

## Input Boolean

Continuando a desenvolver o script, desta vez adicionando uma entrada booleana para permitir que os usuÃ¡rios alternem a exibiÃ§Ã£o das BBs:

```c
//@version=5
indicator("MA", "", true)
maLengthInput = input.int(10,    "MA length", minval = 1)
bbFactorInput = input.float(1.5, "BB factor", inline = "01", minval = 0, step = 0.5)
showBBInput   = input.bool(true, "Show BB",   inline = "01")
ma      = ta.sma(close, maLengthInput)
bbWidth = ta.stdev(ma, maLengthInput) * bbFactorInput
bbHi    = ma + bbWidth
bbLo    = ma - bbWidth
plot(ma, "MA", color.aqua)
plot(showBBInput ? bbHi : na, "BB Hi", color.gray)
plot(showBBInput ? bbLo : na, "BB Lo", color.gray)
```

Observe que:

- Foi adicionada uma entrada usando [input.bool()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}bool) para definir o valor de `showBBInput`.
- O parÃ¢metro `inline` foi usado nessa entrada e na de `bbFactorInput` para colocÃ¡-las na mesma linha. O argumento `"01"` foi usado em ambos os casos. Ã‰ assim que o compilador do Pine Script reconhece que elas pertencem Ã  mesma linha. A string especÃ­fica usada como argumento nÃ£o Ã© importante e nÃ£o aparece em nenhum lugar na aba "_Inputs_" ("_Entradas_"); Ã© usada apenas para identificar quais entradas vÃ£o na mesma linha.
- Os argumentos `title` das chamadas `input.*()` foram alinhados verticalmente para facilitar a leitura.
- A variÃ¡vel `showBBInput` Ã© usada nas duas chamadas [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) para plotar condicionalmente. Quando o usuÃ¡rio desmarca a caixa de seleÃ§Ã£o da entrada `showBBInput`, o valor da variÃ¡vel se torna `false`. Quando isso acontece, as chamadas [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) plotam o valor [na](https://br.tradingview.com/pine-script-reference/v5/#var_na), que nÃ£o exibe nada. O valor padrÃ£o da entrada Ã© `true`, entÃ£o as BBs sÃ£o plotadas por padrÃ£o.
- Como o parÃ¢metro `inline` Ã© usado para a variÃ¡vel `bbFactorInput`, seu campo de entrada na aba "_Inputs_" ("_Entradas_") nÃ£o se alinha verticalmente com o de `maLengthInput`, em que nÃ£o usa `inline`.

![Input boolean](./imgs/Inputs-InputTypes-04.png)

## Input Color

Como explicado na seÃ§Ã£o seleÃ§Ã£o de cores atravÃ©s das configuraÃ§Ãµes do script da pÃ¡gina "[Cores](./05_07_cores.md)", as seleÃ§Ãµes de cores que geralmente aparecem na aba "_Settings_/_Style_" ("_ConfiguraÃ§Ãµes_/_Estilo_") nem sempre estÃ£o disponÃ­veis. Quando isso ocorre, os usuÃ¡rios do script nÃ£o terÃ£o meios de alterar as cores usadas pelo seu script. Para esses casos, Ã© essencial fornecer entradas de cor se for desejado que as cores do script sejam modificÃ¡veis atravÃ©s das "ConfiguraÃ§Ãµes" do script. Em vez de usar a aba "_Settings_/_Style_" ("_ConfiguraÃ§Ãµes_/_Estilo_") para alterar cores, os usuÃ¡rios do script poderÃ£o alterar as cores usando chamadas para [input.color()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}color).

Suponha que seja necessÃ¡rio plotar as BBs em um tom mais claro quando os valores [high](https://br.tradingview.com/pine-script-reference/v5/#var_high) e [low](https://br.tradingview.com/pine-script-reference/v5/#var_low) forem maiores/menores do que as BBs.

Poderia-se usar um cÃ³digo como este para criar as cores:

```c
bbHiColor = color.new(color.gray, high > bbHi ? 60 : 0)
bbLoColor = color.new(color.gray, low  < bbLo ? 60 : 0)
```

Quando componentes de cor dinÃ¢micos (ou "series"), como a transparÃªncia aqui, sÃ£o usados, os widgets de cor na aba "ConfiguraÃ§Ãµes/Estilo" nÃ£o aparecerÃ£o mais. Criando-se os prÃ³prios widgets, eles aparecerÃ£o na aba "Entradas":

```c
//@version=5
indicator("MA", "", true)
maLengthInput = input.int(10,           "MA length", inline = "01", minval = 1)
maColorInput  = input.color(color.aqua, "",          inline = "01")
bbFactorInput = input.float(1.5,        "BB factor", inline = "02", minval = 0, step = 0.5)
bbColorInput  = input.color(color.gray, "",          inline = "02")
showBBInput   = input.bool(true,        "Show BB",   inline = "02")
ma      = ta.sma(close, maLengthInput)
bbWidth = ta.stdev(ma, maLengthInput) * bbFactorInput
bbHi    = ma + bbWidth
bbLo    = ma - bbWidth
bbHiColor = color.new(bbColorInput, high > bbHi ? 60 : 0)
bbLoColor = color.new(bbColorInput, low  < bbLo ? 60 : 0)
plot(ma, "MA", maColorInput)
plot(showBBInput ? bbHi : na, "BB Hi", bbHiColor, 2)
plot(showBBInput ? bbLo : na, "BB Lo", bbLoColor, 2)
```

Observe que:

- Foram adicionadas duas chamadas para input.color() para obter os valores das variÃ¡veis maColorInput e bbColorInput. maColorInput Ã© usado diretamente na chamada plot(ma, "MA", maColorInput), e bbColorInput Ã© usado para construir as variÃ¡veis bbHiColor e bbLoColor, que modulam a transparÃªncia usando a posiÃ§Ã£o do preÃ§o em relaÃ§Ã£o Ã s BBs. Um valor condicional Ã© usado para o valor de transp na chamada color.new(), para gerar diferentes transparÃªncias da mesma cor base.
- NÃ£o Ã© usado um argumento title para as novas entradas de cor porque elas estÃ£o na mesma linha que outras entradas, permitindo que os usuÃ¡rios entendam a quais plots se aplicam.
- Os argumentos inline foram reorganizados para refletir o fato de que as entradas estÃ£o agrupadas em duas linhas distintas.

![Input color](./imgs/Inputs-InputTypes-05.png)

## Input Timeframe

Entradas de timeframe podem ser Ãºteis quando se deseja em poder alterar o timeframe usado para calcular valores em seus scripts.

Eliminando as BBs das seÃ§Ãµes anteriores e adicionando uma entrada de timeframe a um script simples de MA:

```c
//@version=5
indicator("MA", "", true)
tfInput = input.timeframe("D", "Timeframe")
ma = ta.sma(close, 20)
securityNoRepaint(sym, tf, src) =>
    request.security(sym, tf, src[barstate.isrealtime ? 1 : 0])[barstate.isrealtime ? 0 : 1]
maHTF = securityNoRepaint(syminfo.tickerid, tfInput, ma)
plot(maHTF, "MA", color.aqua)
```

Observe que:

- A funÃ§Ã£o [input.timeframe()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}timeframe) Ã© usada para receber a entrada de timeframe.
- A funÃ§Ã£o cria um widget do menu suspenso onde alguns timeframes padrÃ£o sÃ£o propostos. A lista de timeframes tambÃ©m inclui qualquer timeframe que tenha sido favoritado na interface do usuÃ¡rio do grÃ¡fico.
- `tfInput` Ã© usado na chamada [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request{dot}security). TambÃ©m Ã© usado `gaps = barmerge.gaps_on` na chamada, para que a funÃ§Ã£o sÃ³ retorne dados quando o timeframe superior for completado.

![Input timeframe](./imgs/Inputs-InputTypes-06.png)

## Input Symbol

A funÃ§Ã£o [input.symbol()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}symbol) cria um widget que permite aos usuÃ¡rios pesquisar e selecionar sÃ­mbolos, assim como fariam na interface do usuÃ¡rio do grÃ¡fico.

Adicionando uma entrada de sÃ­mbolo ao script:

```c
//@version=5
indicator("MA", "", true)
tfInput = input.timeframe("D", "Timeframe")
symbolInput = input.symbol("", "Symbol")
ma = ta.sma(close, 20)
securityNoRepaint(sym, tf, src) =>
    request.security(sym, tf, src[barstate.isrealtime ? 1 : 0])[barstate.isrealtime ? 0 : 1]
maHTF = securityNoRepaint(symbolInput, tfInput, ma)
plot(maHTF, "MA", color.aqua)
```

Observe que:

- O argumento `defval` usado Ã© uma string vazia. Isso faz com que [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request{dot}security), onde a variÃ¡vel `symbolInput` contendo essa entrada Ã© usada, para utilizar o sÃ­mbolo do grÃ¡fico por padrÃ£o. Se o usuÃ¡rio selecionar outro sÃ­mbolo e quiser retornar ao valor padrÃ£o usando o sÃ­mbolo do grÃ¡fico, ele precisarÃ¡ usar a seleÃ§Ã£o "_Reset Settings_" ("_Redefinir ConfiguraÃ§Ãµes_") do menu "_Defaults_" ("_PadrÃµes_") da aba "_Inputs_" ("_Entradas_").
- A funÃ§Ã£o definida pelo usuÃ¡rio `securityNoRepaint()` Ã© usada para [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request{dot}security) de maneira que nÃ£o cause repaint; ela sÃ³ retorna valores quando o timeframe superior Ã© completado.

## Input Session

Entradas de sessÃ£o sÃ£o Ãºteis para coletar valores de inÃ­cio e tÃ©rmino para perÃ­odos de tempo. A funÃ§Ã£o incorporada [input.session()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}session) cria um widget de entrada permitindo que os usuÃ¡rios especifiquem o inÃ­cio e o fim de uma sessÃ£o. As seleÃ§Ãµes podem ser feitas usando um menu suspenso ou inserindo valores de tempo no formato "hh:mm".

O valor retornado por [input.session()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}session) Ã© uma string vÃ¡lida no formato de sessÃ£o. Veja a pÃ¡gina do manual sobre [sessÃµes](./05_17_sessoes.md) para maiores informaÃ§Ãµes.

As informaÃ§Ãµes de sessÃ£o tambÃ©m podem conter informaÃ§Ãµes sobre os dias em que a sessÃ£o Ã© vÃ¡lida. Aqui, a chamada da funÃ§Ã£o [input.string()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}string) Ã© usada para inserir essas informaÃ§Ãµes de dias:

```c
//@version=5
indicator("Session input", "", true)
string sessionInput = input.session("0600-1700", "Session")
string daysInput = input.string("1234567", tooltip = "1 = Sunday, 7 = Saturday")
sessionString = sessionInput + ":" + daysInput
inSession = not na(time(timeframe.period, sessionString))
bgcolor(inSession ? color.silver : na)
```

Observe que:

- Este script propÃµe uma sessÃ£o padrÃ£o de "0600-1700".
- A chamada [input.string()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}string) usa uma dica de ferramenta para fornecer aos usuÃ¡rios ajuda sobre o formato a ser usado para inserir informaÃ§Ãµes de dias.
- Uma string de sessÃ£o completa Ã© construÃ­da concatenando as duas strings que o script recebe como entradas.
- O tipo das duas entradas Ã© explicitamente declarado com a palavra-chave [string](https://br.tradingview.com/pine-script-reference/v5/#type_string) para deixar claro que essas variÃ¡veis conterÃ£o uma string.
- Ã‰ detectado se a barra do grÃ¡fico estÃ¡ na sessÃ£o definida pelo usuÃ¡rio chamando [time()](https://br.tradingview.com/pine-script-reference/v5/#fun_time) com a string da sessÃ£o. Se o valor de [time](https://br.tradingview.com/pine-script-reference/v5/#var_time) da barra atual (o tempo no [open](https://br.tradingview.com/pine-script-reference/v5/#var_open) da barra) nÃ£o estiver na sessÃ£o, [time()](https://br.tradingview.com/pine-script-reference/v5/#fun_time) retorna [na](https://br.tradingview.com/pine-script-reference/v5/#var_na), entÃ£o `inSession` serÃ¡ verdadeiro sempre que [time()](https://br.tradingview.com/pine-script-reference/v5/#fun_time) retornar um valor que nÃ£o seja [na](https://br.tradingview.com/pine-script-reference/v5/#var_na).

![Input session](./imgs/Inputs-InputTypes-07.png)

## Input Source

Entradas da fonte, [input.source()](https://br.tradingview.com/pine-script-reference/v5/#fun_input.source), sÃ£o Ãºteis para fornecer uma seleÃ§Ã£o de dois tipos de fontes:

- Valores de preÃ§o, a saber: [open](https://br.tradingview.com/pine-script-reference/v5/#var_open), [high](https://br.tradingview.com/pine-script-reference/v5/#var_high), [low](https://br.tradingview.com/pine-script-reference/v5/#var_low), [close](https://br.tradingview.com/pine-script-reference/v5/#var_close), [hl2](https://br.tradingview.com/pine-script-reference/v5/#var_hl2), [hlc3](https://br.tradingview.com/pine-script-reference/v5/#var_hlc3) e [ohlc4](https://br.tradingview.com/pine-script-reference/v5/#var_ohlc4).
- Os valores plotados por outros scripts no grÃ¡fico. Isso pode ser Ãºtil para "_link_" ("_vincular_") dois ou mais scripts juntos, enviando a saÃ­da de um como entrada para outro script.

Este script simplesmente plota a seleÃ§Ã£o da fonte do usuÃ¡rio. PropÃµe-se o valor [high](https://br.tradingview.com/pine-script-reference/v5/#var_high) como padrÃ£o:

```c
//@version=5
indicator("Source input", "", true)
srcInput = input.source(high, "Source")
plot(srcInput, "Src", color.new(color.purple, 70), 6)
```

Isso mostra um grÃ¡fico onde, alÃ©m do script, foi carregado um indicador "Arnaud Legoux Moving Average". Veja aqui como o widget de entrada da fonte do script Ã© usado para selecionar a saÃ­da do script ALMA como entrada no script. Como script plota essa fonte em uma linha grossa de cor roxo claro, Ã© possÃ­vel ver os plots dos dois scripts sobrepostos porque eles plotam o mesmo valor:

![Input source](./imgs/Inputs-InputTypes-08.png)

## Input Time

Entradas de tempo usam a funÃ§Ã£o [input.time()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}time). A funÃ§Ã£o retorna um horÃ¡rio Unix em milissegundos (veja a pÃ¡gina [Tempo](./05_21_time.md) para mais informaÃ§Ãµes). Esse tipo de dado tambÃ©m contÃ©m informaÃ§Ãµes de data, entÃ£o a funÃ§Ã£o [input.time()](https://br.tradingview.com/pine-script-reference/v5/#fun_input{dot}time) _retorna um horÃ¡rio __e__ uma data_. Por isso, seu widget permite a seleÃ§Ã£o de ambos.

Aqui, o tempo da barra Ã© testado contra um valor de entrada, e uma seta Ã© plotada quando o tempo Ã© maior:

```c
//@version=5
indicator("Time input", "T", true)
timeAndDateInput = input.time(timestamp("1 Aug 2021 00:00 +0300"), "Date and time")
barIsLater = time > timeAndDateInput
plotchar(barIsLater, "barIsLater", "ðŸ †", location.top, size = size.tiny)
```

Observe que o valor `defval` usado Ã© uma chamada para a funÃ§Ã£o [timestamp()](https://br.tradingview.com/pine-script-reference/v5/#fun_timestamp).
