
# Text e Shapes (_Texto e Formas_)

√â poss√≠vel exibir texto ou formas usando cinco maneiras diferentes no Pine Script:

- [plotchar()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotchar)
- [plotshape()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotshape)
- [plotarrow()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotarrow)
- _Labels_ criados com [label.new()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dnew)
- Tabelas criadas com [table.new()](https://br.tradingview.com/pine-script-reference/v5/#fun_table%7Bdot%7Dnew) (veja [Tabelas](./05_19_tables.md))

Qual utilizar depende das necessidades:

- As tabelas podem exibir texto em v√°rias posi√ß√µes relativas nos gr√°ficos que n√£o se movem conforme os usu√°rios rolam ou ampliam o gr√°fico horizontalmente. Seu conte√∫do n√£o est√° vinculado a barras. Em contraste, o texto exibido com [plotchar()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotchar), [plotshape()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotshape) ou [label.new()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dnew) est√° sempre vinculado a uma barra espec√≠fica, movendo-se com a posi√ß√£o da barra no gr√°fico. Veja a p√°gina sobre [Tabelas](./05_19_tables.md) para mais informa√ß√µes.
- Tr√™s fun√ß√µes podem exibir formas predefinidas: [plotshape()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotshape), [plotarrow()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotarrow) e _labels_ criados com [label.new()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dnew).
- [plotarrow()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotarrow) n√£o pode exibir texto, apenas setas para cima ou para baixo.
- [plotchar()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotchar) e [plotshape()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotshape) podem exibir texto n√£o din√¢mico em qualquer barra ou em todas as barras do gr√°fico.
- [plotchar()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotchar) pode exibir apenas um caractere, enquanto [plotshape()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotshape) pode exibir strings, incluindo quebras de linha.
- [label.new()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dnew) pode exibir no m√°ximo 500 _labels_ no gr√°fico. Seu texto __pode__ conter texto din√¢mico, ou "series string". Quebras de linha tamb√©m s√£o suportadas no texto dos _labels_.
- Enquanto [plotchar()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotchar) e [plotshape()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotshape) podem exibir texto com um deslocamento fixo no passado ou futuro, que n√£o pode mudar durante a execu√ß√£o do script, cada chamada [label.new()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dnew) pode usar um deslocamento "series" que pode ser calculado dinamicamente.

Estas s√£o algumas considera√ß√µes sobre strings no Pine Script:

- Como o par√¢metro `text` em [plotchar()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotchar) e [plotshape()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotshape) requer um argumento "const string", ele n√£o pode conter valores como pre√ßos que s√≥ podem ser conhecidos na barra ("series string").
- Para incluir valores "series" no texto exibido usando [label.new()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dnew), eles precisam ser convertidos em strings usando [str.tostring()](https://br.tradingview.com/pine-script-reference/v5/#fun_str%7Bdot%7Dtostring).
- O operador de concatena√ß√£o para strings no Pine √© `+`. Ele √© usado para juntar componentes de string em uma √∫nica string, por exemplo, `msg = "Chart symbol: " + syminfo.tickerid` (onde [syminfo.tickerid](https://br.tradingview.com/pine-script-reference/v5/#var_syminfo%7Bdot%7Dtickerid) √© uma vari√°vel incorporada que retorna as informa√ß√µes de troca e s√≠mbolo do gr√°fico em formato de string).
- Os caracteres exibidos por todas essas fun√ß√µes podem ser caracteres Unicode, que podem incluir s√≠mbolos Unicode. Veja este script [Explorando Unicode](https://br.tradingview.com/script/0rFQOCKf-Exploring-Unicode/) para ter uma ideia do que pode ser feito com caracteres Unicode.
- A cor ou o tamanho do texto √†s vezes podem ser controlados usando par√¢metros de fun√ß√£o, mas nenhuma formata√ß√£o inline (negrito, it√°lico, monoespa√ßo, etc.) √© poss√≠vel.
- O texto dos scripts Pine sempre √© exibido no gr√°fico na fonte Trebuchet MS, que √© usada em muitos textos do TradingView, incluindo este.

Este script exibe texto usando os quatro m√©todos dispon√≠veis no Pine Script:

```c
//@version=5
indicator("Four displays of text", overlay = true)
plotchar(ta.rising(close, 5), "`plotchar()`", "ü†Ö", location.belowbar, color.lime, size = size.small)
plotshape(ta.falling(close, 5), "`plotchar()`", location = location.abovebar, color = na, text = "‚Ä¢`plotshape()‚Ä¢`\nü†á", textcolor = color.fuchsia, size = size.huge)

if bar_index % 25 == 0
    label.new(bar_index, na, "‚Ä¢LABEL‚Ä¢\nHigh = " + str.tostring(high, format.mintick) + "\nü†á", yloc = yloc.abovebar, style = label.style_none, textcolor = color.black, size = size.normal)

printTable(txt) => var table t = table.new(position.middle_right, 1, 1), table.cell(t, 0, 0, txt, bgcolor = color.yellow)
printTable("‚Ä¢TABLE‚Ä¢\n" + str.tostring(bar_index + 1) + " bars\nin the dataset")
```

![Texto e formas](./imgs/TextAndShapes-Introduction-01.Caf7GxqL_Z22HKK1.webp)

__Note que:__

- O m√©todo usado para exibir cada string de texto √© mostrado com o texto, exceto para as setas para cima em verde-lima exibidas usando [plotchar()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotchar), pois s√≥ pode exibir um caractere.
- As chamadas de _label_ e tabela podem ser inseridas em estruturas condicionais para controlar quando s√£o executadas, enquanto [plotchar()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotchar) e [plotshape()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotshape) n√£o podem. Sua plotagem condicional deve ser controlada usando seu primeiro argumento, que √© um "bool series" cujo valor `true` ou `false` determina quando o texto √© exibido.
- Os valores num√©ricos exibidos na tabela e nos _labels_ s√£o convertidos em string usando [str.tostring()](https://br.tradingview.com/pine-script-reference/v5/#fun_str%7Bdot%7Dtostring).
- Utiliza-se o operador `+` para concatenar componentes de string.
- [plotshape()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotshape) √© projetado para exibir uma forma com texto acompanhante. Seu par√¢metro `size` controla o tamanho da forma, n√£o do texto. √â usado [na](https://br.tradingview.com/pine-script-reference/v5/#var_na) para seu argumento `color` para que a forma n√£o seja vis√≠vel.
- Ao contr√°rio de outros textos, o texto da tabela n√£o se mover√° ao rolar ou redimensionar o gr√°fico.
- Algumas strings de texto cont√™m a seta Unicode ü†á (U+1F807).
- Algumas strings de texto cont√™m a sequ√™ncia `\n` que representa uma nova linha.

## `plotchar()`

Esta fun√ß√£o √© √∫til para exibir um √∫nico caractere nas barras. Tem a seguinte sintaxe:

```c
plotchar(series, title, char, location, color, offset, text, textcolor, editable, size, show_last, display, force_overlay) ‚Üí void
```

Veja a [entrada do Manual de Refer√™ncia para plotchar()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotchar) para detalhes sobre seus par√¢metros.

Conforme explicado na se√ß√£o sobre [quando a escala do script deve ser preservada](./06_02_debugging.md#sem-afetar-a-escala) da p√°gina sobre [depura√ß√£o](./06_02_debugging.md), a fun√ß√£o pode ser usada para exibir e inspecionar valores na Janela de Dados ou nos valores do indicador exibidos √† direita do nome do script no gr√°fico:

```c
//@version=5
indicator("", "", true)
plotchar(bar_index, "Bar index", "", location.top)
```

![plotchar() 01](./imgs/TextAndShapes-Plotchar-01.Bocx9V6g_Z13RLu0.webp)

__Note que:__

- O cursor est√° na √∫ltima barra do gr√°fico.
- O valor de [bar_index](https://br.tradingview.com/pine-script-reference/v5/#var_bar_index) nessa barra √© exibido nos valores do indicador (1) e na Janela de Dados (2).
- Utiliza-se [location.top](https://br.tradingview.com/pine-script-reference/v5/#const_location%7Bdot%7Dtop) porque o padr√£o [location.abovebar](https://br.tradingview.com/pine-script-reference/v5/#const_location%7Bdot%7Dabovebar) colocaria o pre√ßo em jogo na escala do script, o que frequentemente interfere com outras plotagens.

O [plotchar()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotchar) tamb√©m funciona bem para identificar pontos espec√≠ficos no gr√°fico ou para validar se as condi√ß√µes s√£o `true` quando √© esperado que sejam. Este exemplo exibe uma seta para cima abaixo das barras onde [close](https://br.tradingview.com/pine-script-reference/v5/#var_close), [high](https://br.tradingview.com/pine-script-reference/v5/#var_high) e [volume](https://br.tradingview.com/pine-script-reference/v5/#var_volume) est√£o todos subindo por dois barras:

```c
//@version=5
indicator("", "", true)
bool longSignal = ta.rising(close, 2) and ta.rising(high, 2) and (na(volume) or ta.rising(volume, 2))
plotchar(longSignal, "Long", "‚ñ≤", location.belowbar, color = na(volume) ? color.gray : color.blue, size = size.tiny)
```

![plotchar() 02](./imgs/TextAndShapes-Plotchar-02.CP9gwRwQ_cuUlM.webp)

__Note que:__

- Usa-se `(na(volume) or ta.rising(volume, 2))` para que o script funcione em s√≠mbolos sem dados de [volume](https://br.tradingview.com/pine-script-reference/v5/#var_volume). Se n√£o considerasse quando n√£o h√° dados de [volume](https://br.tradingview.com/pine-script-reference/v5/#var_volume), que √© o que `na(volume)` faz ao ser `true` quando n√£o h√° volume, o valor da vari√°vel `longSignal` nunca seria `true` porque `ta.rising(volume, 2)` resultaria em `false` nesses casos.
- √â mostrado a seta em cinza quando n√£o h√° volume, para lembrar que todas as tr√™s condi√ß√µes b√°sicas n√£o est√£o sendo atendidas.
- Como [plotchar()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotchar) agora est√° exibindo um caractere no gr√°fico, √© utilizado `size = size.tiny` para controlar seu tamanho.
- Foi adaptado o argumento `location` para exibir o caractere abaixo das barras.

Caso n√£o importe em plotar apenas c√≠rculos, tamb√©m pode usar [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot) para alcan√ßar um efeito semelhante:

```c
//@version=5
indicator("", "", true)
longSignal = ta.rising(close, 2) and ta.rising(high, 2) and (na(volume) or ta.rising(volume, 2))
plot(longSignal ? low - ta.tr : na, "Long", color.blue, 2, plot.style_circles)
```

Este m√©todo tem o inconveniente de que, como n√£o h√° mecanismo de posicionamento relativo com [plot()](https://br.tradingview.com/pine-script-reference/v5/#fun_plot), √© preciso deslocar os c√≠rculos para baixo usando algo como [ta.tr](https://br.tradingview.com/pine-script-reference/v5/#var_ta%7Bdot%7Dtr) (o "True Range" da barra):

![plotchar() 03](./imgs/TextAndShapes-Plotchar-03.lnUtjJIt_5GDoG.webp)


## `plotshape()`

Esta fun√ß√£o √© √∫til para exibir formas predefinidas e/ou texto nas barras. A sintaxe √© a seguinte:

```c
plotshape(series, title, style, location, color, offset, text, textcolor, editable, size, show_last, display, force_overlay) ‚Üí void
```

Veja a [entrada do Manual de Refer√™ncia para plotshape()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotshape) para detalhes sobre seus par√¢metros.

O uso da fun√ß√£o para alcan√ßar mais ou menos o mesmo resultado que no segundo exemplo da se√ß√£o anterior:

```c
//@version=5
indicator("", "", true)
longSignal = ta.rising(close, 2) and ta.rising(high, 2) and (na(volume) or ta.rising(volume, 2))
plotshape(longSignal, "Long", shape.arrowup, location.belowbar)  
```

Note que aqui, em vez de usar um caractere de seta, √© utilizado o argumento `shape.arrowup` para o par√¢metro `style`.

![plotshape() 01](./imgs/TextAndShapes-Plotshape-01.JOPpSRCa_1wGGd4.webp)

√â poss√≠vel usar diferentes chamadas de [plotshape()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotshape) para sobrepor texto nas barras. Ser√° necess√°rio usar `\n` seguido de um caractere especial n√£o imprim√≠vel que n√£o seja removido para preservar a funcionalidade da nova linha. Aqui est√° sendo usado um espa√ßo em branco de largura zero do Unicode (U+200E). Embora n√£o vis√≠vel nas strings do c√≥digo a seguir, ele est√° presente e pode ser copiado/colado. O caractere Unicode especial precisa ser o __√∫ltimo__ na string para texto ascendente e o __primeiro__ quando plotando abaixo da barra e o texto for descendente:

```c
//@version=5
indicator("Lift text", "", true)
plotshape(true, "", shape.arrowup,   location.abovebar, color.green,  text = "A")
plotshape(true, "", shape.arrowup,   location.abovebar, color.lime,   text = "B\n‚Äã")
plotshape(true, "", shape.arrowdown, location.belowbar, color.red,    text = "C")
plotshape(true, "", shape.arrowdown, location.belowbar, color.maroon, text = "‚Äã\nD")
```

![plotshape() 02](./imgs/TextAndShapes-Plotshape-02.CuvXGcSI_3Ad17.webp)

As formas dispon√≠veis que podem ser usadas com o par√¢metro `style` s√£o:

![plotshape() 03](./imgs/plotshape-available-shape-style.png)

## `plotarrow()`

A fun√ß√£o [plotarrow](https://br.tradingview.com/pine-script-reference/v5/#fun_plotarrow) exibe setas para cima ou para baixo de comprimento vari√°vel, com base no valor relativo da s√©rie usada no primeiro argumento da fun√ß√£o. A sintaxe √© a seguinte:

```c
plotarrow(series, title, colorup, colordown, offset, minheight, maxheight, editable, show_last, display, force_overlay) ‚Üí void
```

Veja a [entrada do Manual de Refer√™ncia para plotarrow()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotarrow) para detalhes sobre seus par√¢metros.

O par√¢metro `series` em [plotarrow()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotarrow) n√£o √© um "series bool" como em [plotchar()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotchar) e [plotshape()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotshape); √© um "series int/float" e h√° mais do que um simples valor `true` ou `false` determinando quando as setas s√£o plotadas. Esta √© a l√≥gica que governa como o argumento fornecido para `series` afeta o comportamento de [plotarrow()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotarrow):

- `series > 0`: Uma seta para cima √© exibida, cujo comprimento ser√° proporcional ao valor relativo da s√©rie naquela barra em rela√ß√£o a outros valores da s√©rie.
- `series < 0`: Uma seta para baixo √© exibida, com tamanho proporcional usando as mesmas regras.
- `series == 0 ou na(series)`: Nenhuma seta √© exibida.

Os tamanhos m√°ximos e m√≠nimos poss√≠veis para as setas (em pixels) podem ser controlados usando os par√¢metros `minheight` e `maxheight`.

Aqui est√° um script simples ilustrando como [plotarrow()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotarrow) funciona:

```c
//@version=5
indicator("", "", true)
body = close - open
plotarrow(body, colorup = color.teal, colordown = color.orange)
```

![plotarrow() 01](./imgs/TextAndShapes-Plotarrow-01.KkXXJXUI_Z1LV4X8.webp)

Repare como a altura das setas √© proporcional ao tamanho relativo dos corpos das barras.

Qualquer s√©rie pode ser usada para plotar as setas. Aqui √© usado o valor do "Oscilador de Chaikin" "_Chaikin Oscillator_" para controlar a localiza√ß√£o e o tamanho das setas:

```c
//@version=5
indicator("Chaikin Oscillator Arrows", overlay = true)
fastLengthInput = input.int(3,  minval = 1)
slowLengthInput = input.int(10, minval = 1)
osc = ta.ema(ta.accdist, fastLengthInput) - ta.ema(ta.accdist, slowLengthInput)
plotarrow(osc)
```

![plotarrow() 02](./imgs/TextAndShapes-Plotarrow-02.ChRmPIiy_Zb0OYl.webp)

Repare que o "Oscilador de Chaikin" "_Chaikin Oscillator_" √© exibido em um painel abaixo do gr√°fico, para que seja poss√≠vel ver quais valores s√£o usados para determinar a posi√ß√£o e o tamanho das setas.

## Labels

Os _labels_ est√£o dispon√≠veis a partir da v4. Funcionam de maneira muito diferente de [plotchar()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotchar) e [plotshape()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotshape).

_Labels_ s√£o objetos, como [linhas e caixas](./05_12_lines_e_boxes.md), ou [tabelas](./05_19_tables.md). Como eles, s√£o referenciados usando um ID, que atua como um ponteiro. IDs de _labels_ s√£o do tipo "label". Assim como outros objetos, os IDs de _labels_ s√£o "time series" e todas as fun√ß√µes usadas para gerenci√°-los aceitam argumentos "series", o que os torna muito flex√≠veis.

> __Observa√ß√£o!__\
> Nos gr√°ficos do TradingView, um conjunto completo de "_Ferramentas de Desenho_" "_Drawing Tools_" permite que os usu√°rios criem e modifiquem desenhos usando a√ß√µes do mouse. Embora √†s vezes possam parecer semelhantes a objetos de desenho criados com c√≥digo Pine Script, s√£o entidades n√£o relacionadas. Objetos de desenho criados usando c√≥digo Pine n√£o podem ser modificados com a√ß√µes do mouse, e desenhos feitos manualmente na interface do usu√°rio do gr√°fico n√£o s√£o vis√≠veis em scripts Pine.

_Labels_ s√£o vantajosos porque:

- Permitem que valores "series" sejam convertidos em texto e colocados nos gr√°ficos. Isso significa que s√£o ideais para exibir valores que n√£o podem ser conhecidos antecipadamente, como valores de pre√ßos, n√≠veis de suporte e resist√™ncia, ou quaisquer outros valores que o script calcular.
- Suas op√ß√µes de posicionamento s√£o mais flex√≠veis do que as fun√ß√µes `plot*()`.
- Oferecem mais modos de exibi√ß√£o.
- Ao contr√°rio das fun√ß√µes `plot*()`, as fun√ß√µes de manipula√ß√£o de _labels_ podem ser inseridas em estruturas condicionais ou de loop, facilitando o controle de seu comportamento.
- √â poss√≠vel adicionar tooltips aos _labels_.

Uma desvantagem de usar _labels_ em compara√ß√£o com [plotchar()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotchar) e [plotshape()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotshape) √© que √© poss√≠vel desenhar apenas uma quantidade limitada deles no gr√°fico. O padr√£o √© cerca de 50, mas pode-se usar o par√¢metro `max_labels_count` na declara√ß√£o [indicator()](https://br.tradingview.com/pine-script-reference/v5/#fun_indicator) ou [strategy()](https://br.tradingview.com/pine-script-reference/v5/#fun_strategy) para especificar at√© 500. _Labels_, como [linhas e caixas](./05_12_lines_e_boxes.md), s√£o gerenciados usando um mecanismo de "_coleta de lixo_" "_garbage collection_" que exclui os mais antigos no gr√°fico, de modo que apenas os _labels_ mais recentemente desenhados sejam vis√≠veis.

A caixa de ferramentas de built-ins para gerenciar _labels_ est√° no namespace `label`. Inclui:

- [label.new()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dnew) para criar _labels_.
- Fun√ß√µes `label.set_*()` para modificar as propriedades de um _label_ existente.
- Fun√ß√µes `label.get_*()` para ler as propriedades de um _label_ existente.
- [label.delete()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Ddelete) para excluir _labels_.
- O array [label.all](https://br.tradingview.com/pine-script-reference/v5/#var_label%7Bdot%7Dall), que sempre cont√©m os IDs de todos os _labels_ vis√≠veis no gr√°fico. O tamanho do array depender√° do n√∫mero m√°ximo de _labels_ para o script e quantos deles foram desenhados. `array.size(label.all)` retornar√° o tamanho do array.

### Criando e Modificando _Labels_

A fun√ß√£o [label.new()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dnew) cria um novo _label_. Tem a seguinte assinatura:

```c
label.new(x, y, text, xloc, yloc, color, style, textcolor, size, textalign, tooltip, force_overylay) ‚Üí series label
```

As fun√ß√µes _setter_ que permitem alterar as propriedades de um _label_ s√£o:

- [label.set_x()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_x)
- [label.set_y()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_y)
- [label.set_xy()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_xy)
- [label.set_text()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_text)
- [label.set_xloc()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_xloc)
- [label.set_yloc()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_yloc)
- [label.set_color()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_color)
- [label.set_style()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_style)
- [label.set_textcolor()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_textcolor)
- [label.set_size()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_size)
- [label.set_textalign()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_textalign)
- [label.set_tooltip()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_tooltip)

Todas t√™m uma assinatura semelhante. A de [label.set_color()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_color) √©:

```c
label.set_color(id, color) ‚Üí void
```

Onde:

- `id` √© o ID do _label_ cuja propriedade deve ser modificada.
- O pr√≥ximo par√¢metro √© a propriedade do _label_ a ser modificada. Depende da fun√ß√£o setter usada. [label.set_xy()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dset_xy) altera duas propriedades, portanto, possui dois desses par√¢metros.

√â assim que se pode criar _labels_ em sua forma mais simples:

```c
//@version=5
indicator("", "", true)
label.new(bar_index, high)
```

![Criando e modificando labels 01](./imgs/TextAndShapes-CreatingLabels-01.BHaO-o78_Y8WYV.webp)

__Note que:__

- O _label_ √© criado com os par√¢metros `x = bar_index` (o √≠ndice da barra atual, [bar_index](https://br.tradingview.com/pine-script-reference/v5/#var_bar_index)) e `y = high` (o valor [high](https://br.tradingview.com/pine-script-reference/v5/#var_high) da barra).
- N√£o √© fornecido um argumento para o par√¢metro `text` da fun√ß√£o. Seu valor padr√£o sendo uma string vazia, nenhum texto √© exibido.
- Nenhuma l√≥gica controla a chamada [label.new()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dnew), ent√£o _labels_ s√£o criados em cada barra.
- Apenas os √∫ltimos 54 _labels_ s√£o exibidos porque a chamada [indicator()](https://br.tradingview.com/pine-script-reference/v5/#fun_indicator) n√£o usa o par√¢metro `max_labels_count` para especificar um valor diferente do padr√£o (~50).
- _Labels_ persistem nas barras at√© que o script os exclua usando [label.delete()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Ddelete) ou a coleta de lixo os remova.

No pr√≥ximo exemplo, um _label_ √© exibido na barra com o valor mais alto de [high](https://br.tradingview.com/pine-script-reference/v5/#var_high) nos √∫ltimos 50 barras:

```c
//@version=5
indicator("", "", true)

// Find the highest `high` in last 50 bars and its offset. Change its sign so it is positive.
LOOKBACK = 50
hi = ta.highest(LOOKBACK)
highestBarOffset = -ta.highestbars(LOOKBACK)

// Create label on bar zero only.
var lbl = label.new(na, na, "", color = color.orange, style = label.style_label_lower_left)
// When a new high is found, move the label there and update its text and tooltip.
if ta.change(hi)
    // Build label and tooltip strings.
    labelText = "High: " + str.tostring(hi, format.mintick)
    tooltipText = "Offset in bars: " + str.tostring(highestBarOffset) + "\nLow: " + str.tostring(low[highestBarOffset], format.mintick)
    // Update the label's position, text and tooltip.
    label.set_xy(lbl, bar_index[highestBarOffset], hi)
    label.set_text(lbl, labelText)
    label.set_tooltip(lbl, tooltipText)
```

![Criando e modificando labels 02](./imgs/TextAndShapes-CreatingLabels-02.CaxmDfMG_1vmoqQ.webp)

__Note que:__

- O _label_ √© criado apenas na primeira barra, utilizando a palavra-chave [var](https://br.tradingview.com/pine-script-reference/v5/#kw_var) para declarar a vari√°vel `lbl` que cont√©m o ID do _label_. Os argumentos `x`, `y` e `text` nessa chamada de [label.new()](https://br.tradingview.com/pine-script-reference/v5/#fun_label%7Bdot%7Dnew) s√£o irrelevantes, pois o _label_ ser√° atualizado nas barras seguintes. No entanto, cuida-se de usar a `color` e `style` desejadas para os _labels_, para que n√£o precisem ser atualizadas posteriormente.
- Em cada barra, detecta-se se um novo valor m√°ximo foi encontrado testando altera√ß√µes no valor de `hi`.
- Quando ocorre uma mudan√ßa no valor m√°ximo, o _label_ √© atualizado com novas informa√ß√µes. Para isso, tr√™s chamadas `label.set*()` s√£o usadas para alterar as informa√ß√µes relevantes do _label_. Refere-se ao _label_ usando a vari√°vel `lbl`, que cont√©m o ID do _label_. O script mant√©m o mesmo _label_ em todas as barras, movendo-o e atualizando suas informa√ß√µes quando um novo valor m√°ximo √© detectado.

Aqui, um _label_ √© criado em cada barra, mas suas propriedades s√£o definidas condicionalmente, dependendo da polaridade da barra:

```c
//@version=5
indicator("", "", true)
lbl = label.new(bar_index, na)
if close >= open
    label.set_text(lbl, "green")
    label.set_color(lbl, color.green)
    label.set_yloc(lbl, yloc.belowbar)
    label.set_style(lbl, label.style_label_up)
else
    label.set_text(lbl, "red")
    label.set_color(lbl, color.red)
    label.set_yloc(lbl, yloc.abovebar)
    label.set_style(lbl, label.style_label_down)
```

![Criando e modificando labels 03](./imgs/TextAndShapes-CreatingLabels-03.ClglPmUL_Z1Jm6jL.webp) -->
