
# Repintando (_Repainting_)

__*Repintura*__ ou __*Repainting*__ √© definido como: __comportamento do script que faz com que c√°lculos ou plotagens hist√≥ricas e em tempo real se comportem de maneira diferente__.

O comportamento de repintura √© generalizado e muitos fatores podem caus√°-lo. Estima-se que mais de 95% dos indicadores existentes exibem algum tipo de comportamento de repintura. Indicadores comumente usados, como MACD e RSI, por exemplo, mostram valores confirmados em barras hist√≥ricas, mas flutuam em uma barra de gr√°fico em tempo real, n√£o confirmada, at√© que ela se feche. Portanto, eles se comportam _diferentemente_ em estados hist√≥ricos e em tempo real.

__Nem todo comportamento de _repainting_ √© inerentemente in√∫til ou enganoso__, nem impede que traders experientes usem indicadores com esse comportamento. Por exemplo, quem pensaria em desacreditar um indicador de perfil de volume apenas porque ele atualiza seus valores em barras de tempo real?

Pode-se encontrar qualquer uma das seguintes formas de _repainting_ nos scripts que usam, dependendo do que os c√°lculos de um script envolvem:

- __Generalizado, mas frequentemente aceit√°vel__: Um script pode usar valores que s√£o atualizados com as mudan√ßas de pre√ßo em tempo real na barra n√£o confirmada. Por exemplo, se algu√©m usar a vari√°vel [close](https://br.tradingview.com/pine-script-reference/v5/#var_close) em c√°lculos realizados em uma barra de gr√°fico aberta, seus valores refletir√£o o pre√ßo mais recente na barra. No entanto, o script s√≥ confirmar√° um novo ponto de dados em sua s√©rie hist√≥rica quando a barra se fechar. Outro caso comum √© usar [request.security](https://br.tradingview.com/pine-script-reference/v5/#fun_request.security) para buscar dados de um intervalo de tempo maior em barras de tempo real, conforme explicado na se√ß√£o [Comportamento hist√≥rico e tempo real](./05_14_outros_timeframes_e_dados.md#comportamento-hist√≥rico-e-tempo-real) da p√°gina [Outros per√≠odos e dados](./05_14_outros_timeframes_e_dados.md). Assim como a barra n√£o confirmada no intervalo de tempo do gr√°fico, [request.security](https://br.tradingview.com/pine-script-reference/v5/#fun_request.security) pode rastrear valores n√£o confirmados de um contexto de intervalo de tempo maior em barras de tempo real, o que pode levar a repintura ap√≥s o script reiniciar sua execu√ß√£o. Muitas vezes, n√£o h√° nada de errado em usar esses scripts, desde que entenda como eles funcionam. No entanto, ao optar por usar esses scripts para emitir alertas ou ordens de negocia√ß√£o, √© importante entender a diferen√ßa entre seu comportamento em tempo real e hist√≥rico e decidir por si mesmo se ele oferece utilidade para suas necessidades.
- __Potencialmente enganoso__: Scripts que plotam valores no passado, calculam resultados em barras de tempo real que n√£o podem ser replicados em barras hist√≥ricas ou realocam eventos passados s√£o potencialmente enganosos. Por exemplo, Ichimoku, a maioria dos scripts baseados em piv√¥s, a maioria das estrat√©gias que usam `calc_on_every_tick = true`, scripts que usam [request.security](https://br.tradingview.com/pine-script-reference/v5/#fun_request.security) quando ele se comporta de maneira diferente em barras de tempo real, muitos scripts que usam [varip](https://br.tradingview.com/pine-script-reference/v5/#kw_varip), muitos scripts que usam [timenow](https://br.tradingview.com/pine-script-reference/v5/#var_timenow), e alguns scripts que usam vari√°veis `barstate.*` podem exibir comportamento de _repainting_ enganoso.
- __Inaceit√°vel__: Scripts que vazam informa√ß√µes futuras para o passado, estrat√©gias que executam em [gr√°ficos n√£o padr√£o](./05_13_dados_de_graficos_nao_padronizados.md), e scripts que usam barras intradi√°rias em tempo real para gerar alertas ou ordens s√£o exemplos que podem produzir um comportamento de repintura fortemente enganoso.
- __Inevit√°vel__: Revis√µes do feed de dados de um provedor e varia√ß√µes na barra inicial do hist√≥rico do gr√°fico podem causar um comportamento de repintura que pode ser inevit√°vel em um script.

Os dois primeiros tipos de _repainting_ podem ser perfeitamente aceit√°veis se:

1. Voc√™ est√° ciente do comportamento.
2. Voc√™ pode conviver com isso, ou
3. Voc√™ pode contorn√°-lo.

Deve estar claro agora que nem __todo__ comportamento de repintura √© errado e deve ser evitado a todo custo. Em muitas situa√ß√µes, algumas formas de repintura podem ser exatamente o que um script precisa. O importante √© saber quando o comportamento de _repainting_ __n√£o__ √© aceit√°vel para suas necessidades. Para evitar repintura que n√£o seja aceit√°vel, √© importante entender como uma ferramenta funciona ou como deve projetar as ferramentas que constr√≥i. Ao [publicar](./06_04_publicando_scripts.md) scripts, certifique-se de mencionar qualquer comportamento potencialmente enganoso junto com as outras limita√ß√µes de seu script na descri√ß√£o da publica√ß√£o.

> __Observa√ß√£o!__\
> N√£o √© discutido os perigos de usar estrat√©gias em gr√°ficos n√£o padr√£o, pois esse problema n√£o est√° relacionado ao _repainting_. Veja o script [Backtesting on Non-Standard Charts: Caution!](https://br.tradingview.com/script/q9laJNG9-Backtesting-on-Non-Standard-Charts-Caution-PineCoders-FAQ/) para uma discuss√£o sobre o assunto.

## Para Usu√°rios de Scripts

Pode-se decidir usar indicadores que fazem _repainting_ se entender o comportamento e se esse comportamento atende aos seus requisitos de an√°lise. N√£o seja um dos novatos que colocam "repaint" em scripts publicados na tentativa de desacredit√°-los, pois isso revela uma falta de conhecimento b√°sico sobre o assunto.

Perguntar simplesmente se um script faz repintura √© relativamente insignificante, dado que existem formas de comportamento de repintura que s√£o perfeitamente aceit√°veis em um script. Portanto, tal pergunta n√£o gerar√° uma resposta significativa. Deve-se, em vez disso, fazer perguntas _espec√≠ficas_ sobre o comportamento de _repainting_ potencial de um script, como:

- O script calcula/exibe da mesma maneira em barras hist√≥ricas e em tempo real?
- Os alertas do script aguardam o final de uma barra em tempo real antes de serem acionados?
- Os marcadores de sinal mostrados pelo script aguardam o final de uma barra em tempo real antes de serem exibidos?
- O script plotagem/desenha valores no passado?
- A estrat√©gia usa `calc_on_every_tick = true`?
- As chamadas [request.security](https://br.tradingview.com/pine-script-reference/v5/#fun_request.security) do script vazam informa√ß√µes futuras para o passado em barras hist√≥ricas?

O importante √© que entenda como as ferramentas funcionam e se o comportamento delas √© compat√≠vel com seus objetivos, independentemente de fazerem repintura ou n√£o. Como aprender√° ao ler esta p√°gina, _repainting_ √© um assunto complexo. Ele tem muitas faces e muitas causas. Mesmo se voc√™ n√£o programar em Pine Script, esta p√°gina o ajudar√° a entender a gama de causas que podem levar ao repintura e, possibilitar√° discuss√µes mais significativas com os autores de scripts.

## Para Programadores de Pine Script

Conforme discutido acima, nem todas as formas de comportamento de _repainting_ devem ser evitadas a todo custo, nem todo comportamento potencial de repintura √© necessariamente evit√°vel. Esta p√°gina o ajude a entender melhor as din√¢micas em jogo para que possa projetar suas ferramentas de negocia√ß√£o com esses comportamentos em mente. O conte√∫do desta p√°gina deve ajudar a conscientiz√°-lo sobre erros de codifica√ß√£o comuns que produzem resultados de repintura enganosos.

Quaisquer que sejam suas decis√µes de design, se voc√™ [publicar](./06_04_publicando_scripts.md) seu script, explique o script aos traders para que eles possam entender como ele se comporta.

Esta p√°gina cobre tr√™s amplas categorias de causas de _repainting_:

- [C√°lculos hist√≥ricos vs em tempo real](./05_16_repintura.md#c√°lculos-hist√≥ricos-vs-em-tempo-real)
- [Plotagem no passado](./05_16_repintura.md#plotagem-no-passado)
- [Varia√ß√µes no conjunto de dados](./05_16_repintura.md#varia√ß√µes-no-conjunto-de-dados)

## C√°lculos Hist√≥ricos vs em Tempo Real

### Valores de Dados Fluidos

Dados hist√≥ricos n√£o incluem registros de movimentos de pre√ßo intermedi√°rios em barras; apenas valores de [abertura](https://br.tradingview.com/pine-script-reference/v5/#var_open), [m√°xima](https://br.tradingview.com/pine-script-reference/v5/#var_high), [m√≠nima](https://br.tradingview.com/pine-script-reference/v5/#var_low) e [fechamento](https://br.tradingview.com/pine-script-reference/v5/#var_close) (OHLC).

Em barras de tempo real (barras em execu√ß√£o quando o mercado do instrumento est√° aberto), no entanto, os valores de [m√°xima](https://br.tradingview.com/pine-script-reference/v5/#var_high), [m√≠nima](https://br.tradingview.com/pine-script-reference/v5/#var_low) e [fechamento](https://br.tradingview.com/pine-script-reference/v5/#var_close) n√£o s√£o fixos; eles podem mudar muitas vezes antes que a barra em tempo real se feche e seus valores HLC sejam fixados. Eles s√£o _fluidos_. Isso leva a um script √†s vezes funcionando de maneira diferente em dados hist√≥ricos e em tempo real, onde apenas o pre√ßo de [abertura](https://br.tradingview.com/pine-script-reference/v5/#var_open) n√£o mudar√° durante a barra.

Qualquer script que use valores como [m√°xima](https://br.tradingview.com/pine-script-reference/v5/#var_high), [m√≠nima](https://br.tradingview.com/pine-script-reference/v5/#var_low) e [fechamento](https://br.tradingview.com/pine-script-reference/v5/#var_close) em tempo real est√° sujeito a produzir c√°lculos que podem n√£o ser repet√≠veis em barras hist√≥ricas ‚Äî portanto, repinta.

Veja este script simples. Ele detecta cruzamentos do valor de [fechamento](https://br.tradingview.com/pine-script-reference/v5/#var_close) (na barra em tempo real, isso corresponde ao pre√ßo atual do instrumento) sobre e sob uma [EMA](https://br.tradingview.com/support/solutions/43000592270):

![Valores de Dados Fluidos](./imgs/Repainting-01.B4bnEQz8_PQ677.webp)

```c
//@version=5
indicator("Repainting", "", true)
ma = ta.ema(close, 5)
xUp = ta.crossover(close, ma)
xDn = ta.crossunder(close, ma)
plot(ma, "MA", color.black, 2)
bgcolor(xUp ? color.new(color.lime, 80) : xDn ? color.new(color.fuchsia, 80) : na)
```

__Note que:__

- O script usa [bgcolor()](https://br.tradingview.com/pine-script-reference/v5/#fun_bgcolor) para colorir o fundo de verde quando [fechamento](https://br.tradingview.com/pine-script-reference/v5/#var_close) cruza acima da EMA, e vermelho em cruzamentos abaixo da EMA.
- A captura de tela mostra o script em tempo real em um gr√°fico de 30 segundos. Um cruzamento acima da EMA foi detectado, portanto, o fundo da barra em tempo real est√° verde.
- O problema aqui √© que nada garante que essa condi√ß√£o se manter√° at√© o final da barra em tempo real. A seta aponta para o temporizador que mostra que faltam 21 segundos para a barra em tempo real, e qualquer coisa pode acontecer at√© l√°.
- Testemunha-se um script com repintura.

Para evitar esse _repainting_, √© necess√°rio reescrever o script para que ele n√£o use valores que flutuam durante a barra em tempo real. Isso exigir√° o uso de valores de uma barra que j√° passou (tipicamente a barra anterior) ou o pre√ßo de [abertura](https://br.tradingview.com/pine-script-reference/v5/#var_open), que n√£o varia em tempo real.

Isso pode ser alcan√ßado de v√°rias maneiras. Este m√©todo adiciona uma condi√ß√£o `and barstate.isconfirmed` √†s detec√ß√µes de cruzamento, que exige que o script esteja executando na √∫ltima itera√ß√£o da barra, quando ela se fecha e os pre√ßos s√£o confirmados. √â uma maneira simples de evitar o repintura:

```c
//@version=5
indicator("Repainting", "", true)
ma = ta.ema(close, 5)
xUp = ta.crossover(close, ma) and barstate.isconfirmed
xDn = ta.crossunder(close, ma) and barstate.isconfirmed
plot(ma, "MA", color.black, 2)
bgcolor(xUp ? color.new(color.lime, 80) : xDn ? color.new(color.fuchsia, 80) : na)
```

Isso usa os cruzamentos detectados na barra anterior:

```c
//@version=5
indicator("Repainting", "", true)
ma = ta.ema(close, 5)
xUp = ta.crossover(close, ma)[1]
xDn = ta.crossunder(close, ma)[1]
plot(ma, "MA", color.black, 2)
bgcolor(xUp ? color.new(color.lime, 80) : xDn ? color.new(color.fuchsia, 80) : na)
```

Isso usa apenas valores de [fechamento](https://br.tradingview.com/pine-script-reference/v5/#var_close) e EMA confirmados para seus c√°lculos:

```c
//@version=5
indicator("Repainting", "", true)
ma = ta.ema(close[1], 5)
xUp = ta.crossover(close[1], ma)
xDn = ta.crossunder(close[1], ma)
plot(ma, "MA", color.black, 2)
bgcolor(xUp ? color.new(color.lime, 80) : xDn ? color.new(color.fuchsia, 80) : na)
```

Isso detecta cruzamentos entre a [abertura](https://br.tradingview.com/pine-script-reference/v5/#var_open) da barra em tempo real e o valor da EMA das barras anteriores. Note que a EMA √© calculada usando [fechamento](https://br.tradingview.com/pine-script-reference/v5/#var_close), ent√£o ela faz repintura. √â necess√°rio garantir o uso de um valor confirmado para detectar cruzamentos, portanto, `ma[1]` na l√≥gica de detec√ß√£o de cruzamento:

```c
//@version=5
indicator("Repainting", "", true)
ma = ta.ema(close, 5)
xUp = ta.crossover(open, ma[1])
xDn = ta.crossunder(open, ma[1])
plot(ma, "MA", color.black, 2)
bgcolor(xUp ? color.new(color.lime, 80) : xDn ? color.new(color.fuchsia, 80) : na)
```

__Todos esses m√©todos t√™m uma coisa em comum: enquanto evitam o _repainting_, eles tamb√©m acionar√£o sinais mais tarde do que scripts que fazem repintura. Este √© um compromisso inevit√°vel se se deseja evitar o _repainting_. Voc√™ n√£o pode ter o melhor dos dois mundos.__

### Repintura em Chamadas `request.security()`

A fun√ß√£o [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request.security) se comporta de maneira diferente em barras hist√≥ricas e em tempo real. Em barras hist√≥ricas, ela retorna apenas valores _confirmados_ de seu contexto solicitado, enquanto em barras de tempo real pode retornar valores _n√£o confirmados_. Quando o script reinicia sua execu√ß√£o, as barras que estavam em estado de tempo real se tornam barras hist√≥ricas e conter√£o apenas os valores confirmados nessas barras. Se os valores retornados por [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request.security) flutuarem em barras de tempo real sem confirma√ß√£o do contexto, o script os repintar√° quando reiniciar sua execu√ß√£o. Veja a se√ß√£o [Comportamento hist√≥rico e tempo real](./05_14_outros_timeframes_e_dados.md#comportamento-hist√≥rico-e-tempo-real) da p√°gina [Outros per√≠odos e dados](./05_14_outros_timeframes_e_dados.md) para uma explica√ß√£o detalhada.

Pode-se garantir que as solicita√ß√µes de dados de timeframes de tempo maiores retornem apenas valores confirmados em todas as barras, independentemente do estado da barra, deslocando o argumento `expression` em pelo menos uma barra com o operador de refer√™ncia hist√≥rica [[]](https://br.tradingview.com/pine-script-reference/v5/#op_%5B%5D) e usando [barmerge.lookahead_on](https://br.tradingview.com/pine-script-reference/v5/#var_barmerge.lookahead_on) para o argumento `lookahead` na chamada [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request.security), conforme explicado [aqui](./05_14_outros_timeframes_e_dados.md#dados-higher-timeframe-htf-timeframe-superior).

O script abaixo demonstra a diferen√ßa entre solicita√ß√µes de dados HTF que repintam e que n√£o repintam. Ele cont√©m duas chamadas [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request.security). A primeira chamada da fun√ß√£o solicita dados de [fechamento](https://br.tradingview.com/pine-script-reference/v5/#var_close) do `higherTimeframe` sem especifica√ß√£o adicional, e a segunda chamada solicita a mesma s√©rie com um deslocamento e [barmerge.lookahead_on](https://br.tradingview.com/pine-script-reference/v5/#var_barmerge.lookahead_on).

Como se v√™ em todas as barras [realtime](https://br.tradingview.com/pine-script-reference/v5/#var_barstate.isrealtime) (aquelas com fundo laranja), o `repaintingClose` cont√©m valores que flutuam sem confirma√ß√£o do `higherTimeframe`, o que significa que ele _repintar√°_ quando o script reiniciar sua execu√ß√£o. O `nonRepaintingClose`, por outro lado, se comporta da mesma forma em barras em tempo real e hist√≥ricas, ou seja, s√≥ muda seu valor quando novos dados confirmados est√£o dispon√≠veis:

![Repintura em chamadas request.security()](./imgs/Repainting-Repainting-request-security-calls-1.DKoD6okt_2uKSkR.webp)

```c
//@version=5
indicator("Repainting vs non-repainting `request.security()` demo", overlay = true)

//@variable The timeframe to request data from.
string higherTimeframe = input.timeframe("30", "Timeframe")

if timeframe.in_seconds() > timeframe.in_seconds(higherTimeframe)
    runtime.error("The 'Timeframe' input is smaller than the chart's timeframe. Choose a higher timeframe.")

//@variable The current `close` requested from the `higherTimeframe`. Fluctuates without confirmation on realtime bars.
float repaintingClose = request.security(syminfo.tickerid, higherTimeframe, close)
//@variable The last confirmed `close` requested from the `higherTimeframe`. 
// Behaves the same on historical and realtime bars.
float nonRepaintingClose = request.security(
     syminfo.tickerid, higherTimeframe, close[1], lookahead = barmerge.lookahead_on
 )

// Plot the values.
plot(repaintingClose, "Repainting close", color.new(color.purple, 50), 8)
plot(nonRepaintingClose, "Non-repainting close", color.teal, 3)
// Plot a shape when a new `higherTimeframe` starts.
plotshape(timeframe.change(higherTimeframe), "Timeframe change marker", shape.square, location.top, size = size.small)
// Color the background on realtime bars.
bgcolor(barstate.isrealtime ? color.new(color.orange, 60) : na, title = "Realtime bar highlight")
```

__Note que:__

- Foi usada a fun√ß√£o [plotshape()](https://br.tradingview.com/pine-script-reference/v5/#fun_plotshape) para marcar o gr√°fico quando h√° uma [mudan√ßa](https://br.tradingview.com/pine-script-reference/v5/#fun_timeframe.change) no `higherTimeframe`.
- Este script produz um [erro de execu√ß√£o](https://br.tradingview.com/pine-script-reference/v5/#fun_runtime.error) se o `higherTimeframe` for menor que o per√≠odo de tempo do gr√°fico.
- Em barras hist√≥ricas, o `repaintingClose` tem um novo valor no _final_ de cada per√≠odo de tempo, e o `nonRepaintingClose` tem um novo valor no _in√≠cio_ de cada per√≠odo de tempo.

Para facilitar a reutiliza√ß√£o, abaixo est√° uma fun√ß√£o `noRepaintSecurity()` simples que pode ser aplicada em seus scripts para solicitar valores de per√≠odos maiores que n√£o repintam:

```c
//@function Requests non-repainting `expression` values from the context of the `symbol` and `timeframe`.
noRepaintSecurity(symbol, timeframe, expression) =>
    request.security(symbol, timeframe, expression[1], lookahead = barmerge.lookahead_on)
```

__Note que:__

- O deslocamento `[1]` na s√©rie e o uso de `lookahead = barmerge.lookahead_on` s√£o interdependentes. Um __n√£o__ pode ser removido sem comprometer a integridade da fun√ß√£o.
- Ao contr√°rio de uma chamada simples de [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request.security), essa fun√ß√£o wrapper n√£o pode aceitar argumentos de express√£o de tupla. Para casos de uso com m√∫ltiplos elementos, pode-se passar um [tipo definido pelo usu√°rio](./04_09_tipagem_do_sistema.md#tipos-definidos-pelo-usu√°rio) cujos campos contenham os elementos desejados para solicita√ß√£o.

### Usando `request.security()` em Per√≠odos Menores

Alguns scripts usam [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request%7Bdot%7Dsecurity) para solicitar dados de um per√≠odo __menor__ que o per√≠odo do gr√°fico. Isso pode ser √∫til quando fun√ß√µes especificamente projetadas para lidar com intrabarras em per√≠odos menores s√£o enviadas para baixo no per√≠odo de tempo. Quando esse tipo de fun√ß√£o definida pelo usu√°rio requer a detec√ß√£o da primeira barra das intrabarras, como a maioria faz, a t√©cnica s√≥ funcionar√° em barras hist√≥ricas. Isso se deve ao fato de que as intrabarras em tempo real ainda n√£o est√£o ordenadas. O impacto disso √© que esses scripts n√£o podem reproduzir em tempo real seu comportamento em barras hist√≥ricas. Qualquer l√≥gica que gere alertas, por exemplo, ser√° falha, e uma atualiza√ß√£o constante ser√° necess√°ria para recalcular as barras de tempo real passadas como barras hist√≥ricas.

Quando usados em per√≠odos menores que o gr√°fico sem fun√ß√µes especializadas capazes de distinguir entre intrabarras, [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request%7Bdot%7Dsecurity) retornar√° apenas o valor da __√∫ltima__ intrabar na dilata√ß√£o da barra do gr√°fico, o que geralmente n√£o √© √∫til e tamb√©m n√£o se reproduzir√° em tempo real, levando ao repintura.

Por todas essas raz√µes, a menos que entenda as sutilezas de usar [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request%7Bdot%7Dsecurity) em per√≠odos menores que o do gr√°fico, √© melhor evitar usar a fun√ß√£o nesses per√≠odos. Scripts de maior qualidade ter√£o l√≥gica para detectar tais anomalias e impedir a exibi√ß√£o de resultados que seriam inv√°lidos quando um per√≠odo menor √© usado.

Para solicita√ß√µes de dados de per√≠odos menores mais confi√°veis, use [request.security_lower_tf()](https://br.tradingview.com/pine-script-reference/v5/#fun_request.security_lower_tf), conforme explicado [nesta](./05_14_outros_timeframes_e_dados.md#dados-lower-timeframe-ltf-timeframe-inferior) se√ß√£o da p√°gina [Outros per√≠odos e dados](./05_14_outros_timeframes_e_dados.md).

### Vazamento Futuro com `request.security()`

Quando [request.security()](https://br.tradingview.com/pine-script-reference/v5/#fun_request%7Bdot%7Dsecurity) √© usado com `lookahead = barmerge.lookahead_on` para buscar pre√ßos sem deslocar a s√©rie por `[1]`, ele retornar√° dados do futuro em barras hist√≥ricas, o que √© perigosamente enganoso.

Embora as barras hist√≥ricas exibam magicamente pre√ßos futuros antes que eles sejam conhecidos, nenhum lookahead √© poss√≠vel em tempo real porque o futuro l√° √© desconhecido, como deve ser, ent√£o n√£o existem barras futuras.

Este √© um exemplo:

![Vazamento futuro com request.security()](./imgs/Repainting-FutureLeakWithRequestSecurity-01.B8DsNHGV_Kgu8T.webp)

```c
// FUTURE LEAK! DO NOT USE!
//@version=5
indicator("Future leak", "", true)
futureHigh = request.security(syminfo.tickerid, "1D", high, lookahead = barmerge.lookahead_on)
plot(futureHigh)
```

Note como a linha de timeframe maior est√° mostrando o valor da [m√°xima](https://br.tradingview.com/pine-script-reference/v5/#var_high) do timeframe antes que ocorra. A solu√ß√£o para evitar esse efeito √© usar a fun√ß√£o conforme demonstrado [nesta se√ß√£o](./05_16_repintura.md#repintura-em-chamadas-requestsecurity).

Usar lookahead para produzir resultados enganosos n√£o √© permitido em publica√ß√µes de scripts, conforme explicado na se√ß√£o [lookahead](./05_14_outros_timeframes_e_dados.md#lookahead) da p√°gina [Outros timeframes e dados](./05_14_outros_timeframes_e_dados.md). Publica√ß√µes de scripts que usam essa t√©cnica enganosa __ser√£o moderadas__.

### `varip`

Scripts usando o modo de declara√ß√£o [varip](https://br.tradingview.com/pine-script-reference/v5/#kw_varip) para vari√°veis (veja a se√ß√£o sobre [varip](./04_06_declaracoes_de_variavel.md#varip-varip) para mais informa√ß√µes) salvam informa√ß√µes atrav√©s de atualiza√ß√µes em tempo real, que n√£o podem ser reproduzidas em barras hist√≥ricas onde apenas informa√ß√µes OHLC est√£o dispon√≠veis. Esses scripts podem ser √∫teis em tempo real, inclusive para gerar alertas, mas sua l√≥gica n√£o pode ser backtestada, nem suas plotagens em barras hist√≥ricas refletir√£o c√°lculos que ser√£o feitos em tempo real.

### Vari√°veis de Estado da Barra

Scripts usando [estados da barra](./05_05_estados_da_barra.md) podem ou n√£o repintar. Como visto na se√ß√£o anterior, usar [barstate.isconfirmed](https://br.tradingview.com/pine-script-reference/v5/#var_barstate%7Bdot%7Disconfirmed) √© na verdade uma maneira de __evitar__ repintura que __ser√°__ reproduzida em barras hist√≥ricas, que s√£o sempre "confirmadas". O uso de outros estados da barra, como [barstate.isnew](https://br.tradingview.com/pine-script-reference/v5/#var_barstate%7Bdot%7Disnew), no entanto, levar√° √† repintura. A raz√£o √© que em barras hist√≥ricas, [barstate.isnew](https://br.tradingview.com/pine-script-reference/v5/#var_barstate%7Bdot%7Disnew) √© `true` no [fechamento](https://br.tradingview.com/pine-script-reference/v5/#var_close) da barra, enquanto em tempo real, √© `true` na [abertura](https://br.tradingview.com/pine-script-reference/v5/#var_open) da barra. Usar outras vari√°veis de estado da barra geralmente causar√° algum tipo de discrep√¢ncia comportamental entre barras hist√≥ricas e em tempo real.

### `timenow`

A vari√°vel incorporada [timenow](https://br.tradingview.com/pine-script-reference/v5/#var_timenow) retorna a hora atual. Scripts usando essa vari√°vel n√£o podem mostrar comportamento consistente entre hist√≥rico e tempo real, ent√£o necessariamente repintam.

### Estrat√©gias

Estrat√©gias que usam `calc_on_every_tick = true` executam em cada atualiza√ß√£o em tempo real, enquanto estrat√©gias s√£o executadas no [fechamento](https://br.tradingview.com/pine-script-reference/v5/#var_close) de barras hist√≥ricas. Elas provavelmente n√£o gerar√£o as mesmas execu√ß√µes de ordens e, portanto, repintam. Note que quando isso acontece, tamb√©m invalida os resultados de backtesting, pois n√£o representam o comportamento da estrat√©gia em tempo real.

### Plotagem no Passado

Scripts que detectam piv√¥s ap√≥s 5 barras terem decorrido frequentemente voltam ao passado para plotar n√≠veis de piv√¥ ou valores no piv√¥ real, 5 barras no passado. Isso muitas vezes faz com que traders desavisados que olham para plotagens em barras hist√≥ricas inferirem que, quando o piv√¥ acontece em tempo real, as mesmas plotagens aparecer√£o no piv√¥ quando ocorrer, em vez de quando for detectado.

Veja um script que mostra o pre√ßo dos piv√¥s altos colocando o pre√ßo no passado, 5 barras ap√≥s o piv√¥ ter sido detectado:

```c
//@version=5
indicator("Plotting in the past", "", true)
pHi = ta.pivothigh(5, 5)
if not na(pHi)
    label.new(bar_index[5], na, str.tostring(pHi, format.mintick) + "\nü†á", yloc = yloc.abovebar, style = label.style_none, textcolor = color.black, size = size.normal)
```

![Plotagem no passado](./imgs/Repainting-PlottingInThePast-01.CdfEKXRh_VvvoS.webp)

__Note que:__

- Esse script repinta porque uma barra de tempo real que n√£o mostra pre√ßo pode ter um pre√ßo colocado nela se for identificada como um piv√¥, 5 barras ap√≥s o piv√¥ real ocorrer.
- A exibi√ß√£o parece √≥tima, mas pode ser enganosa.

A melhor solu√ß√£o para esse problema ao desenvolver scripts para outros √© plotar __sem__ um deslocamento por padr√£o, mas dar a op√ß√£o para os usu√°rios do script ativarem a plotagem no passado atrav√©s de entradas, para que eles estejam necessariamente cientes do que o script est√° fazendo, por exemplo:

```c
//@version=5
indicator("Plotting in the past", "", true)
plotInThePast = input(false, "Plot in the past")
pHi = ta.pivothigh(5, 5)
if not na(pHi)
    label.new(bar_index[plotInThePast ? 5 : 0], na, str.tostring(pHi, format.mintick) + "\nü†á", yloc = yloc.abovebar, style = label.style_none, textcolor = color.black, size = size.normal)
```

## Varia√ß√µes no Conjunto de Dados

### Pontos de Partida

Scripts come√ßam a ser executados na primeira barra hist√≥rica do gr√°fico e, em seguida, s√£o executados em cada barra sequencialmente, conforme explicado na p√°gina sobre o [modelo de execu√ß√£o do Pine Script](./04_01_modelo_de_execucao.md). Se a primeira barra mudar, o script muitas vezes n√£o calcular√° da mesma forma que fez quando o conjunto de dados come√ßou em um ponto diferente no tempo.

Os seguintes fatores t√™m impacto na quantidade de barras que voc√™ v√™ em seus gr√°ficos e em seu _ponto de partida_:

- O tipo de conta que voc√™ possui.
- Os dados hist√≥ricos dispon√≠veis pelo fornecedor de dados.
- Os requisitos de alinhamento do conjunto de dados, que determinam seu _ponto de partida_.

Estes s√£o os limites de barras espec√≠ficos da conta:

- 20000 barras hist√≥ricas para o plano Premium.
- 10000 barras hist√≥ricas para os planos Pro e Pro+.
- 5000 barras hist√≥ricas para outros planos.

Os pontos de partida s√£o determinados usando as seguintes regras, que dependem do timeframe do gr√°fico:

- __1, 5, 10, 15, 30 segundos__: alinha ao in√≠cio de um dia.
- __1 - 14 minutos__: alinha ao in√≠cio de uma semana.
- __15 - 29 minutos__: alinha ao in√≠cio de um m√™s.
- __30 - 1439 minutos__: alinha ao in√≠cio de um ano.
- __1440 minutos e mais__: alinha ao primeiro ponto de dados hist√≥ricos dispon√≠vel.

Com o passar do tempo, esses fatores fazem com que a hist√≥ria do seu gr√°fico comece em pontos diferentes no tempo. Isso muitas vezes impacta os c√°lculos de seus scripts, pois mudan√ßas nos resultados dos c√°lculos nas barras iniciais podem se propagar por todas as outras barras no conjunto de dados. Usar fun√ß√µes como [ta.valuewhen()](https://br.tradingview.com/pine-script-reference/v5/#fun_ta%7Bdot%7Dvaluewhen), [ta.barssince()](https://br.tradingview.com/pine-script-reference/v5/#fun_ta%7Bdot%7Dbarssince) ou [ta.ema()](https://br.tradingview.com/pine-script-reference/v5/#fun_ta%7Bdot%7Dema), por exemplo, produzir√° resultados que variam com a hist√≥ria inicial.

### Revis√£o de Dados Hist√≥ricos

Barras hist√≥ricas e em tempo real s√£o constru√≠das usando dois feeds de dados diferentes fornecidos por bolsas/corretoras: dados hist√≥ricos e dados em tempo real. Quando as barras em tempo real elapsam, as bolsas/corretoras √†s vezes fazem ajustes pequenos nos pre√ßos das barras, que s√£o ent√£o escritos em seus dados hist√≥ricos. Quando o gr√°fico √© atualizado ou o script √© reexecutado nessas barras de tempo real elapsadas, elas ser√£o ent√£o constru√≠das e calculadas usando os dados hist√≥ricos, que conter√£o essas pequenas revis√µes de pre√ßo, se houver.

Os dados hist√≥ricos tamb√©m podem ser revisados por outros motivos, por exemplo, para desdobramentos de a√ß√µes.
