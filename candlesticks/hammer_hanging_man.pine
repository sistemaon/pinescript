// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© AugustoErni

//@version=5
indicator("Hammer and Hanging Man (Candlestick Pattern)", overlay=true)

import AugustoErni/uptrend_downtrend_loopback_candle_identification_lib/3 as upAndDownTrendsCandleIdentification

hammerHangingManBodySizeThreshold = input.float(defval=0.50, title="Hammer/Hanging Man body size threshold in percentage (0.50 = 50%)", step=0.01)
hammerHangingManLowerShadowMultiplier = input.float(defval=1.8, title="Hammer/Hanging Man lower shadow multiplier", step=0.1)
hammerHangingManUpperShadowThreshold = input.float(defval=0.10, title="Hammer/Hanging Man upper shadow threshold in percentage (0.10 = 10 %)", step=0.01)
lookbackPeriod = input.int(defval=3, title="Lookback Period", minval=1, step=1, tooltip="Loopback period to identify uptrend/downtrend analysis from candles.")
showHammer = input.bool(defval=true, title="Show Hammer", tooltip="Show or hide the Hammer pattern.")
showHangingMan = input.bool(defval=true, title="Show Hanging Man", tooltip="Show or hide the Hanging Man pattern.")

isBullishCandle = close > open
isBearishCandle = open > close
realBody = math.abs(close - open)
upperShadow = high - math.max(close, open)
lowerShadow = math.min(close, open) - low

isSmallRealBody = realBody <= hammerHangingManBodySizeThreshold * (high - low)
isLongLowerShadow = lowerShadow >= hammerHangingManLowerShadowMultiplier * realBody
isShortUpperShadow = upperShadow <= hammerHangingManUpperShadowThreshold * (high - low)

isLookbackDowntrendCandle = upAndDownTrendsCandleIdentification.downtrendLoopbackCandleIdentification(lookbackPeriod)
isLookbackUptrendCandle = upAndDownTrendsCandleIdentification.uptrendLoopbackCandleIdentification(lookbackPeriod)

isHammer = isBullishCandle and isSmallRealBody and isLongLowerShadow and isShortUpperShadow and isLookbackDowntrendCandle
isHangingMan = isBearishCandle and isSmallRealBody and isLongLowerShadow and isShortUpperShadow and isLookbackUptrendCandle

barcolor(isHammer ? color.white : isHangingMan ? color.black : na)

plotshape(isHammer and showHammer, title="Hammer", style=shape.arrowup, location=location.belowbar, color=color.rgb(255, 255, 255), text="Hammer", textcolor=color.green)
plotshape(isHangingMan and showHangingMan, title="Hanging Man", style=shape.arrowdown, location=location.abovebar, color=color.rgb(0, 0, 0), text="Hanging Man", textcolor=color.red)