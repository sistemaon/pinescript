// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© AugustoErni

//@version=5
indicator("Shooting Star and Inverted Hammer Pattern (Candlestick Pattern)", overlay=true)

import AugustoErni/uptrend_downtrend_loopback_candle_identification_lib/3 as upAndDownTrendsCandleIdentification

showShootingStar = input.bool(defval=true, title="Show Shooting Star", tooltip="Show or hide the Shooting Star pattern.")
showInvertedHammer = input.bool(defval=true, title="Show Inverted Hammer", tooltip="Show or hide the Inverted Hammer pattern.")
lookbackPeriod = input.int(defval=5, title="Lookback Period", minval=1, step=1, tooltip="Loopback period to identify uptrend/downtrend analysis from candles.")
weakCandleThreshold = input.float(0.45, title="Weak Candle Threshold (0.45 = 45%)", step=0.01)
upperShadowThreshold = input.float(1, title="Upper Shadow Threshold (1 = 1 times the body length)")
noLowerShadowThreshold = input.float(0.05, title="No/Lower Shadow Threshold (0.05 = 5%)", step=0.01)

isBullishCandle = close > open
isBearishCandle = open > close

isLookbackDowntrendCandle = upAndDownTrendsCandleIdentification.downtrendLoopbackCandleIdentification(lookbackPeriod)
isLookbackUptrendCandle = upAndDownTrendsCandleIdentification.uptrendLoopbackCandleIdentification(lookbackPeriod)

isWeakCandle = math.abs(close - open) <= (high - low) * weakCandleThreshold
isLongUpperShadow = (high - math.max(open, close)) >= upperShadowThreshold * math.abs(open - close)
isNoLowerShadow = (math.min(open, close) - low) <= (high - low) * noLowerShadowThreshold

isShootingStar = isWeakCandle and isLongUpperShadow and isNoLowerShadow and isLookbackUptrendCandle

isInvertedHammer = isWeakCandle and isLongUpperShadow and isNoLowerShadow and isLookbackDowntrendCandle

barcolor(isShootingStar and isBullishCandle ? color.white : isShootingStar and isBearishCandle ? color.black : na)
barcolor(isInvertedHammer and isBullishCandle ? color.white : isInvertedHammer and isBearishCandle ? color.black : na)

plotshape(isShootingStar and showShootingStar, title="Shooting Star", style=shape.arrowdown, location=location.abovebar, color=color.rgb(0, 0, 0), text="Shooting Star", textcolor=color.red)
plotshape(isInvertedHammer and showInvertedHammer, title="Inverted Hammer", style=shape.arrowup, location=location.belowbar, color=color.rgb(255, 255, 255), text="Inverted Hammer", textcolor=color.green)
